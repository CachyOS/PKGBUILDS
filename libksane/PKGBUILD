# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksane
pkgver=19.12.0
pkgrel=1.2
pkgdesc="An image scanning library"
url="https://www.kde.org/applications/graphics/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(ktextwidgets kwallet sane)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-414600.patch::"https://cgit.kde.org/libksane.git/patch/?id=ee889af437946da2aa6c4d00ca52bd739d328841"
kdebug-414601_and_414602.patch::"https://cgit.kde.org/libksane.git/patch/?id=992fd7c77d75c6267b615a3e9e982349639b9e51")
sha256sums=('5fdb8903ad4e711a13273002f9818d46e8c64add840f4fd232aad4616a5cdd6c'
            'SKIP'
            '5cef1e489b97f3bcc6d3a6cc1c808b61b6071d369316022e773f0cf4e820df9c'
            'f3f2296f950650d6eaca089e7dd5e107b3147aa882ea7e327508b34c123242a3')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-414600.patch
  patch -p1 -i ../kdebug-414601_and_414602.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
