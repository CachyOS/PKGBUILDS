# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksane
pkgver=20.08.1
pkgrel=1.2
pkgdesc="An image scanning library"
url="https://www.kde.org/applications/graphics/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(ktextwidgets kwallet sane)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-424010.patch::"https://invent.kde.org/graphics/libksane/commit/a997d11d72166922db14dd93b0cf83f402d3a5a8.patch"
kdebug-415749.patch::"https://invent.kde.org/graphics/libksane/commit/2659134fed260cbb85e708390a787fd8362105c9.patch")
sha256sums=('0d8717ad004a2480aea232d9a2317b4d1cc4678d53f0176bdaeb411eedd19dbe'
            'SKIP'
            'b8a6d5a2ebac71e277c7d98408c4f5741df5aa5daaefa6ff011afbf9e3da7ca1'
            'd9d0e5a51ab599a7cad37c7ed98401fe7d20377ef5fc6250652a914329c8a1b2')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Add kdebug 424010 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-424010.patch
    msg "Add kdebug 415749 patch; fix in 20.12.0"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-415749.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
