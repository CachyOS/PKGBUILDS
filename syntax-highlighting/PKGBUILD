# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=syntax-highlighting
pkgver=5.74.0
pkgrel=1.1
pkgdesc='Syntax highlighting engine for structured text and code'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(GPL2)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-xmlpatterns qt5-tools qt5-doc doxygen)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-426466.patch::"https://invent.kde.org/frameworks/syntax-highlighting/commit/498db9aaa888f994c282bda6de98e22b40c1cf7b.patch")
sha256sums=('a29fad2b869df141d0e68c4b61769c20759dc1b31be7a8570e2b717a18a08e9b'
            'SKIP'
            'be997892640dd2869c3db018b157a54731218498e322bf39b4b5c1890d04ec9c')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 426466 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-426466.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DQRC_SYNTAX=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
