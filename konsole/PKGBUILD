# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with bugfix from github
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=konsole
pkgver=17.12.2
pkgrel=1.2
arch=(x86_64)
url='https://kde.org/applications/system/konsole/'
pkgdesc="KDE's terminal emulator"
license=(GPL LGPL FDL)
groups=(kde-applications kdebase)
depends=(knotifyconfig kpty kparts kinit)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=('keditbookmarks: to manage bookmarks')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-384620.patch::"https://cgit.kde.org/konsole.git/patch/?id=b8a2f0cf"
kdebug-338997.patch::"https://cgit.kde.org/konsole.git/patch/?id=f98c752b"
kdebug-353382.patch::"https://cgit.kde.org/konsole.git/patch/?id=f4fbec0c"
kdebug-386762.patch::"https://cgit.kde.org/konsole.git/patch/?id=d25e5ac7"
kdebug-343071.patch::"https://cgit.kde.org/konsole.git/patch/?id=a7c3bda7"
kdebug-340281.patch::"https://cgit.kde.org/konsole.git/patch/?id=cfa2e015"
kdebug-372348.patch::"https://cgit.kde.org/konsole.git/patch/?id=b392dd49")
sha256sums=('ece6ea3c07074128bb2a601f49cade8e5d6d3cccc17c58a1052e34c0b1ab7ab5'
            'SKIP'
            'dfbc53016665dd9681addc5c110952c2430de5a92f2bf9bfd1c893f3b3720158'
            '7b3402aace31e30b55a9dc6711f711d3117ad166b1ec4941ffd21f2ce9887d23'
            '91ebbea3d84b3b3cd63bfc09b0fdab4acd90c7cb65fb48f1d26cded088d7d066'
            '3aa977bb8634a5864176c9579b1b06a9852306ae6515e30d04e5afdff4e8de43'
            '31cd4dfb497bff8815e6f8939496254705bd75d1f70c2a9f8a25c5fb5cc92646'
            '1745a48e45ba432d046fa5d469b4a41ea84b9027f32349ecd60d6257f2f1738a'
            '26dead0abe10e0213abda029f1630a815a7fb1fc44617239660ef943e66206b1')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
	msg "kdebug 384620 patch"
  patch -p1 -i ../kdebug-384620.patch # See: https://bugs.kde.org/show_bug.cgi?id=384620
	msg "kdebug 338997 patch"
  patch -p1 -i ../kdebug-338997.patch # See: https://bugs.kde.org/show_bug.cgi?id=338997
    msg "kdebug 353382 patch"
  patch -p1 -i ../kdebug-353382.patch # See: https://bugs.kde.org/show_bug.cgi?id=353382
    msg "kdebug 386762 patch"
  patch -p1 -i ../kdebug-386762.patch # See: https://bugs.kde.org/show_bug.cgi?id=386762 fixed in 18.04
    msg "kdebug 343071 patch"
  patch -p1 -i ../kdebug-343071.patch # See: https://bugs.kde.org/show_bug.cgi?id=343071
    msg "kdebug 340281 patch"
  patch -p1 -i ../kdebug-340281.patch # See: https://bugs.kde.org/show_bug.cgi?id=340281
    msg "kdebug 372348 patch"
  patch -p1 -i ../kdebug-372348.patch # See: https://bugs.kde.org/show_bug.cgi?id=372348
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
