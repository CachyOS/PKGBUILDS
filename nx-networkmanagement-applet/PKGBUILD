# Maintainer: pavbaranov <pavbaranov at gmail dot com>

pkgname=plasma-nx-networkmanagement-applet
pkgver=r22.fceb28e
pkgrel=1
pkgdesc="NX Desktop networkmanagement applet for Plasma"
arch=('any')
url="http://www.nx-desktop.org/"
license=('GPL3')
depends=(plasma-workspace)
makedepends=(git qt5-tools qt5-svg extra-cmake-modules)

source=("git+https://github.com/nx-desktop/nx-networkmanagement-applet.git")
sha256sums=('SKIP')

pkgver() {
  cd nx-networkmanagement-applet
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  mkdir build
  cd build
  cmake ../nx-networkmanagement-applet \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
