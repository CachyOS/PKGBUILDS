# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# made on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kapidox
pkgver=5.43.0
pkgrel=1.1
pkgdesc='Frameworks API Documentation Tools'
arch=('any')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('python' 'bash')
#depends=('python-jinja' 'python-yaml')
makedepends=('extra-cmake-modules' 'qt5-base')
groups=('kf5')
#source=("$pkgname-5.43.0.rc1::git://anongit.kde.org/kapidox.git#tag=v5.43.0-rc1")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
kdebug-390904.patch::"https://cgit.kde.org/kapidox.git/patch/?id=70a7f1fb95fbc99c8a0af89959703c7aab0af37f")
sha256sums=('812454d75a8d50ecad67f82a030945b27c448dbfe6f9be9311ef63e0e9456530'
            'SKIP'
            '7844836a3b2302b6a7d087a9094d73c4bb22ba0c9b703040e709950cfd61cd4f')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
	msg "Add KDEBUG 390904 patch" # See: https://bugs.kde.org/show_bug.cgi?id=390904
	patch -p1 -i ../kdebug-390904.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
