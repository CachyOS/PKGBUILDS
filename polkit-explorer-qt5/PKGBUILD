# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# based on polkit-explorer PKGBUILD by:
# Maintainer: Manuel HÃ¼sers <manuel.huesers@uni-ol.de>
# Contributor: Alad Wenter <https://wiki.archlinux.org/index.php/Special:EmailUser/Alad>

pkgname=polkit-explorer-qt5-git
pkgver=11.51ba887
pkgrel=1
pkgdesc='Present PolicyKit information in a human-readable form - Qt5 version.'
arch=('any')
url="https://github.com/maz-1/Polkit-Explorer"
license=('ISC')
depends=('python2-pyqt5' 'python2-lxml')
conflicts=(polkit-explorer)
source=("git+https://github.com/maz-1/Polkit-Explorer.git")
sha256sums=('SKIP')

pkgver() {
	cd "Polkit-Explorer"
	printf '%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd "${srcdir}/Polkit-Explorer"
	local pydir='usr/lib/python2.7/site-packages/polkitex'

	install -d -m755 "${pkgdir}"/{usr/bin,$pydir}
	install -m644 Ui_*.py "${pkgdir}/${pydir}/"

	install -m755 'polkitex.py' "${pkgdir}/${pydir}/"
	ln -s "/${pydir}/polkitex.py" "${pkgdir}/usr/bin/polkitex"
  
	install -Dm644 'LICENSE' "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 "polkitex.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
