# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo
pkgver=5.69.0
pkgrel=1.1
pkgdesc="A framework for searching and managing metadata"
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kfilemetadata kidletime kio lmdb)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc qt5-declarative)
optdepends=('qt5-declarative: QML bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
        'D28400.patch' 'D28401.patch')
sha256sums=('4a996feade929cb6913382e5993f20972a5dc066fbfead13a02f9798ea80c85b'
            'SKIP'
            '4e54bbf5bcea57f87fcfb4a0cce07ab2a102e9d5764d124c6e2895f06661c306'
            'd27e57ce183b3681347ac2bc499c2e378e4e4f0c987512359683bf09bb992854')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Applying D28400 patch; fix in 5.70"
  patch -p1 -i ../D28400.patch
  msg "Applying D28401 patch; fix in 5.70"
  patch -p1 -i ../D28401.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
