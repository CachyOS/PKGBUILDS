# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcoming releases
# made upon the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo
pkgver=5.67.0
pkgrel=2.1
pkgdesc="A framework for searching and managing metadata"
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kfilemetadata kidletime kio lmdb)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc qt5-declarative)
optdepends=('qt5-declarative: QML bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-417127.patch::"https://cgit.kde.org/baloo.git/patch/?id=a0f8e748"
         D27326.patch::"https://cgit.kde.org/baloo.git/patch/?id=bae7a9dd2361761a7142aa15043cfc9997cc5f6d")
sha256sums=('81cf19756572f1d70da3c8788658a8a0d74bff1ad0685164084302231c67be69'
            'SKIP'
            '7647a237636b89564f8af0d0726adfe91c1bb5978e369b950171e42341bc7d68'
            'c3db3662b84a7b98bb141bd95199940b6ce5f0cbdc54c3c2148bdf2169b0c828')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-417127.patch # Fix saving settings changes
  msg "Applying D27326 patch; fix in 5.68"
  patch -p1 -i ../D27326.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
