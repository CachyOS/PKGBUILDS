# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes and improvements from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo
pkgver=5.79.0
pkgrel=1.2
pkgdesc="A framework for searching and managing metadata"
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kfilemetadata kidletime kio lmdb)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc qt5-declarative)
optdepends=('qt5-declarative: QML bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-433095.patch::"https://invent.kde.org/frameworks/baloo/commit/95f3290e43bdf1124122320eacd510aa8803020c.patch"
commit-55d8e32a.patch::"https://invent.kde.org/frameworks/baloo/-/commit/55d8e32a58aa72ece8ab856159b0f093e483cf66.patch")
sha256sums=('80f9aca313fc214b6363ae37546df7825eafdb291bdc6f597d86cb0b8c38ff50'
            'SKIP'
            '4ce284e2faae8845b061e82f06db887d9fa501fcb0d50d90c4f6225195d41208'
            '84e5700d6893e0658e361140c4a0fc000576e1b1fe9d82c27fdcd97c2b4ed5de')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 433095 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433095.patch
    msg "Don't try to index ninja files"
    patch -d $pkgname-$pkgver -p1 -i ../commit-55d8e32a.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
