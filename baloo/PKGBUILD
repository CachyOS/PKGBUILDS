# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo
pkgver=5.51.0
pkgrel=1.1
pkgdesc="A framework for searching and managing metadata"
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kfilemetadata kidletime kio lmdb)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-declarative)
optdepends=('qt5-declarative: QML bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-397242.patch::"https://cgit.kde.org/baloo.git/patch/?id=7e5c005e6a3b563013e2ba8cb9c8f1b282e6f7b2"
D15939.diff::"https://phabricator.kde.org/file/data/bnh4rt3mfaqdalvnqccp/PHID-FILE-thlnxerxa7gffjcwsblv/D15939.diff"
kdebug-364858.patch::"https://cgit.kde.org/baloo.git/patch/?id=a9696978322c08d19ece0a67f430aee391e3918d")
sha256sums=('78ca6f8180e9288fad5fb4ff380f48e71c4723fbfffe0189fc8ae614256320f8'
            'SKIP'
            '5f11d32ce8e50e25223c61c0ec6ec9b331969e1c0f6a43daaec307654aa05231'
            'a92dbc37dd6a71a13192efb6ccf6967b2f6aa1b40d2100a2eb46da6a159a8a5f'
            'efa01f7b6291f2fb26dfbf5976262dcb55cf7059e3096403f4eb5b7fe9191871')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  #Fixed some bugs, all in 5.52
  msg "Add kdebug 397242"
  patch -p1 -i ../kdebug-397242.patch
  msg "Add D15939"
  patch -p1 -i ../D15939.diff
  #msg "Add kdebug 364858"
  #patch -p1 -i ../kdebug-364858.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make 
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
