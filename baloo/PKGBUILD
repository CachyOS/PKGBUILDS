# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo
pkgver=5.44.0
pkgrel=1.1
pkgdesc="A framework for searching and managing metadata"
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kfilemetadata' 'kidletime' 'kio' 'lmdb')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'doxygen' 'qt5-tools')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
D11279.patch::"https://cgit.kde.org/baloo.git/patch/?id=76c207a141423102ab0c5fba43fdafe3e205cca5")
sha256sums=('3eae55b5fa31f695da4145f8505637f6d8d5f77a0472d6f23fd367690ba07d79'
            'SKIP'
            '7538ca9934d68e206cb5496b4a3d0300318ef05b520953cc39eb8eedf50ce1d8')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

	cd $pkgname-$pkgver
	msg "Add D11279 patch" # See: https://phabricator.kde.org/D11279
	patch -p1 -i ../D11279.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
