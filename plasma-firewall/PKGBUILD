# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-firewall
pkgver=5.21.3
pkgrel=1.1
pkgdesc="Control Panel for your system firewall"
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(GPL LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-434415.patch::"https://invent.kde.org/plasma/plasma-firewall/commit/c963f227c5cb6dec7f53735d730e2d0a8d36abf8.patch")
depends=(kcmutils plasma-framework)
makedepends=(extra-cmake-modules)
optdepends=('iproute2: netstat backend'
            'firewalld: firewalld backend'
            'ufw: ufw backend')
groups=(plasma)
sha256sums=('7ca5bda3bdf7dab10726976aca5d838e651dcb0f30deb540623546fea762f52f'
            'SKIP'
            'eacc7360a34e647fa9f526d071f58843728aa6d611b917d76cd3c7937ef49dff')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 434415 patch; fix in 5.21.4"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-434415.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DLIBEXEC_INSTALL_DIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
