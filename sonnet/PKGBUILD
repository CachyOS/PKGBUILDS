# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with fixes from upcomming releases
# made on original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=sonnet
pkgver=5.53.0
pkgrel=1.1
pkgdesc='Spelling framework for Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools hunspell aspell hspell libvoikko doxygen)
optdepends=('hunspell: spell checking via hunspell' 'aspell: spell checking via aspell' 
            'hspell: spell checking for Hebrew' 'libvoikko: Finnish support via Voikko')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-302689.patch::"https://cgit.kde.org/sonnet.git/patch/?id=454d4d8be9dc4612c4dba5845b549b2396bdcff2")
sha256sums=('286c4c377ab38f3c14ff807f7a471f803c691b1fb624cecee8e7c9df349b684a'
            'SKIP'
            'eb1270dc2c27e0e4898904158d1f7d9333d33c961df7dcafbca4ee4fade2a4ce')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 302689 patch"
  patch -p1 -i ../kdebug-302689.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
