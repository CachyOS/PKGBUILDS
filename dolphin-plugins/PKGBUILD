# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dolphin-plugins
pkgver=20.08.3
pkgrel=1.1
pkgdesc="Extra Dolphin plugins"
arch=(x86_64)
url="https://www.kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(dolphin)
makedepends=(extra-cmake-modules ktexteditor)
optdepends=('ktexteditor: Mercurial plugin')
groups=(kde-applications kdesdk)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-424413.patch::"https://invent.kde.org/sdk/dolphin-plugins/commit/2714be04b49d4340c5f23746672eb6a12711f9f6.patch")
sha256sums=('0cb1b232c9b86811107ef9270ba2be42d242157b8daeeeffd1493ae635f0aa3a'
            'SKIP'
            '410684b09229ab94f5684a62ce37e5fc5d5655f11637c1e151891b8d1e89449e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Add kdebug 424413 patch; fix in beta 20.12"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-424413.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
