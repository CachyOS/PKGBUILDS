# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kiconthemes
pkgver=5.51.0
pkgrel=2.1
pkgdesc='Support for icon themes'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-svg kconfigwidgets kitemviews karchive)
makedepends=(extra-cmake-modules doxygen qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-396990.patch::"https://cgit.kde.org/kiconthemes.git/patch/?id=2699d36365fabb7be56a5d6d8a3ef3867eac7605"
kiconthemes-crash.patch::""https://cgit.kde.org/kiconthemes.git/patch/?id=e0006451"")
sha256sums=('89813a2a3ecb2ddd061db6a040dcf4677c10e41cbcce49cad5c926a8f1ce5c8f'
            'SKIP'
            '91e08998f812f84e082f904dfa9acee3ab5fd45c0c803a3a13ad870ff168138d'
            '8c86d73139a7be1f9b8a95883bab0b31c4d5c30b0541e3f14cca08c2ad857512')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

	cd $pkgname-$pkgver
	msg "Add kdebug 396990 patch" #Probably fix in 5.52
	patch -p1 -i ../kdebug-396990.patch
	msg "Add kiconthemes-crash patch"
	patch -p1 -i ../kiconthemes-crash.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
