# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# especialy for CachyOS and only for version with bugfixes and improves from upcomming version
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=(plasma-workspace plasma-wayland-session)
pkgver=5.25.2
pkgrel=2.3
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(knotifyconfig ksystemstats ktexteditor libqalculate kde-cli-tools appstream-qt
         xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-xmessage milou prison kwin
         plasma-integration kpeople kactivities-stats libkscreen kquickcharts kuserfeedback
         accountsservice kio-extras kio-fuse qt5-tools oxygen-sounds)
makedepends=(extra-cmake-modules kdoctools gpsd baloo networkmanager-qt plasma-wayland-protocols kunitconversion kinit appmenu-gtk-module networkmanager-qt kdepim-addons gpsd)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgbase-$pkgver.tar.xz{,.sig} kde.pam
#patches:
kdebug-455227.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/298ffbc22253bb96dc2b08e482856c6a01d250d6.patch" #fix in 5.25.3
kdebug-431603.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/0e3b24c95428a4f70867d33bf5536b0720339f94.patch" #fix in 5.26
kdebug-449132.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/520e30a64a4e7d13c56fb8cd1df27698cae234ea.patch" #fix in 5.25.3
#kdebug-455894.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/a8d8e8ed52e3cae586d02b9d67cc3d3bf35a97fb.patch" #fix in 5.26
kdebug-391609.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/9bad434fbf94b1398056fafd8caf49a371ea6140.patch" #fix in 5.26
kdebug-192019.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/134e2d5c989c36ac0e985ee0ae382996c6b7b56e.patch" #fix in 5.26; it's also bugfix for: BUG: 341235, BUG: 344588, BUG: 394477, BUG: 397974, BUG: 397975, BUG: 403580, BUG: 417564, BUG: 420268, BUG: 429474, BUG: 431292, BUG: 444772, BUG: 446785, BUG: 447787, BUG: 448324, BUG: 448355, BUG: 451919, BUG: 451944, BUG: 454991
kdebug-455395.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/9f46232368583ef97d617782ee29f9eabd20eda8.patch" #fix in 5.25.3 and also fix kdebug 454047
kdebug-456275.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/0093ea7db5ad53f2b775c6d1b0a0b59c2bb1b564.patch" #fix in 5.25.3 probably
)
sha256sums=('dce4612e9c30f84ac61b646b31691fee426f9a054ae36e08f43d26bf2a5c9099'
            'SKIP'
            '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
            '1aacddd82b5eee9cb8ca771737a74e3eb907644ba82b7cafa38f4f2d3e5b745a'
            '43fe311657f76bad35d84d0ee5592d9a740c24aad767b5b14cb118ad261698b1'
            '3f51dc670da3b85459dbe1a28550d49754c77a53055136e7702698d4f36a63d1'
            'f17b0aaff89d3af2a67d737974c24095ae725e2f8e9d04d33aaf2eb60f822645'
            '581f4289655d91823d064dac2933be5523ded6ff0e694bb2739589209c8f11d7'
            'cb4d9b7b136cb6384de7642fe5934408435d9f9b10db9400406e86e7396f6e6c'
            'fcfde39de176e919e80448089ecf01871a3663ca57ca98bb8fa46471fe8233fa')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
options=(debug)

prepare() {
cd $pkgname-$pkgver
### Patching sources
        local src
        for src in "${source[@]}"; do
          src="${src%%::*}"
          src="${src##*/}"
          [[ $src = *.patch ]] || continue
          echo "Applying patch $src..."
          patch -Np1 < "../$src"
        done
}

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation'
              'kdepim-addons: displaying PIM events in the calendar'
              'appmenu-gtk-module: global menu support for GTK2 and some GTK3 applications'
              'baloo: Baloo search runner' 'discover: manage applications installation from the launcher')
  backup=('etc/pam.d/kde')

  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace qt5-wayland kwayland-integration xorg-xwayland)
  groups=()

  install -Dm644 build/login-sessions/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
