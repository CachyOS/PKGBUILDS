# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=(plasma-workspace plasma-wayland-session)
pkgver=5.23.3
pkgrel=1.1
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(knotifyconfig ksystemstats ktexteditor libqalculate kde-cli-tools appstream-qt
         xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-xmessage milou prison kwin
         plasma-integration kpeople kactivities-stats libkscreen kquickcharts kuserfeedback kio-extras kio-fuse)
makedepends=(extra-cmake-modules kdoctools gpsd baloo networkmanager-qt plasma-wayland-protocols kunitconversion kinit)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgbase-$pkgver.tar.xz{,.sig} kde.pam
    kdebug-444322.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/12e307399c5da49db97943f1c78eb0223d832a84.patch"
    # kdebug-338599.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/32a565bae7e2f7243d2715df56b8617dc8dd3612.patch"
    kdebug-427894.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/5169dff83384cf255ecfbd600036b80763f0885e.patch"
    # kdebug-420541.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/be7efa5ec2bcf2c87947ee2ff5e2b9c70eb2084e.patch"
    # kdebug-384950.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/590e0c0708febb9de4f88f4127e0f26fba6a9f70.patch"
    kdebug-399354.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/f2afc13d6b3adb439c0571695ecd1e68d63b6a5f.patch"
    # kdebug-433293.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/f377696bff39e35ae0f7ae6104d8732b3856744e.patch"
)
sha256sums=('86802e3305bd91faef25c1876e333ad22ae751e556e8503945481b52ad52e257'
            'SKIP'
            '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
            'd84c90ff64dfbd73a6be520d057ecef9da7faf45ab0ecec4b3d2c0f3bc675928'
            '8bba979818d5e255320f1e88ca197d250b6134a97bc19524ecb02ee600f630a8'
            'c79a24218ab04e9fe8c688331d5394550a2cf0a0e4a73ee3670ecdc40482cdcc')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
              
prepare() {
    msg "Add kdebug 444322 patch; fix in 5.24"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-444322.patch
    # msg "Add kdebug 338599 patch; fix in 5.24"
    # patch -d $pkgname-$pkgver -p1 -i ../kdebug-338599.patch
    msg "Add kdebug 427894 patch; fix in 5.24"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-427894.patch
    # msg "Add kdebug 420541 patch; fix in 5.24"
    # patch -d $pkgname-$pkgver -p1 -i ../kdebug-420541.patch
    # msg "Add kdebug 384950 patch; fix in 5.24"
    # patch -d $pkgname-$pkgver -p1 -i ../kdebug-384950.patch
    msg "Add kdebug 399354 patch; fix in 5.24"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-399354.patch
    # msg "Add kdebug 433293 patch; fix in 5.24"
    # patch -d $pkgname-$pkgver -p1 -i ../kdebug-433293.patch
}

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation'
              'kdepim-addons: displaying PIM events in the calendar'
              'appmenu-gtk-module: global menu support for GTK2 and some GTK3 applications'
              'baloo: Baloo search runner' 'discover: manage applications installation from the launcher')
  backup=('etc/pam.d/kde')

  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace qt5-wayland kwayland-integration xorg-xwayland)
  groups=()

  install -Dm644 build/login-sessions/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
