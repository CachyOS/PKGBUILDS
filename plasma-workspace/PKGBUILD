# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=(plasma-workspace plasma-wayland-session)
pkgver=5.21.1
pkgrel=1.3
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(knotifyconfig libksysguard ktexteditor libqalculate kde-cli-tools appstream-qt phonon-qt5
         xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-xmessage milou prison kwin
         plasma-integration kdelibs4support kpeople kactivities-stats libkscreen kquickcharts kuserfeedback)
makedepends=(extra-cmake-modules kdoctools gpsd baloo networkmanager-qt kdesignerplugin kinit plasma-wayland-protocols)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgbase-$pkgver.tar.xz"{,.sig} kde.pam
# Plasma 5.22 patches:
kdebug-431691.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/22e02fbbab3584932199adde24121ccceb66187b.patch"
kdebug-432857.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/51ca0cc28fd6d8d08844a3b27b86526ee0dcc71f.patch"
kdebug-384469.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/47f79ce60703d2ae36d97f97ba829d4c8f257d1f.patch"
kdebug-431398.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/97022f2f67c269cec2660ae4690e527cf452b322.patch"
commit-3fa447f5.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/3fa447f5648ff96985c288794c15df91484ec0f8.patch"
commit-906a423c.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/906a423ce1e448ddf3bd98092fc1dad3f65c9dc6.patch"
# commit-45c2a361.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/45c2a3613e55626f28fca738f304563f5577fc50.patch"
kdebug-433534.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/e8a7d8dac4655e2b5e75a250fb9e034246e0cb23.patch"
kdebug-408778.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/52b625081217dfe89713a918d892de1b30a517af.patch"
commit-67c96352.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/67c963523a2654a8eddbe010c52daf5c5f958c9f.patch"
#commit-2c2cd9ba.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/2c2cd9babf2470bbb7ad1aa8626b1508204cfa24.patch"
)
sha256sums=('4a30f9b5cb8f24be75f818a01c3fa8a3b70aff3c4552add6f88fcaa85109022b'
            'SKIP'
            '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
            '841fce6201550accdf03ab47189cb6dbc9428fddb4f467634e5a8bf44d5726fd'
            'ca69f693fc76e9f139aa4d0708c740d5f8cba1bde46f968241d840faacb060a5'
            '5b329cd8ec47c03593435e28dc3eb31081fb8516236f6039fc2737ae97922d39'
            '4b5be104e614c676486e12666fa3be82f1bb793d2b18543f6ec6850fb987e224'
            '7693722afbc2c2c3bf718dd9eb554985bcc35921074509ab3a592d8f887338d4'
            'b4bc5a08b30c0c7b17dd38589d2f50898a073fbe02e10aead486f681f0c067ee'
            'c4c97578deefbcb5160ee5f13c3e7abed24c87b092037ea50149bd6b4a3dcf01'
            '8afd50b76ad7c3e370fdf605570a964ddd50e326387633bcd1784c5fae6072df'
            'c7d16348c54a861273f69c90ca1cf294f4789b93bea40e1648712d9b85a24c36')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 431691 patch; fix in 5.22"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-431691.patch
    msg "Add kdebug 432857 patch; fix in 5.22"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-432857.patch
    msg "Add kdebug 384469 patch; fix in 5.22"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-384469.patch
    msg "Add kdebug 431398 patch; fix in 5.22"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-431398.patch
    msg "Add commit 3fa447f5 patch; fix in 5.22"
    patch -d $pkgbase-$pkgver -p1 -i ../commit-3fa447f5.patch
    msg "Add commit 906a423c patch; fix in 5.22"
    patch -d $pkgbase-$pkgver -p1 -i ../commit-906a423c.patch
    msg "Add kdebug 433534 patch; fix in 5.21.2"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-433534.patch
 #   msg "Add commit 45c2a361 patch; fix in 5.22"
 #  patch -d $pkgname-$pkgver -p1 -i ../commit-45c2a361.patch
    msg "Add kdebug 408778 patch; fix in 5.22"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-408778.patch
    msg "Add commit 67c96352 patch; fix in 5.22"
    patch -d $pkgbase-$pkgver -p1 -i ../commit-67c96352.patch
 #   msg "Add commit 2c2cd9ba patch; will be in 5.22"
 #   patch -d $pkgbase-$pkgver -p1 -i ../commit-2c2cd9ba.patch
}

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DPLASMA_SYSTEMD_BOOT=ON
  cmake --build build
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation'
              'kdepim-addons: displaying PIM events in the calendar'
              'appmenu-gtk-module: global menu support for GTK2 and some GTK3 applications'
              'qt5-virtualkeyboard: virtual keyboard support in lock screen'
              'baloo: Baloo search runner' 'discover: manage applications installation from the launcher')
  provides=(notification-daemon)
  backup=('etc/pam.d/kde')

  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace qt5-wayland kwayland-integration xorg-server-xwayland)
  groups=()

  install -Dm644 build/login-sessions/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
