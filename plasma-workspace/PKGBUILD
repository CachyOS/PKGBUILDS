# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# made upon the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=(plasma-workspace plasma-wayland-session)
pkgver=5.18.5
pkgrel=3.1
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(knotifyconfig libksysguard ktexteditor libqalculate kde-cli-tools appstream-qt
         xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-xmessage milou prison kwin
         plasma-integration kdelibs4support kpeople kactivities-stats libkscreen kquickcharts kuserfeedback
	 phonon-qt5)
makedepends=(extra-cmake-modules kdoctools gpsd baloo networkmanager-qt kdesignerplugin)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/${pkgver}/$pkgbase-$pkgver.tar.xz"{,.sig} kde.pam
kdebug-414885.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=c7c190488e46f49d85ab8695f32008eb6f352ad8"
kdebug-411973.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=db2518b063e493da5bf5f5a48c931ff51bd22924"
kdebug-416510.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=175424169d18766565a9b105b76136c4113515e1"
D28989.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=0e40a1cadf480d1c38700396367d0919db4b4247"
kdebug-420085.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=ed09a3baf3e00b6f831132ae55391548130f1d48"
plasma-animation-duration.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=29587025")
sha256sums=('14e82033be745f4db46a70d319e2c86012295ea31056092bc974004189b92354'
            'SKIP'
            '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
            'ac9a74a9a43942e874e1562ec8c8ab091c4d27fd84b23fcbefc160f85626ee48'
            '83a650587a28a85abdd79b843d3711e6982078120ce7d005e6bc63171821f416'
            '96999d6b97751da41f6b0bd309a9f4b1c99ea9d737a111898b53a50bea483277'
            '27ab47530d4f2c04bb6e9b45a84d9a8f3bc08e64ea73a7a3cd45e4f8134588a2'
            '24c09c164ab182c3e50a7a799db17259fa64ba9640f1fa43db6b126b23e85ead'
            'ced9380016948ee2ca10fa822b9c3d6359c417b3fc9c311389c51a3ec0e4fad4')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  sed -e 's|dbus-run-session ||' -i $pkgbase-$pkgver/login-sessions/plasmawayland.desktop.cmake # Don't start a second user dbus session

  cd $pkgname-$pkgver
  msg "Applying kdebug 414885 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-414885.patch
  msg "Applying kdebug 411973 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-411973.patch
  msg "Applying kdebug 416510 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-416510.patch
  msg "Applying D28989 patch"
  patch -p1 -i ../D28989.patch
  msg "Applying kdebug 420085 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-420085.patch
  patch -p1 -i ../plasma-animation-duration.patch # Fix slow animations with Frameworks 5.70
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation'
              'kdepim-addons: displaying PIM events in the calendar'
              'appmenu-gtk-module: global menu support for GTK2 and some GTK3 applications'
              'qt5-virtualkeyboard: virtual keyboard support in lock screen'
              'baloo: Baloo search runner' 'discover: manage applications installation from the launcher')
  provides=(notification-daemon)
  backup=('etc/pam.d/kde')

  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace qt5-wayland kwayland-integration xorg-server-xwayland)
  groups=()

  install -Dm644 build/login-sessions/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
