# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# made on the work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=(plasma-workspace kuiserver plasma-wayland-session)
pkgver=5.12.4
pkgrel=2.1
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kjsembed knotifyconfig libksysguard ktexteditor libqalculate qt5-tools kde-cli-tools bc
         xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-xmessage xorg-xprop milou prison kwin
         plasma-integration kdelibs4support)
makedepends=(extra-cmake-modules kdoctools gpsd baloo appstream-qt krunner networkmanager-qt kdesignerplugin)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgbase-$pkgver.tar.xz"{,.sig} kde.pam
ccbug-358231-342056.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=e1fe26f1fc78"
kdebug-375207.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=8227e4e2b7f0"
kdebug-390499.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=53ed39aaa477"
D10692.diff
kdebug-390280.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=d511a766779b"
kdebug-386068.patch::"https://cgit.kde.org/plasma-workspace.git/patch/?id=8c83cc687d4d"
D10461.diff
cgit-85ca8db.patch::'https://cgit.kde.org/plasma-workspace.git/patch/?id=85ca8dbb9f6fc2d632ce129ef1e5ea9899c594e2'
cgit-f2feef0.patch::'https://cgit.kde.org/plasma-workspace.git/patch/?id=f2feef047ab21a6163988a8c0ed64728148f7ccd'
cgit-d9c2f4e.patch::'https://cgit.kde.org/plasma-workspace.git/patch/?id=d9c2f4ecb9fb773821152cf15d66d5133ef38d1c'
kdebug-380347.patch::'https://cgit.kde.org/plasma-workspace.git/patch/?id=ddf452b6774f481ced47f9551e4a961622194646'
ccbug-391868.patch::'https://cgit.kde.org/plasma-workspace.git/patch/?id=c9ba8e0cf7e518de95952762dc56d930df98339f'
kdebug-390375.patch::'https://cgit.kde.org/plasma-workspace.git/patch/?id=896950a819ea76800007cad7393adacac7f9bf20'
Add_vastly_simplify_image_wallpaper_code.patch::'https://cgit.kde.org/plasma-workspace.git/patch/?id=f713a77e670aea28f560f0459ee78cf39f8e3c45'
kdebug-365317.patch::'https://cgit.kde.org/plasma-workspace.git/patch/?id=ec1931f7b2980a3cd445c09def1bb4a9b5ad607f'
kdebug-382264.patch::'https://cgit.kde.org/plasma-workspace.git/patch/?id=af2a74341d5f10c1dc1ba66538357c94d8e6a6e1'
)
sha256sums=('07d63fc73293d0c1244d67f018e8986a79a7c570b0d5fa92d11a3b20a189c202'
            'SKIP'
            '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
            '1acfd90910c110e0dccba413831b7493a0a08a9e75c857212c8741149d4a4c96'
            '08745bd6c66e51937dd7c28ce7846dca6fe9335b237af4e3b8f4bdeac0bc5beb'
            '2546c42475d9c62a5efbd6c709f52062194df233c098dbee1ce653f2de9376ce'
            'f3f56921ec3b801e9856f496af657b9f368a31675abb2da9bf684107832c1ce4'
            'dc2e4145df5f87a882229461a612e1d3a1af24bc6a173022ab932b5dde3fc7d6'
            'c3960d66fbe31466975e65cac5e47d6ffd48400914265ede5d0fe4d03f8ed251'
            '5e1ef2d39c46492d1ce6a1193656606436eeadffbcdeac6c79ffe19efc9b0919'
            '44c17eff52914d3236e5eaf49678214befc0796b6237e6dd37d330dbc04aafc1'
            '3230293683109b4c97b06db3626e9c6fdae18a3cbb3d936037783f4f83165382'
            'c885e635e7a224eedebe6e78e1c719e478ca27b80851ca2402c6d1ba060d107e'
            '6bf45d42fc01e86a0f1a6a1beaae570b325922bbe3317231d8a36f7e911d866e'
            '9a5166daae9a28b3cf2659ba1eec1be7ce95bfb9a6d47f09b72cc3f99a438a78'
            'd7b3e09594d2e09dec9e3dc7c7e3a468ec905afe8f0fb617334f6b677fdbe658'
            '5e83c0ec199f63aca0a524af3d481357c408a336d7ca14931b33a408fd894c4d'
            '868e1698ffc0e4f08b708b4f6c1fbe19e3e4fe04f2c99ca4fed29911c2689f1b'
            '37045591062916c9bf4e7903aaee4878cf2abeaa8484e0cc0059251ab88cf252')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add CCBUG 358231 & CCBUG 342056 patch" # See: https://cgit.kde.org/plasma-workspace.git/commit/?id=8227e4e2b7f034166cbf5efb01e4e715028b4606
  patch -p1 -i ../ccbug-358231-342056.patch
  msg "Add kdebug-375207 patch" # See: https://bugs.kde.org/show_bug.cgi?id=375207 Fixed in 5.13.0
  patch -p1 -i ../kdebug-375207.patch
  msg "Add KDEBUG 390499 patch" # See: https://bugs.kde.org/show_bug.cgi?id=390499
  patch -p1 -i ../kdebug-390499.patch
  msg "Add D10692 patch" # See: https://phabricator.kde.org/D10692
  patch -p1 -i ../D10692.diff
  msg "Add KDEBUG 390280 patch" # See: https://bugs.kde.org/show_bug.cgi?id=390280
  patch -p1 -i ../kdebug-390280.patch
  msg "Add KDEBUG 386068 patch" # See: https://bugs.kde.org/show_bug.cgi?id=386068 Fixed in 5.13.0
  patch -p1 -i ../kdebug-386068.patch
  msg "Add D10461 patch" # See: https://phabricator.kde.org/D10461 Fixed in 5.13.0
  patch -p1 -i ../D10461.diff
  msg "Add cgit-85ca8db patch" # See: https://cgit.kde.org/plasma-workspace.git/commit/?id=85ca8dbb9f6fc2d632ce129ef1e5ea9899c594e2
  patch -p1 -i ../cgit-85ca8db.patch
  msg "Add cgit-f2feef0 patch" # See: https://cgit.kde.org/plasma-workspace.git/commit/?id=f2feef047ab21a6163988a8c0ed64728148f7ccd
  patch -p1 -i ../cgit-f2feef0.patch
  msg "Add cgit-d9c2f4e patch" # See: https://cgit.kde.org/plasma-workspace.git/commit/?id=d9c2f4ecb9fb773821152cf15d66d5133ef38d1c
  patch -p1 -i ../cgit-d9c2f4e.patch
  msg "Add kdebug-380347 patch" # See: https://bugs.kde.org/show_bug.cgi?id=380347
  patch -p1 -i ../kdebug-380347.patch
  msg "Add ccbug-391868 patch" # See: 
  patch -p1 -i ../ccbug-391868.patch
  msg "Add kdebug-390375 patch" # See: https://bugs.kde.org/show_bug.cgi?id=390375 Fixed in 5.12.5
  patch -p1 -i ../kdebug-390375.patch
  msg "Add vastly simplify image wallpaper code patch"
  patch -p1 -i ../Add_vastly_simplify_image_wallpaper_code.patch
  msg "Add KDEBUG 365317 and CCBUG 378010 patch" # See: https://bugs.kde.org/show_bug.cgi?id-365317
  patch -p1 -i ../kdebug-365317.patch
  msg "Add KDEBUG 382264 patch" # See: https://bugs.kde.org/show_bug.cgi?id=382264
  patch -p1 -i ../kdebug-382264.patch
  }

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-workspace() {
  depends+=(kuiserver)
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation'
              'kdepim-addons: displaying PIM events in the calendar'
              'appmenu-qt4: global menu support for Qt4 applications'
              'qt5-virtualkeyboard: virtual keyboard support in lock screen'
              'baloo: Baloo search runner'
              'appstream-qt: package search runner')
  provides=(notification-daemon)
  backup=('etc/pam.d/kde')

  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Remove conflicts with kuiserver
  rm "$pkgdir"/usr/bin/kuiserver5
  rm "$pkgdir"/usr/lib/libkdeinit5_kuiserver5.so
  rm "$pkgdir"/usr/share/dbus-1/services/kf5_org.kde.kuiserver.service
  rm "$pkgdir"/usr/share/kservices5/kuiserver.desktop
  rm "$pkgdir"/etc/xdg/kuiserver.categories

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_kuiserver() {
  pkgdesc='KDE Progress Info UI server'
  depends=(kxmlgui)

  cd build/kuiserver
  make DESTDIR="$pkgdir" install
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace qt5-wayland kwayland-integration xorg-server-xwayland)
  groups=()

  install -Dm644 build/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
