# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktextwidgets
pkgver=5.69.0
pkgrel=1.1
pkgdesc='Advanced text editing widgets'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kcompletion kconfigwidgets sonnet qt5-speech)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-256001.patch::"https://cgit.kde.org/ktextwidgets.git/patch/?id=1d1eb6feb7f85d5e09e00f75114f671eceff1598")
sha256sums=('182661253bf2ed940ab52edfeb2d8cadf1abbde76cade2ba50ef490311002ec9'
            'SKIP'
            '0b1f281db667b88d0c8d0e62d05a1fcb99ea8bc3c206d4e128f66bf88df3c927')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  msg "Applying kdebug 256001 patch; fix in 5.70"
  patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-256001.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
