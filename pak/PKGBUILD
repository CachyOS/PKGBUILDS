# Maintainer: nycko123 <nycko123 at gmail dot com>
# Contributor: pavbaranov <pavbaranov at gmail dot com>

# You had to install all optdepends for full functionality of this script

pkgname=pak
pkgver=3.2
pkgrel=1
pkgdesc="Simple pacman and AUR frontend, POLAUR helper"
arch=('any')
url="https://gitlab.com/nycko123/pak"
license=('GPL')
depends=('bash' 'git' 'curl' 'jq' 'pacman-contrib' 'man-db')
optdepends=("asp: to download PKGBUILD's of official packages"
			"auracle-git: to manage AUR packages"
			"namcap: to validate built packages"
			"reflector: to keep mirrorlist up to date"
			"subversion: to check upstream for updated -svn packages"
			"fzf: to display all data about installed packages")
backup=(etc/xdg/pak.conf)
changelog="CHANGELOG"
conflicts=(pak-git)
 
source=("https://gitlab.com/nycko123/pak/raw/master/src/${pkgname}-${pkgver}.tar.gz")
 
sha256sums=('f4a2475d9e0352928557fa1d597e1b2c1cf2592b5970f59cf8bd84c20d06a414')
 
package(){
 install -Dm755 "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"
 # configuration 
 install -Dm644 "$srcdir/pak.conf" "$pkgdir/etc/xdg/pak.conf"
 # translations
 install -Dm644 "$srcdir/locale/pl.mo" "$pkgdir/usr/share/locale/pl/LC_MESSAGES/pak.mo"
 # man-pages
 install -Dm644 "$srcdir/man/pak.8.gz" "$pkgdir/usr/share/man/man8/pak.8.gz"
 install -Dm644 "$srcdir/man/pak.conf.5.gz" "$pkgdir/usr/share/man/man5/pak.conf.5.gz"
 install -Dm644 "$srcdir/man/pl/pak.conf.5.gz" "$pkgdir/usr/share/man/pl/man5/pak.conf.5.gz"
 # bash autocompletions
 install -Dm644 "$srcdir/completion/pak.bash" "$pkgdir/usr/share/bash-completion/completions/pak"
 # zsh autocompletions
 install -Dm644 "$srcdir/completion/pak.zsh" "$pkgdir/usr/share/zsh/site-functions/_pak"
}
