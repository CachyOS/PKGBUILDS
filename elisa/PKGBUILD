# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa
pkgver=21.04.3
pkgrel=1.1
pkgdesc='A simple music player aiming to provide a nice experience for its users'
url='https://apps.kde.org/elisa/'
arch=(x86_64)
license=(LGPL3)
groups=(kde-applications kde-multimedia)
depends=(baloo kirigami2 kdeclarative vlc) # upnp-player-qt
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
commit-9713eb29.patch::"https://invent.kde.org/multimedia/elisa/-/commit/9713eb2923cf01838d58e36c3a275c2f4986cbcf.patch"
kdebug-436217.patch::"https://invent.kde.org/multimedia/elisa/commit/2667a6474efe55c5941898bc982d52a27e32079e.patch"
commit-ab712cd2.patch::"https://invent.kde.org/multimedia/elisa/-/commit/6bac7e23dfe90af1a27f9a3d0b3c7254b26fd9fd.patch"
)
sha256sums=('13b76c27246f348180501c4dd26c9c8070f32b3fbebb49f5e7d4596d9b27eec2'
            'SKIP'
            '592f9e6243e25afe5cc35ee1bf147d48d94121f71be5582c898680d507b99953'
            'b3c354126cc606cfd47477ea16e7e8587dd36ecafa4503b7df82e8cfdd569cbd'
            '8472f3147ffbcf7eaf9577e9a856504ee72ab75c5179e2cacd11757f8df4a955')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
    msg "Add commit 9713eb29 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../commit-9713eb29.patch
    msg "Add kdebug 436217 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-436217.patch
    msg "Add commit ab712cd2 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../commit-ab712cd2.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
