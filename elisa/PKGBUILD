# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa
pkgver=0.1.1
pkgrel=1.1
pkgdesc='A simple music player aiming to provide a nice experience for its users'
url='https://community.kde.org/Elisa'
arch=(x86_64)
license=(LGPL3)
depends=(baloo qt5-quickcontrols qt5-quickcontrols2 qt5-graphicaleffects kcmutils) # upnp-player-qt
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-394259.patch::"https://cgit.kde.org/elisa.git/patch/?id=dd84041481ee4ca39c6a416a87c543fb1d14157a")
sha256sums=('3ca7dce9092de422cea82f37fa16613e5aa969cfdae016dce87d38a46d8c463b'
            'SKIP'
            'e4d66ad75ab65d01149c9f9bb7dbfa9112ec7837bf50b2e19f0fbb15ecbc6f97')
validpgpkeys=(267BF70F7905C2723B0243267D0F74F05C22F553) # Matthieu Gallien <matthieu_gallien@yahoo.fr>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG 394259 patch"
  patch -p1 -i ../kdebug-394259.patch
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
