# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=breeze-icons
pkgver=5.78.0
pkgrel=1.1
pkgdesc='Breeze icon themes'
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
makedepends=(extra-cmake-modules qt5-base python-lxml)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-431883.patch::"https://invent.kde.org/frameworks/breeze-icons/commit/5be003361fdf93537ce0c554363583b826fdaff5.patch")
sha256sums=('425f6d0163dac092939dede0b1023de5b175738cb59bb2ec002c821973a449b9'
            'SKIP'
            'aea697f613a7a11897d3236078fee608479c24d1b7204c79308e17194487814c')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 431883 patch; fix in 5.79 and it's (low) important only with Plasma >=5.20.90"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-431883.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
