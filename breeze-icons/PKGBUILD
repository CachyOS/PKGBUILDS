# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=breeze-icons
pkgver=5.73.0
pkgrel=1.1
pkgdesc='Breeze icon themes'
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
makedepends=(extra-cmake-modules qt5-base)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-425089.patch::"https://invent.kde.org/frameworks/breeze-icons/commit/6c9c1e76d7ae35aa9c30604ca2de069f9b15c335.patch"
kdebug-425059.patch::"https://invent.kde.org/frameworks/breeze-icons/commit/a18d1ced8cbfa30a6b4f21417655293ae99844d3.patch")
sha256sums=('b6caff26f69008a3e0d53ae5fcfcf070b70ad1b17d407daecbbabeb6a606a08b'
            'SKIP'
            '8f9593fba21e050106573dfba9261b408b7adb03f5c3fcffbd5c1a055b81060b'
            '03e7c8ab8aa3eaef40db326f1ab6fd748c4546f7a3551ac191773461be21bd9b')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  msg "Add kdebug 425059 patch"
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-425059.patch
  msg "Add kdebug 425089 patch"
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-425089.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
