# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=breeze-icons
pkgver=5.56.0
pkgrel=1.1
pkgdesc='Breeze icon themes'
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
makedepends=(extra-cmake-modules qt5-base)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-405195.patch::"https://cgit.kde.org/breeze-icons.git/patch/?id=3bc4079c220e1ba681fc8a0841a1ffa6ce88fb7b")
sha256sums=('7d98d6be0795cc5dd7468e3d8093541726dcc10586a91477c02879f5eb8fcf58'
            'SKIP'
            'c7032f182c98c021031fc9cc4dede307db07d1ded0b3e8ce453268a0a5f492f4')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply KDEBUG 405195 patch; fix in 5.57"
  patch -p1 -i ../kdebug-405195.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
