# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=breeze-icons
pkgver=5.75.0
pkgrel=1.1
pkgdesc='Breeze icon themes'
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
makedepends=(extra-cmake-modules qt5-base)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-397996.patch::"https://invent.kde.org/frameworks/breeze-icons/commit/0639985e692a5a01bebb3a8d97f70d9afbba5675.patch"
kdebug-392533.patch::"https://invent.kde.org/frameworks/breeze-icons/commit/2883dff5da67c7e595236e92710249846923dd90.patch")
sha256sums=('21985f62c111e60ebd34bd632b3ee06bb01a1f9f1671d1295f0d24bc21e106a2'
            'SKIP'
            '891c0c838bb35889d2844f33f9fa4b3cd586bdc7a72f298d94ac0ad170ea12bf'
            '209820f2d9ffabd7e241b22b5966eb2aefefd52e253357aa5127add5ee174637')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 397996 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-397996.patch
    msg "Add kdebug 392533 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-392533.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
