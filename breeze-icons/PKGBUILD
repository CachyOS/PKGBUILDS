# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=breeze-icons
pkgver=5.52.0
pkgrel=1.1
pkgdesc='Breeze icon themes'
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
makedepends=(extra-cmake-modules qt5-base)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-393318.patch::"https://cgit.kde.org/breeze-icons.git/patch/?id=64b3a8fa6ff713c526e2ebdc607865b8475c72a2")
sha256sums=('979db0040d8c7ef7adcac133988f6440ebe21bf3f4c4479e830d38195ba1dc5a'
            'SKIP'
            '16c532badb7591d88e4f4c441b1705b01248da5e7dbeec354e5d1b14b3721a9d')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG 393318 patch" #Fix in 5.53
  patch -p1 -i ../kdebug-393318.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF
  make -j5
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
