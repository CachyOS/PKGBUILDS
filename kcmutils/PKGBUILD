# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcmutils
pkgver=5.74.0
pkgrel=1.1
pkgdesc='Utilities for interacting with KCModules'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kdeclarative)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-423080.patch::"https://invent.kde.org/frameworks/kcmutils/commit/8ac50dd36e5a9d33ffdb5e3ca4fcf67516263da9.patch")
sha256sums=('c2d54364997ed75961ad875d73a667f6bc8d6732cfacd7ad40ba84c594d567c7'
            'SKIP'
            '0f6e066e917d022e9facedfef17643450f34c5ff264d68056abfb226393e501e')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 423080 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-423080.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
