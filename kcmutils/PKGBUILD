# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcmutils
pkgver=5.52.0
pkgrel=1.1
pkgdesc='Utilities for interacting with KCModules'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kdeclarative)
makedepends=(extra-cmake-modules doxygen qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-397894.patch::"https://cgit.kde.org/kcmutils.git/patch/?id=2da8aed41663d2e765dddb3bf66fad038879d6d4")
sha256sums=('b11bbabdb11c205bdf7419e7a397555dd769353ffe217182d2c187f98e3142b3'
            'SKIP'
            'bd0c7ce9fb8c3b00f64dbb8fd1ab4bcd9bb1df9853dfa27ce30d029bd9e9e3d6')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add kdebug 397894 patch" # Fix in 5.53
  patch -p1 -i ../kdebug-397894.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make 
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
