# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcmutils
pkgver=5.79.0
pkgrel=1.1
pkgdesc='Utilities for interacting with KCModules'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kdeclarative)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-431855.patch::"https://invent.kde.org/frameworks/kcmutils/commit/5a5718b22a396d392df8241ef6e98b8b6faca009.patch"
)
sha256sums=('73d286b84ad32e3fa350b667993a6db731ee4c7e878d0a0cf9f5c81933423ef3'
            'SKIP'
            'c85698772895f7edecb17d276c31756e32932f891a7f41ea8a7c2e1f461e9632')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 431855 patch; fix in 5.80"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-431855.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
