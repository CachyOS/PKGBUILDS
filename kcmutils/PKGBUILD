# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcmutils
pkgver=5.78.0
pkgrel=1.1
pkgdesc='Utilities for interacting with KCModules'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kdeclarative)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-431843.patch::"https://invent.kde.org/frameworks/kcmutils/-/commit/293374dc9ff305f4dae202334332bdae0e01a3b9.patch"
)
sha256sums=('68165fa953286ba5034b1766d09b02542c9d3eaa13dc4d64e8ced84cc70cc2d3'
            'SKIP'
            '4abe90bcc0787678ca48c41dda90a499cb4d4fc4d4ca7a942d5eff5d181fcaa0')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 431843 patch; fix in 5.79"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-431843.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
