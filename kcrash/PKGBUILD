# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# made upon the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcrash
pkgver=5.68.0
pkgrel=1.1
pkgdesc='Support for application crash analysis and bug report from apps'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kcoreaddons kwindowsystem)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
optdepends=('drkonqi: KDE crash handler application')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-375913.patch::"https://cgit.kde.org/kcrash.git/patch/?id=6177c81d04a7490de558ab5f266d4a81f2fb8f20")
sha256sums=('60daf2cee87c652619b098b688edce6f993c7960783159cd8be9d9145df29f7f'
            'SKIP'
            'ef051224e901aac5f0e1faca1281e56990a48e5cdb9d0fc889e30c27322bd112')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir build
  
  cd $pkgname-$pkgver
  msg "Apply KDEBUG 375913 patch"
  patch -p1 -i ../kdebug-375913.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
