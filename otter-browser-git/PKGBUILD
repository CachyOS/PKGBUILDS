#Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
#Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Maintaners and Contributors PKGBUILD in AUR:
#Maintainer: m4sk1n <m4sk1n@vivaldi.net>
#Contributor: Christian Hesse <mail@eworm.de>

pkgname=otter-browser-git
pkgver=1.0.81.r8442.g405222cd3
pkgrel=1
pkgdesc='Browser aiming to recreate the best aspects of the classic Opera (12.x) UI using Qt5 - git checkout'
arch=('x86_64')
url='http://otter-browser.org/'
license=('GPL3')
provides=('otter-browser')
conflicts=('otter-browser' 'otter-browser-weekly')
depends=( 'qt5-webkit' 'qt5-multimedia>=5.4' 'qt5-xmlpatterns>=5.4')
makedepends=('cmake' 'git' 'qt5-declarative>=5.4' 'qt5-webengine' 'hunspell')
optdepends=('hunspell: for spell checking'
            'qt5-webengine: for experimental backend using the Chromium browser project')
source=('git+https://github.com/OtterBrowser/otter-browser.git')
sha256sums=('SKIP')

pkgver() {
	cd otter-browser/
        _ver="$(cat CMakeLists.txt | grep -m3 -e MAJOR_VERSION -e MINOR_VERSION -e PATCH_VERSION | grep -o "[[:digit:]]*" | paste -sd'.')"
        printf "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
    mkdir -p otter-browser/build
    }
    
build() {
	cd otter-browser/build/

	cmake -DCMAKE_INSTALL_PREFIX="/usr" \
          -DENABLE_QTWEBENGINE=ON \
          -DENABLE_QTWEBKIT=ON \
          -DENABLE_SPELLCHECK=ON \
          ../
	make
}

package() {
	cd otter-browser/build/
	make DESTDIR=${pkgdir} install
}
