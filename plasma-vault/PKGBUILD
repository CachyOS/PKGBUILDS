# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes or improvements from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-vault
pkgver=5.21.5
pkgrel=1.2
pkgdesc="Plasma applet and services for creating encrypted vaults"
arch=(x86_64)
url="https://www.kde.org/"
license=(LGPL)
groups=(plasma)
depends=(plasma-workspace networkmanager-qt)
makedepends=(extra-cmake-modules)
optdepends=('encfs: to use encFS for encryption' 'cryfs: to use cryFS for encryption' 'gocryptfs: to use gocryptfs for encryption')
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
commit-2f5288a8.patch::"https://invent.kde.org/plasma/plasma-vault/-/commit/2f5288a83fcd8fcae20b725e9df1905cf8f02570.patch"
commit-03c7ff3e.patch::"https://invent.kde.org/plasma/plasma-vault/-/commit/03c7ff3e12849b5bb333d7b3bd0e81783ab167a5.patch"
)
sha256sums=('7519b5da20e59f571128b66fc22e5d2b2027752e01caa1c70574b12dcfd8979b'
            'SKIP'
            '81111b83e7c717f35b249f91808a8ccd4eb15bcbe15ac29421c8f21a44212f35'
            '94171944f1065a547f4177ccfe88ba29e5e06fd4e733329b936670f386afac68')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add "Show in File Manager" action; fix in 5.22"
    patch -d $pkgname-$pkgver -p1 -i ../commit-2f5288a8.patch
    msg "Add commit-03c7ff3e patch; fix in 5.22"
    patch -d $pkgname-$pkgver -p1 -i ../commit-03c7ff3e.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
