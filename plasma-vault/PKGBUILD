# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-vault
pkgver=5.18.4
pkgrel=1.1
pkgdesc="Plasma applet and services for creating encrypted vaults"
arch=(x86_64)
url="https://www.kde.org/"
license=(LGPL)
groups=(plasma)
depends=(plasma-workspace networkmanager-qt)
makedepends=(extra-cmake-modules)
optdepends=('encfs: to use encFS for encryption' 'cryfs: to use cryFS for encryption')
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-418262.patch::"https://cgit.kde.org/plasma-vault.git/patch/?id=f6a924b37880b7340e796846a6e5474413ca6533"
kdebug-412779.patch::"https://cgit.kde.org/plasma-vault.git/patch/?id=1e2988d7d3ca68f9ff00e576c634aff4fe5b8b32")
sha256sums=('f0865a99884f7a072da0c665aa9ae9fe596d7f50e8b678530e82c272604158d2'
            'SKIP'
            'e099d7bd060a06a14e5a310ca0a76eb78021c8d2c2e3a3801376046fbbb65f0b'
            '8f03ccaac34b1fe2a3f63453db41bfadd06df8b1389a577181e2649fa6d3c156')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-418262.patch
  patch -p1 -i ../kdebug-412779.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
