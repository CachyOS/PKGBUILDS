# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-vault
pkgver=5.18.5
pkgrel=1.1
pkgdesc="Plasma applet and services for creating encrypted vaults"
arch=(x86_64)
url="https://www.kde.org/"
license=(LGPL)
groups=(plasma)
depends=(plasma-workspace networkmanager-qt)
makedepends=(extra-cmake-modules)
optdepends=('encfs: to use encFS for encryption' 'cryfs: to use cryFS for encryption')
source=("https://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-412779.patch::"https://cgit.kde.org/plasma-vault.git/patch/?id=1e2988d7d3ca68f9ff00e576c634aff4fe5b8b32"
D28914.patch::"https://cgit.kde.org/plasma-vault.git/patch/?id=6944aab19e86e153c89bceb6828cb0f6fea42a8c")
sha256sums=('cae2713823e8c59c7a2beb96d362a15024fe260cf10419ba037e8a798f3c1b41'
            'SKIP'
            '8f03ccaac34b1fe2a3f63453db41bfadd06df8b1389a577181e2649fa6d3c156'
            '2a93ecdf5c551d67b64344b11644bf488314d8a2dc1380cb393a9b2891770412')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-412779.patch
  patch -p1 -i ../D28914.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
