# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcomming releases
# make on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwayland
pkgver=5.43.0
pkgrel=1.1
pkgdesc='Qt-style Client and Server library wrapper for the Wayland libraries'
arch=('x86_64')
url='https://www.kde.org'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules' 'doxygen' 'qt5-tools')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
kdebug-389221.patch::"https://cgit.kde.org/kwayland.git/patch/?id=2dfe16d774c79ede20ac9439f66459b6a8026525")
sha256sums=('51aa27792577a52d3c09fa5f08e4d057827cd569dc836839f3528eb2ed4a973e'
            'SKIP'
            '672cd8ee97c816e77b381fdba9a06a6768d1d5bac3859b480b301a56e644bb17')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
	cd $pkgname-$pkgver
	msg "KDEBUG389221 Patch" # See: https://bugs.kde.org/show_bug.cgi?id=389221 Fixed in 5.44
	patch -p1 -i ../kdebug-389221.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
