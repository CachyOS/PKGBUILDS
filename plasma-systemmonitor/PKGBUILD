# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-systemmonitor
pkgver=5.21.5
pkgrel=1.2
pkgdesc="An interface for monitoring system sensors, process information and other system resources"
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(GPL LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-429758.patch::"https://invent.kde.org/plasma/plasma-systemmonitor/commit/a4fc2686753f1749001f80a8b98ff85d15135bbc.patch"
ccbug-429758.patch::"https://invent.kde.org/plasma/plasma-systemmonitor/commit/8a69730b5411dd77cea2b545262c517ce5118960.patch"
kdebug-429417.patch::"https://invent.kde.org/plasma/plasma-systemmonitor/commit/9c2d8b7676ec68b60a6f8e991d61c12b6575ad17.patch"
kdebug-436686.patch::"https://invent.kde.org/plasma/plasma-systemmonitor/commit/5f35b40c6e8da2d6582fb99e50e5c3498617bc6b.patch"
)
depends=(ksysguard kirigami2)
makedepends=(extra-cmake-modules)
groups=(plasma)
sha256sums=('3c4d7facd472fd68eee6c7f7175539eeae42bf8a6d6c8e41925a0b0fd6988ecf'
            'SKIP'
            'e50de31896cb2924f1791d2dd2ad61d7aadf24fa807e84df2de6654a9e831d40'
            'a375054ed794482bc31f746ec4e38bf056239a3c67c1d45ff112b8419cbfd2ef'
            'b4e4fa4d3a23a575c50d18d623d79086fa0c4f2fb7b5f19b489cf3b2bcd6be20'
            'b78e93b5a039a9ceec63634638a73e581db420fdbdff5c2561d7eecee3bcb947')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 429758 patches; fix probably in 5.22"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-429758.patch
    patch -d $pkgname-$pkgver -p1 -i ../ccbug-429758.patch
    msg "Add kdebug 429417 patch; fix in 5.22"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-429417.patch
    msg "Add kdebug 436686 patch; fix in 5.22"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-436686.patch
}
    
build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
