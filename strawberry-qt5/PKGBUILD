# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix

pkgname=strawberry-qt5
pkgver=0.9.1
pkgrel=2.1
pkgdesc="A music player aimed at audio enthusiasts and music collectors"
arch=(x86_64)
url="https://www.strawbs.org/"
license=(GPL3)
depends=(qt5-base chromaprint protobuf gst-plugins-base gst-plugins-good udisks2 libcdio libgpod libmtp fftw vlc)
makedepends=(cmake boost qt5-tools)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
source=("https://github.com/jonaski/strawberry/releases/download/$pkgver/${pkgname%-*}-$pkgver.tar.xz")
conflicts=(strawberry)
replace=(strawberry)
provides=(strawberry)
sha256sums=('b71da4cec9cc7b60961bbb82378d15fb79798e6b880852414ce05276084047a0')

build() {
  cmake -B build -S ${pkgname%-*}-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT6=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
