# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwidgetsaddons
pkgver=5.69.0
pkgrel=1.2
pkgdesc='Addons to QtWidgets'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools qt5-doc clang python-pyqt5 doxygen sip)
optdepends=('python-pyqt5: for the Python bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-245637.patch::"https://cgit.kde.org/kwidgetsaddons.git/patch/?id=75298895a5e3ceb1e6fc3d4e5ebc9dcbb039576f"
kdebug-420287.patch::"https://cgit.kde.org/kwidgetsaddons.git/patch/?id=b52622993b45a61c4060d19517a5c3b833b3b854")
sha256sums=('5fcc39eb17d832a14e8c2fe98ff26a300aa5c88013c63bcf8eef0017ca4eaf37'
            'SKIP'
            '32dc398363a07ec9a7217a4785c5a1d00fe06b4f2a86446c91a5cb416357cd83'
            '3cce5812abe23a6b3115fbd5998174e9c30e681043be5486cc3018ad653bb2c1')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  msg "Applying KDEBUG 245637 patch; fix in 5.70"
  patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-245637.patch
  msg "Applying KDEBUG 420287 patch; fix in 5.70"
  patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-420287.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
