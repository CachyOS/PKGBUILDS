# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwidgetsaddons
pkgver=5.71.0
pkgrel=1.1
pkgdesc='Addons to QtWidgets'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools qt5-doc clang python-pyqt5 doxygen sip)
optdepends=('python-pyqt5: for the Python bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
fix-memory-leak.patch::"https://invent.kde.org/frameworks/kwidgetsaddons/-/commit/b77465d532f0d8145f1ab97c46cef723ff17ffde.patch")
sha256sums=('897077995bcf4125d0f90d2964500e718d2a3fd5f117e1b7906177ad13a5082e'
            'SKIP'
            'e0b7e13cda54854565f4e63cf1f69ec5dfb6f5ac4023a2baceef21946ed5d374')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Fix potential memory leak"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/fix-memory-leak.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
