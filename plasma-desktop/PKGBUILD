# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcoming releases
# made upon the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=plasma-desktop
pkgname=(plasma-desktop knetattach)
pkgver=5.18.5
pkgrel=2.1
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)

url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
source=("https://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-416553.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=a35d2b9adf40072e242b8820dcb07cf0a7ed037b"
        kdebug-415649.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=c32b5a85cae6f99bb5097b16a25983fc611aecc6"
        kdebug-417461.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=fffb352cfa1eb5e18f038d5b423d2dce0dca8727"
        kdebug-406932.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=3e8c7dada73ed838a93b0b0ffcd5c679627ffa54"
        kdebug-403842.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=0cf73170479bb271fb7498e67d38d3c6255594a4"
        kdebug-420079.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=fe4d395d792d50174fd4529cd259fcf933b7af09"
        kdebug-413954.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=35e18420a76ab4f738a813e67c2e304834b60bf7"
        kdebug-413198.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=fd6f3bacc91e8651aae21557f0f316690e6b8852"
#        kdebug-157468.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=1ee6660ceb62d4584a5371d0cce107b7008e75df"
        kdebug-418269.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=1d557e8a78a737b6c63bf2400e863738f1f063ff"
        kdebug-418165.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=2c46f28170c785f04fa396803037a608c8077025"
        plasma-desktop-animation-duration.patch)
depends=(polkit-kde-agent libxkbfile kmenuedit systemsettings ksysguard baloo)
makedepends=(extra-cmake-modules kdoctools xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel
             libibus scim kdesignerplugin)
groups=(plasma)
sha256sums=('aeb106018fd90da79c8a3c444d880282846a842029b1223e7830db2d4b42df9f'
            'SKIP'
            '674976dfb7b78c66f489d4f04bafcad395c0dddff439fb7bef386cb7cd3a86ee'
            '2d83b72c1fa5e44a68140ec981eb2d9ea2e39270fc7246910e12341df31b3a92'
            '9dc1158dadf84731d298e1d3f3fbb64f3b02df95e04f584f88093aebe43e7a4f'
            '5d688a9f644be7c57469b84211b565fd4b4242b6a812bd1df289aeb4805979ee'
            '8315a85a5e5ef3fac6e07755c2b3abec7326de2225c82871f3198c006d3919c5'
            '3373e4f8122e52c204b623c43df3d1e2de8ae3aaf228acc88b2189b1e6375b64'
            '814602b30a7160fe550e1deec64f270f0c7d56e0ebb90e260ebfb0795d592b5a'
            '489054d747b8f577d53bac3d0d2f5acb943a680b112a3e6de218104272bf6563'
            'ab10818d57fe6eed5a6c629b4e519a48a02085772b0a8fa87038b940ce2bcc4f'
            '4a56a39be443cb316b4b576f039be388928745766f8fe81ec39165b0b71b4650'
            'ebb9c75c8b913a965b899b82ec6c809b1fd24036730b961cd3dcd40237ec784c')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Applying kdebug 416553.patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-416553.patch
  msg "Applying kdebug 415649 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-415649.patch
  msg "Applying kdebug 397595 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-417461.patch
  msg "Applying kdebug 406932 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-406932.patch
  msg "Applying kdebug 403842 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-403842.patch
  msg "Applying kdebug 420079 patch"
  patch -p1 -i ../kdebug-420079.patch
  msg "Applying kdebug 413954 patch"
  patch -p1 -i ../kdebug-413954.patch
  msg "Applying kdebug 413198 patch; fix in 5.19"
  patch -p1 -i ../kdebug-413198.patch
#  msg "Applying kdebug 157468, 213101, 230583, 250121, 251437, 272554, 318964, 341817, 348264, 366257, 369020, 388574, 393403, 408942, 416149, 416737, 417915, 419215, 419515, 419624, 419692, 419825, 419909, 420093 patch; fix in 5.19.0"
#  patch -p1 -i ../kdebug-157468.patch
  msg "Applying kdebug 418269 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-418269.patch
  msg "Applying kdebug 418165 patch"
  patch -p1 -i ../kdebug-418165.patch
  patch -p1 -i ../plasma-desktop-animation-duration.patch # Fix slow animations with Frameworks 5.70

}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make 
}

package_plasma-desktop() {
  depends+=(knetattach)
  optdepends=('plasma-nm: Network manager applet'
              'powerdevil: power management, suspend and hibernate support'
              'kscreen: screen management'
              'ibus: kimpanel IBUS support'
              'scim: kimpanel SCIM support')

  cd build
  make DESTDIR="$pkgdir" install

# Split knetattach
  rm "$pkgdir"/usr/{bin/knetattach,share/applications/org.kde.knetattach.desktop}
}

package_knetattach() {
  pkgdesc='Wizard which makes it easier to integrate network resources with the Plasma Desktop'
  depends=(kdelibs4support)

  cd build/knetattach
  make DESTDIR="$pkgdir" install
}
