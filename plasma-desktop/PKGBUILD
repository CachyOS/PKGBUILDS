# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for 5.12.x version with bugfix patches form master branch
# or even from Phabricator
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# This is experimental version of plasma-desktop with patches from KDE's GIT or from
# phabricator. Be carefull!

pkgbase=plasma-desktop
pkgname=(plasma-desktop knetattach)
pkgver=5.12.1
pkgrel=1.1
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
"kdebug-390428.patch::https://cgit.kde.org/plasma-desktop.git/patch/?id=840694cc5bb"
"D10485.patch::https://cgit.kde.org/plasma-desktop.git/patch/?id=502e8dc1080"
"kdebug-370588.patch::https://cgit.kde.org/plasma-desktop.git/patch/?id=2b098f69d35"
"enable-smooth-scaling-for-icon.patch::https://cgit.kde.org/plasma-desktop.git/patch/?id=b7e212441cb"
"kdebug-390502.patch::https://cgit.kde.org/plasma-desktop.git/patch/?id=0ead1a6e77e"
"kdebug-390353.patch::https://cgit.kde.org/plasma-desktop.git/patch/?id=a2c5ffe37a8"
"D10259.patch::https://cgit.kde.org/plasma-desktop.git/patch/?id=6ab44dcb86b")

depends=(polkit-kde-agent libcanberra libxkbfile kmenuedit appstream-qt systemsettings ksysguard kpeople baloo 
qt5-graphicaleffects libibus)
makedepends=(extra-cmake-modules kdoctools boost xf86-input-evdev xf86-input-synaptics xorg-server-devel
             libibus scim python kdesignerplugin)
groups=(plasma)
sha256sums=('bca9dbb7b60c9ab20d60c858a0377eab3738853685e3c9e0bb7bd706f6965cd6'
            'SKIP'
            '1097bb12314c39fb101cdd5f143ab5c0d081ddb0416541fa26016769a9f39acc'
            '6920e29d0596ac427eef094de32e094b917db33b282f7da40342ee6a7fd521e3'
            '6bd84bae3b4193f94ed13da16dacd7f5746056f04033364214f02bc7441ed257'
            '03a460a6216fe48353d7c9ca6666279b0953059e57dcef8d35d17155e56d2a83'
            'a464330798965204d28ba060474a8dc606e202fb2dfa0dac2989f3a7ddf3c0e0'
            '3327635a34d22e45c81eabb43d41555ef9d029987fb9dc639f8c8e75aef6a1e0'
            'fa022c9f178f4b109cd1cf34ccaf755584c616573e8837d89d9359cec1e4b3cf')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

	cd $pkgname-$pkgver
	msg "Patch for KDEBUG 390353" # Fixed in 5.12.2
	patch -p1 -i ../kdebug-390353.patch
	msg "Patch with D10259 patch" # See https://phabricator.kde.org/D10259
	patch -p1 -i ../D10259.patch
	msg "Patch for KDEBUG 390502" # Fixed in 5.13.0
	patch -p1 -i ../kdebug-390502.patch
	msg "Patch for KDEBUG 390428" # See https://phabricator.kde.org/D10510
	patch -p1 -i ../kdebug-390428.patch
	msg "Patch with D10485 patch" # See https://phabricator.kde.org/D10485
	patch -p1 -i ../D10485.patch
	msg "Patch with KDEBUG 370588" # See https://bugs.kde.org/show_bug.cgi?id=370588 Urgent in testing!
	patch -p1 -i ../kdebug-370588.patch
	msg "Patch with enable smooth scaling for icon"
	patch -p1 -i ../enable-smooth-scaling-for-icon.patch

	}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-desktop() {
  depends+=(knetattach)
  optdepends=('plasma-nm: Network manager applet'
              'powerdevil: power management'
              'ibus: kimpanel IBUS support'
              'scim: kimpanel SCIM support'
              'discover: manage applications installation from the launcher')

  cd build
  make DESTDIR="$pkgdir" install

# Split knetattach
  rm "$pkgdir"/usr/{bin/knetattach,share/applications/org.kde.knetattach.desktop}
}

package_knetattach() {
  pkgdesc='Wizard which makes it easier to integrate network resources with the Plasma Desktop'
  depends=(kdelibs4support)

  cd build/knetattach
  make DESTDIR="$pkgdir" install
}
