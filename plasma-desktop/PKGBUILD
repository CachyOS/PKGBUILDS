# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=plasma-desktop
pkgname=(plasma-desktop knetattach)
pkgver=5.20.3
pkgrel=1.1
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
#commit-41c47566.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/41c47566f736fefe1e7589efd9d21bd3d05cbe33.patch"
kdebug-423682.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/7079d0952b313b636126c39f0437ff6ba8687ca7.patch"
commit-0c8e0e7d.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/0c8e0e7deefa65ff0a04dc882c041648ec162a74.patch"
kdebug-398803.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/4e2f4a67fed50340691c833e2560f779055b673a.patch"
kdebug-366462.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/a366714231dcdb24e17bf12c39d4457ac5d7dbb7.patch"
#kdebug-426775.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/23d221319820f27368929d30392289b33fdf535a.patch"
commit-6a0d8d6c.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/6a0d8d6c35dbbdf5c486fceb156aaf3aaf30b827.patch"
)
depends=(polkit-kde-agent libxkbfile kmenuedit systemsettings ksysguard baloo libibus accountsservice)
makedepends=(extra-cmake-modules kdoctools xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel scim kdesignerplugin kaccounts-integration intltool)
groups=(plasma)
sha256sums=('d78ce0d288bb657b1e417179fc5b28bb291b44df7b7840400f034243d4e10b3a'
            'SKIP'
            '7729d8f368cfd148c5c575e19138a6eb6517c2b093e9ce22077c7d355e56edab'
            '1bda68d7c5e1215a3f32cb907dbf5141febef4244b609e1681cbc999630a6daa'
            'b8d3aa9407b55963cb99eb48c991827f85eab7a88358d576f850dc89ececf049'
            '2b20e4128d7fc42b4caad93b95483a947a7b2466b88e7fdb5c51a2079a4e8bdc'
            '22bfb16cec5e4a56fc5e0b454bca74ce3f316ec7885ebb4906ec3301b2ad8d8b')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    # msg "Add Highlight changed settings; fix in 5.21"
    # patch -d $pkgname-$pkgver -p1 -i ../commit-41c47566.patch    
#    msg "Add kdebug 423682 patch"
#    patch -d $pkgname-$pkgver -p1 -i ../kdebug-423682.patch
    msg "Add Show frequently used settings modules for System Settings in Task Manager patch"
    patch -d $pkgname-$pkgver -p1 -i ../commit-0c8e0e7d.patch
    msg "Add kdebug 398803 patch; it's neccessary to build kservice from POLAUR"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-398803.patch
    msg "Add Animate rebberband exit patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../commit-6a0d8d6c.patch
    msg "Add kdebug 366462 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-366462.patch
#    msg "Add kdebug 426775 patch; fix in 5.21"
#    patch -d $pkgname-$pkgver -p1 -i ../kdebug-426775.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_plasma-desktop() {
  depends+=(knetattach)
  optdepends=('plasma-nm: Network manager applet'
              'powerdevil: power management, suspend and hibernate support'
              'kscreen: screen management'
              'ibus: kimpanel IBUS support'
              'scim: kimpanel SCIM support')

  DESTDIR="$pkgdir" cmake --install build

# Split knetattach
  rm "$pkgdir"/usr/{bin/knetattach,share/applications/org.kde.knetattach.desktop}
}

package_knetattach() {
  pkgdesc='Wizard which makes it easier to integrate network resources with the Plasma Desktop'
  depends=(kdelibs4support)

  DESTDIR="$pkgdir" cmake --install build/knetattach
}
