# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for 5.12.x version with bugfix patches form master branch
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=plasma-desktop
pkgname=(plasma-desktop knetattach)
pkgver=5.12.4
pkgrel=1.1
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}

kdebug-379432.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=8208d9b3b3b9ba903b09a5e173031296a5688d43"
#kdebug-392050.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=8af970fd49f3ca33450cf3ccd81856694a0dc752"
kdebug-387444.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=db311474f0486d5cfcef069663e53be37d764cf2"
kdebug-379888.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=2683d4c6e60a9b8cca32f9b342d9010f4cc46086"
ccbug-391485.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=d6bc5f35232a4f72a705bd68727c9ea0820b70ac"
kdebug-390888.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=20bcc8f0501bb3cd368b03797c0bdea54de9f84b"
)

depends=(polkit-kde-agent libcanberra libxkbfile kmenuedit appstream-qt systemsettings ksysguard kpeople baloo 
qt5-graphicaleffects libibus)
makedepends=(extra-cmake-modules kdoctools boost xf86-input-evdev xf86-input-synaptics xorg-server-devel
             libibus scim python kdesignerplugin)
groups=(plasma)
sha256sums=('bec2bf03d3cc44e12a92cb16b40957425708ea58269eee6030d49ee696dde299'
            'SKIP'
            '5a26918940efc2526ef0f37e6dc321a4681f9ff025f1662da70cd99dac73319e'
            '4fae7448f43b085fe4b4094e1efddd0acdcf730ea9274d43b40a5d775d2323be'
            '50644bacc15c2ac7062a893df2497147b71055b72bacca3ac510385c64ab56dd'
            '16b55b80c261172395cf279abc9ab4d6cad187cd8d5e7bb62f2140202c74737f'
            '33fdd8b809d27f275e1a6f1c743f23634c8af7758570af35d32c388fdc1578e7')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
    mkdir -p build
    
    cd $pkgname-$pkgver
    msg "Add KDEBUG 379432 patch" # See: https://bugs.kde.org/show_bug.cgi?id=379432
    patch -p1 -i ../kdebug-379432.patch
#    msg "Add KDEBUG 392050 patch" # See: https://bugs.kde.org/show_bug.cgi?id=392050
#    patch -p1 -i ../kdebug-392050.patch
    msg "Add KDEBUG 387444 patch" # See: https://bugs.kde.org/show_bug.cgi?id=387444
    patch -p1 -i ../kdebug-387444.patch
    msg "Add KDEBUG 379888 patch" # See: https://bugs.kde.org/show_bug.cgi?id=378688
    patch -p1 -i ../kdebug-379888.patch
    
    msg "Add CCBUG 391485 patch"
    patch -p1 -i ../ccbug-391485.patch
    msg "Add KDEBUG 390888 patch"
    patch -p1 -i ../kdebug-390888.patch
	}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-desktop() {
  depends+=(knetattach)
  optdepends=('plasma-nm: Network manager applet'
              'powerdevil: power management'
              'ibus: kimpanel IBUS support'
              'scim: kimpanel SCIM support'
              'discover: manage applications installation from the launcher')

  cd build
  make DESTDIR="$pkgdir" install

# Split knetattach
  rm "$pkgdir"/usr/{bin/knetattach,share/applications/org.kde.knetattach.desktop}
}

package_knetattach() {
  pkgdesc='Wizard which makes it easier to integrate network resources with the Plasma Desktop'
  depends=(kdelibs4support)

  cd build/knetattach
  make DESTDIR="$pkgdir" install
}
