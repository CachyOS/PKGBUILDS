# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-desktop
pkgver=5.22.4
pkgrel=1.1
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
commit-47c6f8fc.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/47c6f8fc15cd1b111dc1a5f042abf636f3b21f45.patch"
kdebug-409980.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/b2d694c75b13bf26fe5f16a1fe35099ca409a18a.patch"
)
depends=(gawk kdelibs4support polkit-kde-agent libxkbfile kmenuedit systemsettings baloo accountsservice xdg-user-dirs)
optdepends=('plasma-nm: Network manager applet'
            'powerdevil: power management, suspend and hibernate support'
            'kscreen: screen management'
            'ibus: kimpanel IBUS support'
            'scim: kimpanel SCIM support'
            'kaccounts-integration: OpenDesktop integration plugin'
            'packagekit-qt5: to install new krunner plugins')
replaces=(user-manager knetattach)
makedepends=(extra-cmake-modules kdoctools xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel
             scim kdesignerplugin kaccounts-integration intltool packagekit-qt5 kinit)
groups=(plasma)
sha256sums=('728e9f8ec670f4f0370c9cc86ede317c682f95f3655bddcf8bb06a38192d4230'
            'SKIP'
            '549071bae66e14edcc257159f6dbccf98b15f411be86da776f34496c47466c78'
            '4f97f3c958f8555c69f9dd483e12bc04a562e278d8625da99cfeecea2f1beee7')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare(){
    msg "Add commit 47c6f8fc patch; fix in 5.23"
    patch -d $pkgname-$pkgver -p1 -i ../commit-47c6f8fc.patch
    msg "Add kdebug-409980 patch; fix in 5.23"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-409980.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
