# Maintanier: pavbaranov <pavbaranov at gmail dot com>
# for version with fixes from upcoming releases
# made upon the original works of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=plasma-desktop
pkgname=(plasma-desktop knetattach)
pkgver=5.15.4
pkgrel=1.1
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-395513.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=43804eb7f15ffc44f43f3edceb160f71b9d855cc"
kdebug-404786.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=cc59e2075340c19165c6c0eec62926d4e55c1c07"
kdebug-387156.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=8ca7335288fa621b987ae3fec4428c6601ceef67"
kdebug-399516.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=763c0cf117bca5039a18c502cb6a6d88bde2281e"
kdebug-398799.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=cf1305a2fb40b96d4b57041bafd57ef44d0534b0"
kdebug-406084.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=4545601adec06a63c7ba471422f11510ff7f1b51"
kdebug-401464.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=dcb4c158b41452d18e629b30b472b6d3294070ef")
depends=(polkit-kde-agent libxkbfile kmenuedit appstream-qt systemsettings ksysguard kpeople baloo)
makedepends=(extra-cmake-modules kdoctools boost xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel
             libibus scim kdesignerplugin)
groups=(plasma)
sha256sums=('0c4900ce7cc46261185bfc4fce2b47dc69a659f6c1fc8d94d770ee6a0d06d028'
            'SKIP'
            '761b91845e47d7f5cae118338d94838273e56b368d5e9f275a86410385349265'
            'dc3b611eabafaa717b8f498d1609155f3e93d09f1397c0d28501772301f0e854'
            '37831cfb62375db69e955f9a83ce37b96a47975a8d254c25f698adf97ea9ac98'
            'a2cf79bd1b0acc401136f8317a766c2e40860891f2d87eebdaa1b55c86b07ee4'
            'c210a62ed331e31e956f16824e38bf44e726e1286247636a8b615cc96a415b79'
            '06988b6f6e50cc11cba884612eca0ec4bad45a3b4115fe9e0e0af40817baf108'
            '5784e828d4ededc0a2e0605fb94f8fcfc5e2e95c535a048987e7c67da22de93d')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply KDEBUG 395513 patch; fixed in 5.16.0"
  patch -p1 -i ../kdebug-395513.patch
  msg "Apply KDEBUG 404786 patch; fixed in 5.16.0"
  patch -p1 -i ../kdebug-404786.patch
  msg "Apply KDEBUG 387156 patch; fixed in 5.16.0"
  patch -p1 -i ../kdebug-387156.patch
  msg "Apply KDEBUG 399516 patch; fixed in 5.16.0"
  patch -p1 -i ../kdebug-399516.patch
  msg "Apply KDEBUG 398799 patch; fixed in 5.16.0"
  patch -p1 -i ../kdebug-398799.patch
  msg "Apply KDEBUG 406084 patch"
  patch -p1 -i ../kdebug-406084.patch
  msg "Apply KDEBUG 401464 patch"
  patch -p1 -i ../kdebug-401464.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make -j5
}

package_plasma-desktop() {
  depends+=(knetattach)
  optdepends=('plasma-nm: Network manager applet'
              'powerdevil: power management'
              'ibus: kimpanel IBUS support'
              'scim: kimpanel SCIM support'
              'discover: manage applications installation from the launcher')

  cd build
  make DESTDIR="$pkgdir" install

# Split knetattach
  rm "$pkgdir"/usr/{bin/knetattach,share/applications/org.kde.knetattach.desktop}
}

package_knetattach() {
  pkgdesc='Wizard which makes it easier to integrate network resources with the Plasma Desktop'
  depends=(kdelibs4support)

  cd build/knetattach
  make DESTDIR="$pkgdir" install
}
