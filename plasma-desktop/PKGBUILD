# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-desktop
pkgver=5.21.1
pkgrel=1.3
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
# 5.21.2 patches:
kdebug-433203.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/6273f0ba5926d775a8db057e7ea1e0d2c8f17f06.patch"
kdebug-433138.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/1d53973d633dcd9073efe6b38d0cafb6344d89d0.patch"
kdebug-433574.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/97944e4f84200ed6a1ca8a60e5a128cc45677030.patch"
kdebug-431923.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/c03bba6eb40bf12f497f05417ef7ab3d40f7edae.patch"
# 5.22 patches:
kdebug-394119.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/6779cfc8b11cee069833748600ceeabab1f791d5.patch"
)
depends=(polkit-kde-agent libxkbfile kmenuedit systemsettings ksysguard baloo libibus accountsservice)
optdepends=('plasma-nm: Network manager applet'
            'powerdevil: power management, suspend and hibernate support'
            'kscreen: screen management'
            'ibus: kimpanel IBUS support'
            'scim: kimpanel SCIM support'
            'kaccounts-integration: OpenDesktop integration plugin')
replaces=(user-manager knetattach)
makedepends=(extra-cmake-modules kdoctools xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel
             scim kdesignerplugin kaccounts-integration intltool)
groups=(plasma)
sha256sums=('a514664e50f3213b8afde0dab9b8d6a2b097238d2aad4dcdd359aaa16ff02fd2'
            'SKIP'
            '6c8193035daf2ad5c5dd30e4ae25bd5760751a734ac6abf7ce3e5a29f23e89e4'
            '0b8e83ab9482bd8cc91ecbe475d2cd2de40640f5bde74d8500b57fa8996aa2d2'
            '16ea3ee4ddf9c725a90c623c922ca22dcf2435fd04527bb1f6d0e873db273e4d'
            '94e4f57eb5c08aef4ed74f7e2cc695f1df25e3943fc17c552679bedfd134f34b'
            '510dd3aaeec37aeb505c556d1f567fe7e7e2a799a273d402ff3719550b315769')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 433203 patch; fix probably in 5.21.2"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433203.patch
    msg "Add kdebug 394119 patch; fix in 5.22"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-394119.patch
    msg "Add kdebug 433138 patch; fix in 5.21.2"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433138.patch
    msg "Add kdebug 433574 patch; fix in 5.21.2"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433574.patch
    msg "Add kdebug 431923 patch; fix in 5.21.2"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-431923.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
