# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-desktop
pkgver=5.21.0
pkgrel=2.1
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-431923.patch::"https://invent.kde.org/plasma/plasma-desktop/-/merge_requests/353.patch"
kdebug-433026.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/e86a68a5cb168d896393d6548b22ceacb839c82e.patch")
depends=(polkit-kde-agent libxkbfile kmenuedit systemsettings ksysguard baloo libibus accountsservice)
optdepends=('plasma-nm: Network manager applet'
            'powerdevil: power management, suspend and hibernate support'
            'kscreen: screen management'
            'ibus: kimpanel IBUS support'
            'scim: kimpanel SCIM support'
            'kaccounts-integration: OpenDesktop integration plugin')
replaces=(user-manager knetattach)
makedepends=(extra-cmake-modules kdoctools xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel
             scim kdesignerplugin kaccounts-integration intltool)
groups=(plasma)
sha256sums=('ebc5d4021236741b5bcee36238f30bdb6a7b3c37fff4158c3e3fd1379281d45e'
            'SKIP'
            '246ba4ed3501c3ecbcaf871f6327aa9214d8afbc87e29160466e86a59060396e'
            'db654353613aeda4dc10e7cc7febae4511da3abd0a1d9613e43fde9d5ada5419')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    patch -d $pkgname-$pkgver -p1 < kdebug-431923.patch # Fix keyboard repeat setting migration
    msg "Add kdebug-433026 patch; fix in 5.21.1"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433026.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
