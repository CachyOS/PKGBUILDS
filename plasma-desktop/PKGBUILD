# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: sirlucjan

pkgbase=plasma-desktop
pkgname=(plasma-desktop knetattach)
pkgver=5.19.5
pkgrel=1.1
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-419718.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/075a43667cfd75f36ca3430ca87518228617aaa5.patch"
kdebug-414546.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/923e797209a3799cb600f9b39abff3558fa8f959.patch"
kdebug-357790.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/8868bb835d23afd3caf46ade3a3236421c492ba8.patch"
provide-components-for-supporting-thumbnails-on-Wayland.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/6dbf8d7911be05cd15527b2f07dee2e988ffdc06.patch"
)
depends=(polkit-kde-agent libxkbfile kmenuedit systemsettings ksysguard baloo libibus)
makedepends=(extra-cmake-modules kdoctools xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel
             scim kdesignerplugin)
groups=(plasma)
sha256sums=('c63e1c81edc438656f9159e235be31e3b1b11a3f8ecbe5b97b21fcc91eb71a70'
            'SKIP'
            '5588106a44ff13d2c1669cfd818595efd2d9882355ce5cc56c85a3735e8f2f2d'
            'd1a0962e9732a039eba6f0c2a85f4bd6c94ea443754a5422250ed726eeaaa051'
            'f1a7b82da26c0247fe7f6e02ef53b533c8761f109a8c2b1538e4b585e4c76696'
            '64ee65673e7afa8c7ab992ef0968a705f13c813d2353d981b97f1fb25a58548f')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 419718 patch; fix in 5.20"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-419718.patch
    msg "Add kdebug 414546 patch; fix in 5.20"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-414546.patch
    msg "Add kdebug 357790 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-357790.patch
    msg "Add provide-components-for-supporting-thumbnails-on-Wayland patch"
    patch -d $pkgname-$pkgver -p1 -i ../provide-components-for-supporting-thumbnails-on-Wayland.patch
}
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_plasma-desktop() {
  depends+=(knetattach)
  optdepends=('plasma-nm: Network manager applet'
              'powerdevil: power management, suspend and hibernate support'
              'kscreen: screen management'
              'ibus: kimpanel IBUS support'
              'scim: kimpanel SCIM support')

  DESTDIR="$pkgdir" cmake --install build

# Split knetattach
  rm "$pkgdir"/usr/{bin/knetattach,share/applications/org.kde.knetattach.desktop}
}

package_knetattach() {
  pkgdesc='Wizard which makes it easier to integrate network resources with the Plasma Desktop'
  depends=(kdelibs4support)

  DESTDIR="$pkgdir" cmake --install build/knetattach
}
