# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with buggixes and some improve patches from upcomming version
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-desktop
pkgver=5.25.2
pkgrel=1.2
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
# 5.25.3 fixes
kdebug-455927.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/bc6c5fee3683fc1cf44f971ab5aa02fd1f5b8442.patch"
kdebug-451233.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/c6b50001166dbf69ee5d3c00283adffcb6219370.patch"
kdebug-455624.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/88f624c0056f90ec307eb7631488b87ddac3021e.patch"
kdebug-448526.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/9c9b53b6bf021ddc8709a34db998fb21f4138f61.patch"

# 5.26 fixes
kdebug-455770.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/1a0150d726adacddbfa588844171c116c6bffc87.patch" #fix in 5.26

)
depends=(gawk kdelibs4support polkit-kde-agent kmenuedit systemsettings baloo xdg-user-dirs libibus)
optdepends=('plasma-nm: Network manager applet'
            'powerdevil: power management, suspend and hibernate support'
            'kscreen: screen management'
            'ibus: kimpanel IBUS support'
            'scim: kimpanel SCIM support'
            'kaccounts-integration: OpenDesktop integration plugin'
            'packagekit-qt5: to install new krunner plugins')
makedepends=(extra-cmake-modules kdoctools xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel
             scim kdesignerplugin kaccounts-integration intltool packagekit-qt5 kinit)
groups=(plasma)
sha256sums=('41e5c8269e24b9468ee61dd0451aaa9766ee94445cb2d635484ffd9e04ecf626'
            'SKIP'
            'be1ae732a109bf5c41bc5b7244093985aa056a027313b8f64aab656af6928ce2'
            '27a71c69e817c6b1740e0fd7734198c868788eda8e6efa31f3f88fd895df9920'
            'a44a2ce8bb3250f99d72cc2476f1cc16f3fca06ed9f8f568b232d19d09eb2d47'
            'cd62c01165ebaa30447b5c1b28b577aea8d322b1d5dbe90f827757670f4ba0a8'
            'e1d68ad8fdc0dec77a9381fbc68f7ecdd2b5c280eceded8033d958f190a6e70d')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
options=(debug)

prepare() {
cd $pkgname-$pkgver
### Patching sources
        local src
        for src in "${source[@]}"; do
          src="${src%%::*}"
          src="${src##*/}"
          [[ $src = *.patch ]] || continue
          echo "Applying patch $src..."
          patch -Np1 < "../$src"
        done
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
