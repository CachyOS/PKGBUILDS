# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-desktop
pkgver=5.23.5
pkgrel=2.1
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
    kdebug-438246.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/297958c486c77ad9e557893a58f8b22ef8a06c58.patch"
    kdebug-433907.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/ec2d3d56a7eb0ac7bd6eb2a3fb86038fad0f000e.patch"
    # kdebug-443465.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/a4c711a411f47e11c5327efa0dd40c12b26875e5.patch"
    kdebug-425852.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/423c1b69909af11a06f58505ccff6d563a6d8006.patch"
    https://invent.kde.org/plasma/plasma-workspace/-/commit/efddc639.patch
)
depends=(gawk kdelibs4support polkit-kde-agent kmenuedit systemsettings baloo accountsservice xdg-user-dirs)
optdepends=('plasma-nm: Network manager applet'
            'powerdevil: power management, suspend and hibernate support'
            'kscreen: screen management'
            'ibus: kimpanel IBUS support'
            'scim: kimpanel SCIM support'
            'kaccounts-integration: OpenDesktop integration plugin'
            'packagekit-qt5: to install new krunner plugins')
replaces=(user-manager knetattach)
makedepends=(extra-cmake-modules kdoctools xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel
             scim kdesignerplugin kaccounts-integration intltool packagekit-qt5 kinit)
groups=(plasma)
sha256sums=('b3905877a32dbe36b923d8624b9d7ac0174071f7ec08bf057f3e94c1b566a87a'
            'SKIP'
            '4e2ef359ac10c9a736f6589be0bdc20551c5e8f31f7dbe61cdd69c2465a7856f'
            '8989834d783caf1b0cf55f1069255311f23476949b33e3decea6eb7b9e549a4b'
            '84f7e13a775b79fc654a5244e588643940e243c2f990fcb5448ad6010f0f8306'
            'b8eef316fbea8275065a630a7e6f8b26b1e464790e690deac2fa3705ff5270cb')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

              
prepare() {
    msg "Add kdebug 438246 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-438246.patch
    msg "Add kdebug 433907 patch; fix in 5.24"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433907.patch
    # msg "Add kdebug 443465 patch; fix in 5.24"
    # patch -d $pkgname-$pkgver -p1 -i ../kdebug-443465.patch
    msg "Add kdebug 425852 patch; fix in 5.24"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-425852.patch
    patch -d $pkgname-$pkgver -p1 < efddc639.patch # Fix authentication prompt in users KCM
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
