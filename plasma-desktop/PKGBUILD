# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcoming releases
# made upon the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=plasma-desktop
pkgname=(plasma-desktop knetattach)
pkgver=5.18.1
pkgrel=1.5
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-416553.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=a35d2b9adf40072e242b8820dcb07cf0a7ed037b"
        kdebug-415649.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=c32b5a85cae6f99bb5097b16a25983fc611aecc6"
        kdebug-414109.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=ca9f0eeb9b5377a598c6ec58aa1a2beaa08d69c0"
        kdebug-397595.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=6d3d3ffa76a5125da843fe6f58a684bdba6e6d26"
        kdebug-417461.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=fffb352cfa1eb5e18f038d5b423d2dce0dca8727"
        kdebug-417276.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=8f1043c641a2edd86fb6139e0a5cfe108d64a69d")
depends=(polkit-kde-agent libxkbfile kmenuedit systemsettings ksysguard baloo)
makedepends=(extra-cmake-modules kdoctools xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel
             libibus scim kdesignerplugin)
groups=(plasma)
sha256sums=('588b228dab889b13f2942f8682fdab1765bae97f2c057fec4277be21c37aa912'
            'SKIP'
            '674976dfb7b78c66f489d4f04bafcad395c0dddff439fb7bef386cb7cd3a86ee'
            '2d83b72c1fa5e44a68140ec981eb2d9ea2e39270fc7246910e12341df31b3a92'
            'ed62ef6bae189b063a5ecd8b19069ae1c4f9d43689653b199ae919bc8e94bd43'
            '92a91fc8407668192bcfa3024a3ab51cc9a1277f3843c704480acbfb15c79892'
            '9dc1158dadf84731d298e1d3f3fbb64f3b02df95e04f584f88093aebe43e7a4f'
            'c3cfe594e7398cc2d8b58a0b98eea849b5231d539cf5aa98b8f2ca0ccae90081')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Applying kdebug 416553.patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-416553.patch
  msg "Applying kdebug 415649 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-415649.patch
  msg "Applying kdebug 414109 patch; fix in 5.18.2"
  patch -p1 -i ../kdebug-414109.patch
  msg "Applying kdebug 397595 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-397595.patch
  msg "Applying kdebug 417461 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-417461.patch
  msg "Applying kdebug 417276 patch; fix probably in 5.19.0"
  patch -p1 -i ../kdebug-417276.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-desktop() {
  depends+=(knetattach)
  optdepends=('plasma-nm: Network manager applet'
              'powerdevil: power management, suspend and hibernate support'
              'kscreen: screen management'
              'ibus: kimpanel IBUS support'
              'scim: kimpanel SCIM support')

  cd build
  make DESTDIR="$pkgdir" install

# Split knetattach
  rm "$pkgdir"/usr/{bin/knetattach,share/applications/org.kde.knetattach.desktop}
}

package_knetattach() {
  pkgdesc='Wizard which makes it easier to integrate network resources with the Plasma Desktop'
  depends=(kdelibs4support)

  cd build/knetattach
  make DESTDIR="$pkgdir" install
}
