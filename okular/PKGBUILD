# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcomming releases
# made on the work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=okular
pkgver=18.04.1
pkgrel=1.1
pkgdesc='Document Viewer'
arch=(x86_64)
url="https://kde.org/applications/graphics/okular/"
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(djvulibre libspectre libkexiv2 poppler-qt5 qca-qt5 kpty kactivities threadweaver kjs kparts purpose)
makedepends=(extra-cmake-modules ebook-tools kdegraphics-mobipocket kdoctools khtml python chmlib discount)
optdepends=('ebook-tools: mobi and epub support' 'discount: markdown support'
            'kdegraphics-mobipocket: mobi support' 'kirigami2: mobile UI' 'libzip: CHM support'
            'khtml: CHM support' 'chmlib: CHM support' 'calligra: ODT and ODP support')
conflicts=(kdegraphics-okular)
replaces=(kdegraphics-okular)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-389836.patch::"https://cgit.kde.org/okular.git/patch/?id=cbc6f671e35cc901a8edcc0cadda2459c0277485"
kdebug-388532.patch::"https://cgit.kde.org/okular.git/patch/?id=48b9ca66a6fd8892c2f62b34ad56ce66c817e45c"
kdebug-355043.patch::"https://cgit.kde.org/okular.git/patch/?id=004efa70e4a764bbd780d4f3e5bcaf40de70a1a6")

sha256sums=('0fa82f9a31de184966303395148dd03f627c64be2d5a2eb070bf13c5a95c0e3a'
            'SKIP'
            'e86b0c6ef9d88e7eae4b152344a721b495be9106dea212edede59ce4f5e8fe72'
            'ef1f4c00d46863d5811af05c4791c7dc2971fa43e90141764feb4b14fdc8fc0b'
            '77a89527e076a1b8d8fc436c874ed76230bd0fd7dac95a25d2b1e926024d680e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 389836 patch"
  patch -p1 -i ../kdebug-389836.patch
  msg "Add KDEBUG 388532 patch"
  patch -p1 -i ../kdebug-388532.patch
  msg "Add KDEBUG 355043 patch"
  patch -p1 -i ../kdebug-355043.patch
  }

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
