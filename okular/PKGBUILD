# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orignal works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=okular
pkgver=20.08.2
pkgrel=1.1
pkgdesc='Document Viewer'
arch=(x86_64)
url="https://kde.org/applications/graphics/okular/"
license=(GPL LGPL FDL)
groups=(kde-applications kde-graphics)
depends=(djvulibre libspectre libkexiv2 poppler-qt5 qca kpty kactivities threadweaver kjs kparts purpose discount phonon-qt5)
makedepends=(extra-cmake-modules ebook-tools kdegraphics-mobipocket kdoctools khtml chmlib)
optdepends=('ebook-tools: mobi and epub support'
            'kdegraphics-mobipocket: mobi support' 'libzip: CHM support'
            'khtml: CHM support' 'chmlib: CHM support' 'calligra: ODT and ODP support'
            'unrar: Comic Book Archive support' 'unarchiver: Comic Book Archive support (alternative)')
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
remove-artifact.patch::"https://invent.kde.org/graphics/okular/-/commit/5d947905f754ad0c11b1c77e4a34a2ecefa75802.patch"
fix-dangling-annotation-tooltip.patch::"https://invent.kde.org/graphics/okular/-/commit/e238f21bbbad4d139217924a0b5fd055b7c396d3.patch"
kdebug-406831.patch::"https://invent.kde.org/graphics/okular/commit/b696d6dbfb8453948010ed786d9ab24bd3d1b485.patch"
kdebug-186797.patch::"https://invent.kde.org/graphics/okular/-/commit/837a9c1f52a373ddfc441a83aa9abfceda03c7e2.patch"
)
sha256sums=('6f1885ed8050a55bb2cbf05089b452f555852e003ec7fe89fe472c0dbc92e65b'
            'SKIP'
            '5da9747adfe0f6f45f87d191193adf8657d5a73710eba3822e1ce2869b65eecb'
            '3ff366b9b6fb53851fadb7a4900c5db8ca81368c5d6d842783b8fb5f26e36d1a'
            '8a86b6ced4bb58defaaf87b0735e2b64199b0adee1154e759247a33a321a2f42'
            '4ec9fe0d16aa643c03ca513447e7bf2aa99a891ec7e05bbc791b2ff3351ac3bf')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>
options=(!zipman)

prepare() {
    msg "Add Remove artifact while navigating though pages patch"
    patch -d $pkgname-$pkgver -p1 -i ../remove-artifact.patch
    msg "Add Fix dangling annotation tooltip patch"
    patch -d $pkgname-$pkgver -p1 -i ../fix-dangling-annotation-tooltip.patch
    msg "Add kdebug 406831 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-406831.patch
#    msg "Add kdebug 186797 patch; fix in 20.12.0"
#    patch -d $pkgname-$pkgver -p1 -i ../kdebug-186797.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
