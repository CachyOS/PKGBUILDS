# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=okular
pkgver=20.04.2
pkgrel=1.1
pkgdesc='Document Viewer'
arch=(x86_64)
url="https://kde.org/applications/graphics/okular/"
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(djvulibre libspectre libkexiv2 poppler-qt5 qca kpty kactivities threadweaver kjs kparts purpose discount phonon-qt5)
makedepends=(extra-cmake-modules ebook-tools kdegraphics-mobipocket kdoctools khtml chmlib)
optdepends=('ebook-tools: mobi and epub support'
            'kdegraphics-mobipocket: mobi support' 'libzip: CHM support'
            'khtml: CHM support' 'chmlib: CHM support' 'calligra: ODT and ODP support'
            'unrar: Comic Book Archive support' 'unarchiver: Comic Book Archive support (alternative)')
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-419096.patch::"https://cgit.kde.org/okular.git/patch/?id=d735d50cdea61cefc2413637ca164d0b33221eaa"
kdebug-421163.patch::"https://cgit.kde.org/okular.git/patch/?id=b1c02b7e79c8be1716ddba73d92d5792032821c6"
kdebug-421468.patch::"https://cgit.kde.org/okular.git/patch/?id=7eb37c99c9cd27bfd918dffe2dca7b9ec22ab5fb"
# kdebug-415199.patch::"https://cgit.kde.org/okular.git/patch/?id=4b1d85f81bf0df1fdfc86e74ad71e9cf8201092f"
#kdebug-421004.patch::"https://cgit.kde.org/okular.git/patch/?id=390a88411acc9893425789cee9963709255d25c5"
kdebug-421508.patch::"https://invent.kde.org/graphics/okular/commit/d78cbb79c98e01fc524ec91fb099ec8ff98cc1fa.patch"
)
sha256sums=('b783aaac1661d1d8ec5c5e26fdec7035a6b0241a21d18caac1587ecfde44c49b'
            'SKIP'
            '55681350e2a7acfeb1121f2a2829b0680c74f3237e1a1faebfccfc5ce0650328'
            '503d3f7fa0a067b426f22d2053724f29f69d75c61c1d34ac5b925e14689fd859'
            '56efefb738e15fe13691578bbcc6b6fbc6a11ac7e3f9be3d6264a22c45385dfd'
            '891d30e8f263910266abc87bd70490297e22139872e98cd6b3fee0e180dfffc3')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>
options=(!zipman)

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add kdebug 419096 patch"
  patch -p1 -i ../kdebug-419096.patch
  msg "Add kdebug 421163 patch"
  patch -p1 -i ../kdebug-421163.patch
  msg "Add kdebug 421468 patch"
  patch -p1 -i ../kdebug-421468.patch
#  msg "Add kdebug 415199 patch"
#  patch -p1 -i ../kdebug-415199.patch
#  msg "Add kdebug 421004 patch"
#  patch -p1 -i ../kdebug-415199.patch
  msg "Add kdebug 421508 patch"
  patch -p1 -i ../kdebug-421508.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
