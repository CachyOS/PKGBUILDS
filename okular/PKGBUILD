# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=okular
pkgver=21.08.3
pkgrel=1.1
pkgdesc='Document Viewer'
arch=(x86_64)
url='https://apps.kde.org/okular/'
license=(GPL LGPL FDL)
groups=(kde-applications kde-graphics)
depends=(djvulibre libspectre libkexiv2 poppler-qt5 kpty kactivities threadweaver kjs kparts purpose discount phonon-qt5)
makedepends=(extra-cmake-modules ebook-tools kdegraphics-mobipocket kdoctools khtml chmlib)
optdepends=('ebook-tools: mobi and epub support'
            'kdegraphics-mobipocket: mobi support' 'libzip: CHM support'
            'khtml: CHM support' 'chmlib: CHM support' 'calligra: ODT and ODP support'
            'unrar: Comic Book Archive support' 'unarchiver: Comic Book Archive support (alternative)')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
    kdebug-438137.patch::"https://invent.kde.org/graphics/okular/-/commit/e43cdc0d868c9c2068665af78358e8a30845e445.patch"
    kdebug-444971.patch::"https://invent.kde.org/graphics/okular/commit/f686f4c6bf45b92784a6bf90a037199158ff0073.patch"
)
sha256sums=('39b922d2d7c6916025bfceacf8610214cead665369285d7a6904a434fe81f001'
            'SKIP'
            '0f9335149988174138e3ad1e304b4f13cb0a4e8351e03b18861cf8db98c9efbd'
            'f83d54b931fbe1892ab7ed72c35effff0493b9e57b2900e21133f0d5193619ee')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(!zipman)

prepare() {
    # msg "Add kdebug 438137 patch; fix in 21.12"
    # patch -d $pkgname-$pkgver -p1 -i ../kdebug-438137.patch
    msg "Add kdebug 444971 patch; fix in 21.12"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-444971.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
