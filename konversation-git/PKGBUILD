# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: Sergio Jovani Guzman <moret@paretux.org>

pkgname=konversation-git
pkgver=1.7.master.5113.r8641.1a5e7ab4
pkgrel=1
pkgdesc="A user friendly IRC client for KDE. (GIT Version)"
arch=('x86_64')
url='https://kde.org/applications/en/internet/org.kde.konversation'
license=('GPL2' 'FDL')
makedepends=(git extra-cmake-modules kdoctools)
depends=(knotifyconfig kemoticons kparts kidletime qca-qt5 hicolor-icon-theme phonon-qt5 perl)
optdepends=('python: python scripting support' 'keditbookmarks: for managing bookmarks')
conflicts=(konversation)
provides=(konversation)
source=('git+https://invent.kde.org/network/konversation.git'
        'po.tar.gz')
sha256sums=('SKIP'
            '206a03dd5adf8da76620b28bf51bac3b2c17c789e9d381318adb59486befd34d')

pkgver() {
  cd ${pkgname%-*}
  _ver="$(cat src/version.h | grep "#define KONVI_VERSION" | cut -d '"' -f2 | tr - .).$(cat src/commit.h | grep "#define COMMIT" | cut -d ' ' -f3)"
  echo "${_ver}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    mv po ${pkgname%-*}
}

build() {
  cmake -B build -S ${pkgname%-*} \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
