# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kconfigwidgets
pkgver=5.79.0
pkgrel=1.1
pkgdesc='Widgets for KConfig'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kauth kcodecs kconfig kguiaddons ki18n kwidgetsaddons)
makedepends=(extra-cmake-modules kdoctools clang python-pyqt5 doxygen qt5-tools qt5-doc sip)
optdepends=('python-pyqt5: for the Python bindings' 'perl: for preparetips5')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-421391.patch::"https://invent.kde.org/frameworks/kconfigwidgets/commit/6fe641001ff2be40849568b2c3b0c0f90348133f.patch")
sha256sums=('7ea01c69328f460a1e70131626d00a7a668814ab415dca7692dbc6a1631330d3'
            'SKIP'
            '6ad1b77df811954f648946017276cfd46bc098b95d43f0803e301b785d9bd753')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 421391 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-421391.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
