# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=20.12.3
pkgrel=1.1
pkgdesc='UML modeller'
arch=(x86_64)
url='https://kde.org/applications/development/umbrello'
license=(GPL)
depends=(kdelibs4support kdevelop-php qt5-webkit)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin kdevelop-pg-qt llvm kinit doxygen)
groups=(kde-applications kdesdk)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-430720.patch::"https://invent.kde.org/sdk/umbrello/commit/bb0e5c956daa2803071e8a7d78b5f906d2b5343d.patch"
kdebug-430593.patch::"https://invent.kde.org/sdk/umbrello/commit/afe54199882d78ced2a04d520122a573b72151e9.patch"
kdebug-430323.patch::"https://invent.kde.org/sdk/umbrello/commit/a81599106a13b6263e32902d05ed0942341509d6.patch"
# kdebug-363582.patch::"https://invent.kde.org/sdk/umbrello/commit/b0f4278b08330f8fe5729de1a516a8c3d470e1aa.patch"
kdebug-429794.patch::"https://invent.kde.org/sdk/umbrello/commit/eae999b7903c30a7570ba7f5e907801cd399cbc4.patch"
kdebug-429181.patch::"https://invent.kde.org/sdk/umbrello/commit/97ed260f37ad4040bf9b78db1c1259a4b5d82022.patch"
# kdebug-420073.patch::"https://invent.kde.org/sdk/umbrello/commit/2bb95adcdac2b4211466fd1d7bc5425f89e12752.patch"
)
sha256sums=('57fb7d988e115be18514f807bab483a19877acc3f1910aeb952c4d8372f4d378'
            'SKIP'
            '7fd41930e809ce4a760b156a924ad898245b120b20d41ae17aa7480743b88159'
            '0a29a850f080918492254b37e3c84b660d1ed43124478e5d53866c59f8b03fa2'
            '608258dc19b5edc35b006e98422abfc04e0037894612fd0b31a1f152f1d6237d'
            '05eea88c5e6e865fce6f349536fa5b8101b1d0c8dfc424c10384d928136ccf34'
            'c6a319efd4e8dde0315e5aff5643828a3a7586e795a28b59b2da57d9e5a3a998')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare(){
    msg "Added kdebug 430720 patch; Fix in 2.30.80"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-430720.patch
    msg "Added kdebug 430593 patch; Fix in 2.30.80"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-430593.patch
    msg "Added kdebug 430323 patch; Fix in 2.30.80"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-430323.patch
    # msg "Added kdebug 363582 patch; Fix in 2.30.80"
    # patch -d $pkgname-$pkgver -p1 -i ../kdebug-363582.patch
    msg "Added kdebug 429794 patch; Fix in 2.30.80"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-429794.patch
    msg "Added kdebug 429181 patch; Fix in 2.30.80"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-429181.patch
    # msg "Added kdebug 420073 patch; Fix in 2.30.80"
    # patch -d $pkgname-$pkgver -p1 -i ../kdebug-420073.patch
}

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_UNITTESTS=OFF \
    -DBUILD_KF5=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
