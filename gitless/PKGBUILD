# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: epitron <chris@ill-logic.com>

pkgname=gitless
pkgver=0.8.7
pkgrel=5
pkgdesc="A scientifically proven easier-to-use git interface"
arch=("any")
url="http://gitless.com/"
license=("GPL")
depends=('python' 'git' 'python-sh' 'python-clint' 'python-pygit2>=0.28.1' 'python-setuptools')
source=("https://github.com/sdg-mit/gitless/archive/v$pkgver.tar.gz"
        "0001-Bump-to-pygit2-0.28.1.patch")
sha256sums=('f5cdc28dfd26ca839405b726eccadfe3a8dc91a8a8c88788f8941fa336f199d6'
            '73e2fbadf39cca72182817df84d50ff5f8fbbecb500e160d3848271562017420')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ../0001-Bump-to-pygit2-0.28.1.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python setup.py install --root=${pkgdir} --optimize=1 --skip-build
}
