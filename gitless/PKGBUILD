# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: epitron <chris@ill-logic.com>

pkgname=gitless
pkgver=0.8.8
pkgrel=7
pkgdesc="A scientifically proven easier-to-use git interface"
arch=("any")
url="http://gitless.com/"
license=("GPL")
depends=('python' 'git' 'python-sh' 'python-clint' 'python-pygit2>=1.1.0' 'python-setuptools')
source=("https://github.com/gitless-vcs/gitless/archive/v$pkgver.tar.gz"
        '0001-Update-libgit2.patch')
sha256sums=('470aab13d51baec2ab54d7ceb6d12b9a2937f72d840516affa0cb34a6360523c'
            'f4398171b45f4f6b8772e1e50dc22c5e74873d3769594c20b88ad4a6c2fe2aba')

prepare() {
  cd ${pkgname}-${pkgver}
  # Bump libgit2
  patch -p1 -i ../0001-Update-libgit2.patch
}
            
build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root=${pkgdir} --optimize=1 --skip-build
}
