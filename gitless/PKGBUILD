# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Contributor: epitron <chris@ill-logic.com>

pkgname=gitless
pkgver=0.8.8
pkgrel=4
pkgdesc="A scientifically proven easier-to-use git interface"
arch=("any")
url="http://gitless.com/"
license=("GPL")
depends=('python' 'git' 'python-sh' 'python-clint' 'python-pygit2>=1.0.0' 'python-setuptools')
source=("https://github.com/sdg-mit/gitless/archive/v$pkgver.tar.gz"
        '0001-Bump-to-pygit-1.0.0.patch')
sha256sums=('470aab13d51baec2ab54d7ceb6d12b9a2937f72d840516affa0cb34a6360523c'
            '3935c12f4b2b60717976bd9ebb79647ca4715275bca9a89f3073e3bdacb04cb3')

prepare() {
  cd ${pkgname}-${pkgver}
  # Bump pygit2
  patch -p1 -i ../0001-Bump-to-pygit-1.0.0.patch
}
            
build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root=${pkgdir} --optimize=1 --skip-build
}
