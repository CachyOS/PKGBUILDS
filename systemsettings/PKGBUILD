# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=systemsettings
pkgver=5.20.2
pkgrel=1.2
pkgdesc='KDE system manager for hardware, software, and workspaces'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-workspace)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-427935.patch::"https://invent.kde.org/plasma/systemsettings/commit/fcdfc86d40794621e80c9c05be0b96f3f6211a5a.patch"
kdebug-377309.patch::"https://invent.kde.org/plasma/systemsettings/commit/48b0e798a4513e85899e2af3560fb268fbee9ee9.patch"
kdebug-427869.patch::"https://invent.kde.org/plasma/systemsettings/commit/cee4ba93c659aa7e5bc62cac5a1ca959fcc5241c.patch"
kdebug-427806.patch::"https://invent.kde.org/plasma/systemsettings/commit/35db75d5618282c39d5ed17fcb8937969761a670.patch"
)
sha256sums=('0c7e044becc4d492fe5a313911512d7dccc08a4cd7aabfc0bf7cd878240cb6da'
            'SKIP'
            'bcd87ce5f192300769593daababbd06aa06651162122cb32b3838becf404db75'
            'f3e3142fa77238efa91c1100704c14f822ccdd0514a1abe05cf980bce8b91416'
            'e6a9f0e1d65bb223b616622c3aa7826014a0278e423d24fc6d0f8ced3e14e040'
            'f524745585833a4fee500d8f86898c8e9e16fe11f3e4d322d7801013eb26e294')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 427935 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-427935.patch
    msg "Add kdebug 377309 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-377309.patch
    msg "Add kdebug 427869 patch; fix in 5.20.3"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-427869.patch
    msg "Add kdebug 427806 patch; fix in 5.20.3"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-427806.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
