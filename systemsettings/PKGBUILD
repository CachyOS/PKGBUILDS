# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=systemsettings
pkgver=5.14.3
pkgrel=1.2
pkgdesc='KDE system manager for hardware, software, and workspaces'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kactivities-stats plasma-workspace)
makedepends=(extra-cmake-modules kdoctools khtml)
optdepends=('khtml: for the tree view')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-342938.patch::"https://cgit.kde.org/systemsettings.git/patch/?id=ed4367d86a6324023ba17624b6731f861f68f173")
sha256sums=('ecde7eb26b02b2c1b355e888cf363388f6c382568d059c98ca95bd60c98a46cf'
            'SKIP'
            '3a2805d712831a032c18530735b7ee6bfb7597a30ab6e87afefd001b6ff38104')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  cd $pkgname-$pkgver
  msg "Add KDEBUG 342938 patch" # Fix in 5.15.0
  patch -p1 -i ../kdebug-342938.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
