# Maintanier: pavbaranov <pavbaranov at gmail dot com>
# for version with fixes from upcoming releases
# made upon the original works of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=systemsettings
pkgver=5.15.4
pkgrel=1.1
pkgdesc='KDE system manager for hardware, software, and workspaces'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kactivities-stats plasma-workspace)
makedepends=(extra-cmake-modules kdoctools khtml)
optdepends=('khtml: for the tree view')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-386748.patch::"https://cgit.kde.org/systemsettings.git/patch/?id=8b2e8d7eb61a82b61df40489eb09ce297a21c3eb"
)
sha256sums=('2cff11f842266296192381a214df6fd55debfd444b754b6dec80f70a3154defb'
            'SKIP'
            '40a6509477e93c5d8b1a7d1dc420c7f82d38220b45d6223f4df208658e978c76')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply KDEBUG 386748 patch; fix in 5.15.5"
  patch -p1 -i ../kdebug-386748.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
