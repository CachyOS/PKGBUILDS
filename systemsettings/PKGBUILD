# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=systemsettings
pkgver=5.20.3
pkgrel=1.1
pkgdesc='KDE system manager for hardware, software, and workspaces'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-workspace)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-427935.patch::"https://invent.kde.org/plasma/systemsettings/commit/fcdfc86d40794621e80c9c05be0b96f3f6211a5a.patch"
kdebug-377309.patch::"https://invent.kde.org/plasma/systemsettings/commit/48b0e798a4513e85899e2af3560fb268fbee9ee9.patch"
)
sha256sums=('c380df3cedf14ed11d313b10eba22824cad2aaa3460e0d30336c454eba5c823a'
            'SKIP'
            'bcd87ce5f192300769593daababbd06aa06651162122cb32b3838becf404db75'
            'f3e3142fa77238efa91c1100704c14f822ccdd0514a1abe05cf980bce8b91416')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 427935 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-427935.patch
    msg "Add kdebug 377309 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-377309.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
