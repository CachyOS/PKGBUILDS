# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=systemsettings
pkgver=5.21.0
pkgrel=1.1
pkgdesc='KDE system manager for hardware, software, and workspaces'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(plasma-workspace)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
#kdebug-432083.patch::"https://invent.kde.org/plasma/systemsettings/commit/54da94bc12247251f778a5b6757bcdb332aa2a4e.patch"
kdebug-433062.patch::"https://invent.kde.org/plasma/systemsettings/commit/14b5ff1e99735b94ec83a6e2888216c4f7f65367.patch"
)
sha256sums=('830086905ae49d253d6ac48dbe18667a66d0ed2f4dec896f96d96024f8c2e30a'
            'SKIP'
            'df88541b8a0433077f4bb18731c66dce8a1de174bd4be118086e80457bee2a4b')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
 #   msg "Add kdebug 432083 patch; fix in 5.22"
 #   patch -d $pkgname-$pkgver -p1 -i ../kdebug-432083.patch
    msg "Add kdebug 433062 patch; fix in 5.21.1"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433062.patch
    }

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
