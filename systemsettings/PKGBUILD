# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# made upon the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=systemsettings
pkgver=5.13.3
pkgrel=1.3
pkgdesc='KDE system manager for hardware, software, and workspaces'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kactivities-stats plasma-workspace)
makedepends=(extra-cmake-modules kdoctools khtml)
optdepends=('khtml: for the tree view')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-394553.patch::"https://cgit.kde.org/systemsettings.git/patch/?id=6ce66025e976b3629197eff46abad5daf75a217e"
fix-sidebar-search.patch::"https://cgit.kde.org/systemsettings.git/patch/?id=1cb8b2af21df94763f44cce757e84785ebf2d7ba"
kdebug-355818.patch::"https://cgit.kde.org/systemsettings.git/patch/?id=6f032226cebeaf7459382664f8fbb549ac6d4e3c")
sha256sums=('1a93b739a17bd6fb5ff0ccaa3521cf1a2284d57f2552c843b463553ed2338c50'
            'SKIP'
            '84e742431bc2d33cb97c5f4fc03f48e021e7d59c13a915881d4da86bf57b086a'
            '731915dbe41bfc204c408121c66f4ede217191d74624f25e8d35446d0e06cd81'
            '17eb4466729b4c54a0e8c0d09cc6dc1c7dc3a4e3edbe43365ec8ece798781e68')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 394553 patch"
  patch -p1 -i ../kdebug-394553.patch
  msg "Add fix-sidebar-search patch"
  patch -p1 -i ../fix-sidebar-search.patch
  msg "Add KDEBUG 355818 patch"
  patch -p1 -i ../kdebug-355818.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
