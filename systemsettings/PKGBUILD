# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with fixes from upcomming version
# made on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=systemsettings
pkgver=5.12.1
pkgrel=1.1
pkgdesc='KDE system settings'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kirigami2 kcmutils kactivities-stats qt5-quickcontrols)
makedepends=(extra-cmake-modules kdoctools python khtml)
optdepends=('khtml: for the tree view')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-384638.patch::"https://cgit.kde.org/systemsettings.git/patch/?id=6f5b6e41ec4dec")
sha256sums=('1b1a2a9fdabfe6a12a3987a052dbc077458845869b75da14525c888df462fadb'
            'SKIP'
            '29c6a4424b1cc6a63bca837ce538d3385f241644a68d60458d4cd85ef7897f30')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 384638 patch" # See: https://bugs.kde.org/show_bug.cgi?id=384638 Fixed in 5.12.2
  patch -p1 -i ../kdebug-384638.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
