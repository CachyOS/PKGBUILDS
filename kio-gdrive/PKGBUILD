# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kio-gdrive
pkgver=1.2.1
pkgrel=1.1
arch=(x86_64)
pkgdesc="KIO Slave to access Google Drive"
url="https://community.kde.org/KIO_GDrive"
license=(GPL)
depends=(libkgapi kaccounts-providers)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools python intltool)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
kdebug-376735.patch::"https://cgit.kde.org/kio-gdrive.git/patch/?id=f89c3f7fc4eb98665bf37eb0a0785bb5c2c1566d")
sha256sums=('d0c778538dc20412a944ccbd48fd99dc5f531e4df914bf8cbdf813371e857ca3'
            'SKIP'
            '9644054796a2a2c90969979d968ece23b559ba21c2d0b04b85e609e73975d64e')
validpgpkeys=('F07D85CAA18ACF46A346FD017C7FC6EA8633B4EA') # Elvis Angelaccio <elvis.angelaccio@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG 376735 patch" # See: https://bugs.kde.org/show_bug.cgi?id=376735 Fixed in 1.2.2
  patch -p1 -i ../kdebug-376735.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_BUILD_TYPE=Release \
   -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd build
  make DESTDIR="$pkgdir" install
}
