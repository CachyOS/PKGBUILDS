# Maintainer: pavbaranov <pavbaranov @ gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kxmlgui
pkgver=5.71.0
pkgrel=1.1
pkgdesc='User configurable main windows'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kglobalaccel kiconthemes attica)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-177856.patch::"https://invent.kde.org/frameworks/kxmlgui/commit/a957f121882469596330f2afb25e95c9943b1c34.patch")
sha256sums=('2e4b2563daeedf35a54d38002c05d7c39017a36c0b8a19c236ea87324eebf7cc'
            'SKIP'
            '3ab56f3fb0bbe9327e2d472d37c9634f177af1280b5d8ead1c89d8838c634f10')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 177856 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-177856.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
