# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=plasma-integration
pkgver=5.19.5
pkgrel=1.1
pkgdesc="Qt Platform Theme integration plugins for the Plasma workspaces"
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kio kwayland libxcursor noto-fonts ttf-hack qqc2-desktop-style perl)
makedepends=(extra-cmake-modules breeze)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-421450.patch::"https://invent.kde.org/plasma/plasma-integration/commit/857ee264e62e68dfa7c5ce7086dc6fc4195234a5.patch"
kdebug-426573.patch::"https://invent.kde.org/plasma/plasma-integration/commit/b629b1d297b9758fe6207a45e960655cdf50bfd0.patch")
sha256sums=('f7fdd854e78e366a310116ef190bd7bed42df15a059d8ad75b763cbcebfb6389'
            'SKIP'
            '3a134364676294fc9c7602d3c1e3915b3d1a40b9bad5906fe78e108bf006bbe3'
            'c15f93491fb3315016a27dc03566854e6bb6ffa1716c0d924f90048f7175a238')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 421450 patch; fix in 5.20"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-421450.patch
    msg "Add kdebug 426573 patch; fix in 5.20"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-426573.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
