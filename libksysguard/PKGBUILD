# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksysguard
pkgver=5.21.5
pkgrel=1.1
pkgdesc='Library to retrieve information on the current status of computer hardware'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(libxres qt5-webengine kdeclarative knewstuff)
makedepends=(extra-cmake-modules kdoctools qt5-tools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-434042.patch::"https://invent.kde.org/plasma/libksysguard/commit/b867d95e81932f6529bd92ff4e3539b8425ca3ba.patch"
kdebug-428173.patch::"https://invent.kde.org/plasma/libksysguard/commit/38cd07635c83b641dd8c80aeb5aa64a9a9d3513f.patch"
)
sha256sums=('ec094b59a7e111359b550731dafe4d9b92f17ae2c4d663275d97332e1bc5f16b'
            'SKIP'
            'aaa9cce836356ab88940635acf713f5031661fd904a9bb668a3a16b7c0a2adfc'
            'a59ad6a1abb4ac70d2d0bd01cb30f0d25bb6d5cae806eb7d44e8e09d939945c6')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 434042 patch; fix probably in 5.22"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-434042.patch
    msg "Add kdebug 428173 patch;"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-428173.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
