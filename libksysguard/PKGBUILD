# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksysguard
pkgver=5.21.4
pkgrel=1.1
pkgdesc='Library to retrieve information on the current status of computer hardware'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(libxres qt5-webengine kdeclarative knewstuff)
makedepends=(extra-cmake-modules kdoctools qt5-tools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-434515.patch::"https://invent.kde.org/plasma/libksysguard/commit/265337b6901f9170e8151412a5033c71fc79d00e.patch"
kdebug-434042.patch::"https://invent.kde.org/plasma/libksysguard/commit/b867d95e81932f6529bd92ff4e3539b8425ca3ba.patch")
sha256sums=('ec094b59a7e111359b550731dafe4d9b92f17ae2c4d663275d97332e1bc5f16b'
            'SKIP'
            '78588ef7080900d199e6c65c8b44386cadca6b271aabd254545eafafa9d20004'
            'aaa9cce836356ab88940635acf713f5031661fd904a9bb668a3a16b7c0a2adfc')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 434042 patch; fix probably in 5.22"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-434042.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
