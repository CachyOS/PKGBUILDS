# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksysguard
pkgver=5.20.2
pkgrel=1.1
pkgdesc='Library to retrieve information on the current status of computer hardware'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(libxres qt5-webengine kdeclarative knewstuff)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-428048.patch::"https://invent.kde.org/plasma/libksysguard/commit/5e48a74d5f2b0cd271b4edc8308dabd8d2a6fd79.patch"
)
sha256sums=('9f57a3719d665df6d6b3c494a62bb7d60c79b06f30c0e9c75eb03568d2046d71'
            'SKIP'
            'd604546e640b5c2d78caeaa2179e3995ce54afb3b7c2ad47029c87d6e2e1d7c7')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 428048 patch; fix in 5.20.3"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-428048.patch
}
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
