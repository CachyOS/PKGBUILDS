# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksysguard
pkgver=5.14.3
pkgrel=1.2
pkgdesc='Libksysguard'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(libxres qt5-webkit kwindowsystem kconfigwidgets)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
groups=(plasma)
source=("1ttps://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
fix-potential-mem-leak.patch::"https://cgit.kde.org/libksysguard.git/patch/?id=b2704850538bee356116f51d4a87fc593dc3cdf1")
sha256sums=('79ee3bf759ef9116ad9d52940732e29587a46f63533f3e740561965f61b22c25'
            'SKIP'
            'd3638f2ecd269871661c8b679cbf52e939c3c1fc14922ba38c94a274b864fb81')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "fix-potential-mem-leak"
  patch -p1 -i ../fix-potential-mem-leak.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
