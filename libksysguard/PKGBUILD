# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksysguard
pkgver=5.21.2
pkgrel=1.1
pkgdesc='Library to retrieve information on the current status of computer hardware'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(libxres qt5-webengine kdeclarative knewstuff)
makedepends=(extra-cmake-modules kdoctools qt5-tools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
#kdebug-430615.patch::"https://invent.kde.org/plasma/libksysguard/commit/1ce85053e45c95ef43f9a0543e2710f33b81e10b.patch"
kdebug-433954.patch::"https://invent.kde.org/plasma/libksysguard/-/commit/1b6d0046f888b155cf5c4011b10d681c736b914f.patch"
)
sha256sums=('0306e2556d94e3366b015d8bb5d7430c1fb66269ad7c0fbce1fecb8970649982'
            'SKIP'
            'a59c43c495140a2cbe2891a66cb947ad3c78b34f75437c0a06b3f6a1b0817a9e')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
 #   msg "Add kdebug 430615 patch"
 #   patch -d $pkgname-$pkgver -p1 -i ../kdebug-430615.patch
    msg "Add kdebug 433954 patch; fix in 5.21.3"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433954.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
