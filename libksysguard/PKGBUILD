# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksysguard
pkgver=5.14.5
pkgrel=2.1
pkgdesc='Libksysguard'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(libxres qt5-webkit kwindowsystem kconfigwidgets)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
fix-potential-mem-leak.patch::"https://cgit.kde.org/libksysguard.git/patch/?id=b2704850538bee356116f51d4a87fc593dc3cdf1")
sha256sums=('ca571491fbc71a12c39247d25b4474dabd5d08894a1f05b67efb6edcce03df86'
            'SKIP'
            'f1cecdf9f1bf19c0e3e30d6775783bf58b92d8c54278f6c15249660346445f09')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "fix-potential-mem-leak"
  patch -p1 -i ../fix-potential-mem-leak.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make -j5
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
