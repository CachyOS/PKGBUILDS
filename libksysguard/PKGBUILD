# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libksysguard
pkgver=5.21.1
pkgrel=1.3
pkgdesc='Library to retrieve information on the current status of computer hardware'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(libxres qt5-webengine kdeclarative knewstuff)
makedepends=(extra-cmake-modules kdoctools qt5-tools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-433007.patch::"https://invent.kde.org/plasma/libksysguard/commit/504dcbd225e26fe39f1f26d65ad477fc73558762.patch"
kdebug-433169.patch::"https://invent.kde.org/plasma/libksysguard/commit/496802c21d1bddfdaf6ac81ad4298c7b67311f60.patch"
kdebug-433064.patch::"https://invent.kde.org/plasma/libksysguard/commit/b3f7e503276700cd58b685bc1b46f67aad1407df.patch"
)
sha256sums=('e846b0ebf1a347e6f8637a70866c17b1aa8e806c910085729b0fcf08ae74ea93'
            'SKIP'
            '9a663ba26b6be0aa1627684bddcd9dfb6da60e8e390e1f85fbe30bba57f697f8'
            '6e74910fb03d3a52fde15c4dc8a3e2e48a7e1afb76bb844f25b1e5d312b7dcd4'
            '9426f9cb11dbe0c07d769b65d4ba6ef90c05a62afa43445b130e02744eb9a99f')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 433007 and also 424527 patch; fix probably in 5.22"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433007.patch
    msg "Add kdebug 433169 patch; fix in 5.21.2"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433169.patch
    msg "Add kdebug 433064 patch; fix probably in 5.21.2"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433064.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
