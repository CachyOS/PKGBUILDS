# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qqc2-desktop-style
pkgver=5.44.0
pkgrel=1.1
pkgdesc='A style for Qt Quick Controls 2 to make it follow your desktop theme'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(kirigami2 kiconthemes)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-391780.patch::"https://cgit.kde.org/qqc2-desktop-style.git/patch/?id=9a5f7d834f86f57e88c3993fbcf4c17d09a01e10")
sha256sums=('a12cbba9aa5ceb5eb4688f50aa01464e55a574912748e9974328f9cb6c17420b'
            'SKIP'
            '5466b8639b6d50848f8d127b5e082a7c870098ca3728ee00fc8fa7c77a8c6099')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG-391780 patch" # See: https://bugs.kde.org/show_bug.cgi?id=391780 Fixed in 5.45
  patch -p1 -i ../kdebug-391780.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
