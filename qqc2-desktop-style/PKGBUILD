# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qqc2-desktop-style
pkgver=5.69.0
pkgrel=1.1
pkgdesc='A style for Qt Quick Controls 2 to make it follow your desktop theme'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(kirigami2 kiconthemes)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
D27701.patch::"https://cgit.kde.org/qqc2-desktop-style.git/patch/?id=2c777b1b4661b8ab2163c570a67f76f2dfde678b")
sha256sums=('be70ed78ec4b82d96ce6fb573478a18042d14c46fb462bc571dc9b777cf63eb9'
            'SKIP'
            'ad6b7cadfc7dc6799a71f6669d6b772059d6e3ff83248e14bde59eacc1610f2c')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Use transparent dimmer; will be in 5.70"
  patch -p1 -i ../D27701.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
