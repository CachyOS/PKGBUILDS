# Maintainer: pavbaranov <pavbaranov @ gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qqc2-desktop-style
pkgver=5.71.0
pkgrel=1.1
pkgdesc='A style for Qt Quick Controls 2 to make it follow your desktop theme'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(kirigami2 kiconthemes)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-423236.patch::"https://invent.kde.org/frameworks/qqc2-desktop-style/commit/f4167fc479706af808e3bdb4f8d47e09ba5105d6.patch")
sha256sums=('b968ce6fc7c1d111aa2c63584dddc0f74e9066a0b4ea26d1194e46e2f7b38700'
            'SKIP'
            '91b1d9b95d5a45e94981fd247a661990430bd4d7467be8430f67051650af36d8')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare(){
    msg "Add kdebug 423236 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-423236.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
