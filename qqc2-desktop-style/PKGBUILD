# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qqc2-desktop-style
pkgver=5.52.0
pkgrel=1.3
pkgdesc='A style for Qt Quick Controls 2 to make it follow your desktop theme'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(kirigami2 kiconthemes)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
D16651.patch::"https://cgit.kde.org/qqc2-desktop-style.git/patch/?id=8c1fb20f52a5b0f289f186b77e3394e8c53dc574"
D16659.patch::"https://cgit.kde.org/qqc2-desktop-style.git/patch/?id=66d8bdb3273568d82274b48220eeab7560925b7f"
kdebug-396287.patch::"https://cgit.kde.org/qqc2-desktop-style.git/patch/?id=1e02355c178600e89b8c45e6232b2a2dc811ccc6")
sha256sums=('2ee8a3b3a50b2d8a71e097dd9f687ace823dca05acb30595b9bb88cb473b10b7'
            'SKIP'
            '6d273c4f4e0933b3a8e9438901be18f69abdddd0ca97d7bb75136802c1f38f85'
            '78c1d6dddeda9fbfac6c93ad711dc9e4f9437499a064c0a2f98836030bdd4c71'
            '4fa0b29082262b772d779a0d18d689027092a0aa00c9c8c737f5a7bc97df84a8')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add honor flat property of buttons patch"
  patch -p1 -i ../D16651.patch
  msg "Add size according to specified icon width and height patch"
  patch -p1 -i ../D16659.patch
  msg "Add kdebug 396287 patch"
  patch -p1 -i ../kdebug-396287.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
