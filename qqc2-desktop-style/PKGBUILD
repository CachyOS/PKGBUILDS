# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qqc2-desktop-style
pkgver=5.78.0
pkgrel=1.1
pkgdesc='A style for Qt Quick Controls 2 to make it follow your desktop theme'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(kirigami2 kiconthemes)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-400258.patch::"https://invent.kde.org/frameworks/qqc2-desktop-style/commit/9f6e3acc1c9e548a2ac9a71b0b97fedbcb664435.patch")
sha256sums=('33ea60eba2112036b6e3e136161746afe84d44227da7b6d3011b83beb3eb3329'
            'SKIP'
            'c59ae46336bfed2674abe7402e5cd6137867d3f61a54ced40b479908590dac72')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 400258 patch; fix in 5.79"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-400258.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
