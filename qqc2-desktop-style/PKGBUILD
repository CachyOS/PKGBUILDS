# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qqc2-desktop-style
pkgver=5.68.0
pkgrel=1.1
pkgdesc='A style for Qt Quick Controls 2 to make it follow your desktop theme'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(kirigami2 kiconthemes)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-418447.patch::"https://cgit.kde.org/qqc2-desktop-style.git/patch/?id=b738ebbb6369e4c1d9705b4e0c6e0fba4f96bd5d")
sha256sums=('0f522861e5757de6a1205c86a2e5f8d2a7375c96eac1ece95d03a35858dc7b03'
            'SKIP'
            '0177065ad098eb855653cc35c9288429249492884795bc8f19680b44f479b8b1')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply KDEBUG 418447 patch"
  patch -p1 -i ../kdebug-418447.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
