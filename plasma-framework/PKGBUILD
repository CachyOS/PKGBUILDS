# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.80.0
pkgrel=1.1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kactivities kdeclarative kwayland kirigami2)
makedepends=(extra-cmake-modules qt5-tools qt5-doc kdoctools doxygen)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-430622.patch::"https://invent.kde.org/frameworks/plasma-framework/-/commit/2f48e808d5e6c44a1f82d59f98d1cf8286b8bfb1.patch"
)
sha256sums=('d3af0f7628c6404c63ac871ffe7540fc6d12be2bf988503724c6bb00505893d9'
            'SKIP'
            'ec045bf2f6969b59c0f2923ba769f45cf39795922c64eee00fdb15439f1fae0c')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 430622 patch; fix in 5.81"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-430622.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
