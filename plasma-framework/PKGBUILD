# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# made upon original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.47.0
pkgrel=1.1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-quickcontrols kactivities kdeclarative kwayland kirigami2)
makedepends=(extra-cmake-modules qt5-tools kdoctools doxygen)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-394423.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=d15f0fa8322dc2632f15024457bea41e7dd058d7"
kdebug-382324.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=dce258bee3da854980b6f2430b26f7025752a505"
frameSVG-fix_QObject_include.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=a363815a796abfc1d536b140ccec904f2f9d4fdc"
frameSvg-draw_corners_only.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=a46cd14553c281ac61ee0f8cb652987ac3654580"
frameSVG-delete_redundant_checks.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=b169aa50f917a27833f2674141e281ac560593d4")
sha256sums=('4a43bb699e2d6e004b0d218c98079eea329d3a2d3de82b33b7d298e4cb146eb4'
            'SKIP'
            '4653e456c2b20d642a7d6e92eca1760e9f8ac1d89ab061eb65449a367b05781a'
            '5620dca6a476470c565349b1781cf26b7259914a2016b3af3f856d5f8263472b'
            '82349eb2aa6f81b64ae48bb79e2da6d0e0f35a18a0ec2915c80c6567a7d8a09a'
            'e247282cfc5909ab30068cfa7633ecb274cd71d1ffc05dfd8fe00873761e4649'
            '8b1e91a468848c9c83288db8f11b40bc066f0653d9080d7dd09a63de31f2a488')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG 382324, 390632 and 391659 patch" # Fixed in 5.48
  patch -p1 -i ../frameSVG-fix_QObject_include.patch
  patch -p1 -i ../frameSVG-delete_redundant_checks.patch
  patch -p1 -i ../frameSvg-draw_corners_only.patch
  patch -p1 -i ../kdebug-382324.patch
  msg "Add KDEBUG 394423 patch" # Fixed in 5.48
  patch -p1 -i ../kdebug-394423.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
