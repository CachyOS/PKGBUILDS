# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.43.0
pkgrel=1.2
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-quickcontrols kactivities kdeclarative kwayland kirigami2)
makedepends=(extra-cmake-modules qt5-tools kdoctools python doxygen)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
D10512.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=6cbea20bf9f7a02aef404a49a56d0a0d895b0abd"
D10681.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=8346b3e76ab8418b8c6dd7322d13db8abeb0a8e4"
D10722.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=79fb4ebfa77fe7c649ad3e0d7273861d8f3e0bdc"
D10653.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=bf4facd8c1b90f2ca5f0330bbe24f6930a822fb2"
fix_setting_default_preload_policy_if_no_environment_variable_is_set.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=fa9d4be5d1663e912e34a5ed581a3bfee159dab8"
qtbug-66446.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=1ff405cca5b3a5efcfe143dd870799b2b90510c9"
D10533.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=c05b1bdec7901293ad0ad9c06b352a89fe33c3b5"
D10644.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=581dc48ff836cc5228704ef766ea50e6515a45d0"
D10764.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=48b62d1a2763a83cecf78ce86f28dbc6d1af4f68")
sha256sums=('c6108fd4d2cf516cbb97c77c2c58f136b670afc01a7f49face60812f4156eeec'
            'SKIP'
            '33fc25d3f9ea48c265d4f719a1cb0b0d83939b0f8dc873bfe031c89240a2f506'
            '71ffc8cdf62f6cf681188e10061e9b10e88e29763b0851493055f19ee5c213ad'
            '5266868dd96d2d4b9f23a3a26e09470963ced36b7b48ef94bf017de20aee6684'
            '24b97e2c45a47ee6619c07f5e9aa938761c680bebf97f053b01242dc908212c8'
            'fab3773f0f94cd193fdc7a96db52339491e79dd3d418888f9441b31bd55d62dd'
            'bb934c0c01df2f604f47fc59885cf24cb060b86f897677dbe4b41486371a7e0a'
            '189c4dfc17ac5a9d2e63c444f1d35c14ec2791f598ed3ccfcd6d141430a1dc4f'
            '631ebc54e5e563ffb2309cd18f981d960141644268c5fc5b0cd1a7dc6c6fb85c'
            'f8981987c44e0284403403d24051c31284c53375dd31a76abf133f11df7fc732')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add D10512 patch"
  patch -p1 -i ../D10512.patch
  msg "Add D10681 patch"
  patch -p1 -i ../D10681.patch
  msg "Add D10722 patch"
  patch -p1 -i ../D10722.patch
  msg "Add D10653 patch"
  patch -p1 -i ../D10653.patch
  msg "Add fix_setting_default_preload_policy_if_no_environment_variable_is_set patch"
  patch -p1 -i ../fix_setting_default_preload_policy_if_no_environment_variable_is_set.patch
  msg "Add QTBUG 66446 patch"
  patch -p1 -i ../qtbug-66446.patch
  msg "Add D10533 patch"
  patch -p1 -i ../D10533.patch
  msg "Add D10644 patch"
  patch -p1 -i ../D10644.patch
  msg "Add D10764 patch"
  patch -p1 -i ../D10764.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
