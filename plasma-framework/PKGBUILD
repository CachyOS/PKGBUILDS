# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.72.0
pkgrel=1.2
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kactivities kdeclarative kwayland kirigami2)
makedepends=(extra-cmake-modules qt5-tools qt5-doc kdoctools doxygen)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-419239.patch::"https://invent.kde.org/frameworks/plasma-framework/commit/a707731607c5accbc99e813fc3fd3422eec53483.patch"
kdebug-423643.patch::"https://invent.kde.org/frameworks/plasma-framework/commit/ac6bff575f934bceab03abdabc6a5c841c5b1f5e.patch"
         )
sha256sums=('e54c9453df39163f03cc7b4f834fd3fcfe02dfb58ff031935088b95fbb4d3133'
            'SKIP'
            '30ee99c86787f2fee7976b05e95b03933d61e078acea83c980f71e61c88ec41f'
            'bf767f277173a160f7a2a1d264fb6f44c15b619082d1e1893ed8744655c09b16')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 423643 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-423643.patch
    msg "Add kdebug 419239 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-419239.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
