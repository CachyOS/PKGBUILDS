# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.79.0
pkgrel=1.3
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kactivities kdeclarative kwayland kirigami2)
makedepends=(extra-cmake-modules qt5-tools qt5-doc kdoctools doxygen)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-433243.patch::"https://invent.kde.org/frameworks/plasma-framework/commit/b84fbb01f7ea17f6953da06f8ea800b824fc9a57.patch"
kdebug-429927.patch::"https://invent.kde.org/frameworks/plasma-framework/commit/6ac6ff028e9baa9d7ae9f5d0e0666e468e77656b.patch"
kdebug-433864.patch::"https://invent.kde.org/frameworks/plasma-framework/commit/15b75de387af9a9cbbc8aaeac29e29d2a2e2b0f6.patch")
sha256sums=('9dfc3d9a97b6a12ec29df27db063d4ac850c50339688968d9c4768e51b760b96'
            'SKIP'
            '293a472e6fdcf0a5d6995e0316f11524382beddee445452c8cc628ccde1110bb'
            '448fcdca1ff92379e15797ed1903cdf2638060fad513957487370a429fb23267'
            '3b19dee03879a83581f8a27a8ee0bef9cc89a35b8a5ee7259d9a6b89258ead44')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 433243 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433243.patch
    msg "Add kdebug 429927 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-429927.patch
    msg "Add kdebug 433864 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433864.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
