# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.87.0
pkgrel=1.1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kactivities kdeclarative kwayland kirigami2)
makedepends=(extra-cmake-modules qt5-tools qt5-doc kdoctools doxygen)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
    kdebug-440484.patch::"https://invent.kde.org/frameworks/plasma-framework/-/commit/518fb6ac5349a91e0313803d992d200dae9e7d64.patch"
    kdebug-445014.patch::"https://invent.kde.org/frameworks/plasma-framework/-/commit/d3af00fa0be5d1d124dabc45ee1f18e32cbc26f1.patch"
    kdebug-355889.patch::"https://invent.kde.org/frameworks/plasma-framework/commit/c43de5c8ecd5f9602fc68fcc0b8b8236ab3e375b.patch"
)
sha256sums=('4b2ad68437b22647d4c29813176b9e1a199e78b1d49f64959b36a325cf22fcf8'
            'SKIP'
            '0f4c54177b4e6b42b8d004f188138185ae98013a2610e82a8212535a7825e7e3'
            '8089f673399a26092a132b79523e2f6454dae56d74deea8155a1c12c7d2656d4'
            'b1b4494030a0d72d463c4768cdcf6101f96129acb139dd3dcb529cf37481298d')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 440484 patch; fix in 5.88"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-440484.patch
    msg "Add kdebug 445014 patch; fix in 5.88"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-445014.patch
    msg "Add kdebug 355889 patch; fix in 5.88"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-355889.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
