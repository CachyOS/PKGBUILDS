# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.46.0
pkgrel=2.1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-quickcontrols kactivities kdeclarative kwayland kirigami2)
makedepends=(extra-cmake-modules qt5-tools kdoctools doxygen)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-381242.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=11f4e5b392acade56ad240f695f66eb42377e57c"
kdebug-394400.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=b6a6e95afce789ad3ade377edd7719bb57fb7978")
sha256sums=('c70c482480661534c82c2dfe3117ad2f96a4cad810693f84f3895224c232fb7e'
            'SKIP'
            '820637edb44358d6c45d51d680ae9c5f6a7deba54a9ca73021434870358d4162'
            '766722e0c3e4f032be7e5b79ca7c67a12f2db4bd4aad9e95fe38066e7185fee4')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 381242 patch"
  patch -p1 -i ../kdebug-381242.patch
  msg "Add KDEBUG 394400 patch"
  patch -p1 -i ../kdebug-394400.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
