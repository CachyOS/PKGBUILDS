# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.52.0
pkgrel=1.2
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kactivities kdeclarative kwayland kirigami2)
makedepends=(extra-cmake-modules qt5-tools kdoctools doxygen)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
update-breeze-colorschemes.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=2c0ba9826f8f7beef2dc520e9a5c41fe7939a6cb"
fix-leak-in-AppletQuickItem.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=e3eafabd5587467b2d668f538607443227de8841"
fix-memory-leak-in-Corona.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=d13f9e6f853874105160d980d2df4a9f35a58895"
fix-memory-leak-in-DataSource.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=fbfc9bdebb79c29b97ab10ffa29cc6d6841186b9"
fix-memory-leak-in-CalendarPlugin.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=421e1c01266edef07c379cdcaeafe6c91d540ccf")
sha256sums=('64c2d459ec6fc58b52c9313b1b82eee3152f5a67a1b1ea79536477318fd538cf'
            'SKIP'
            '9a271d21911dbec0c41b51d8418faa4e4a8c82a0eca4822d0fbdd2d1604b1a73'
            'b5ea2ad05e209cc4ceff238552d02fcca57e2e0e9e30dcc682c435c0e0a917ca'
            'd6e5bc64b065e486ead14281d9cab3a1fad14c3bd3a378579a9ff4c9b646d204'
            'e487cf19079f63abac5b4922f88ddbfddb13d15d6358da78a947df0b0aa83104'
            '49dc8813a76474f635e407ccda652126f12ae07972ae3a1f7661b1d625c32db5')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "update-breeze-colorschemes patch"
  patch -p1 -i ../update-breeze-colorschemes.patch
  msg "fix-leak-in-AppletQuickItem patch"
  patch -p1 -i ../fix-leak-in-AppletQuickItem.patch
  msg "fix-memory-leak-in-Corona patch"
  patch -p1 -i ../fix-memory-leak-in-Corona.patch
  msg "fix-memory-leak-in-DataSource patch"
  patch -p1 -i ../fix-memory-leak-in-DataSource.patch
  msg "fix-memory-leak-in-CalendarPlugin patch"
  patch -p1 -i ../fix-memory-leak-in-CalendarPlugin.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make 
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
