# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-gtk-config
pkgver=5.21.1
pkgrel=1.2
pkgdesc='GTK2 and GTK3 Configurator for KDE'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kdbusaddons kconfigwidgets)
makedepends=(extra-cmake-modules gtk2 gtk3 sassc)
optdepends=('gtk2: GTK2 apps support' 'gtk3: GTK3 apps support' 'xsettingsd: apply settings to GTK applications on the fly')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-432712.patch::"https://invent.kde.org/plasma/kde-gtk-config/commit/cbfb855ac2f50f91fb39085a337e3151780c861a.patch"
kdebug-433608.patch::"https://invent.kde.org/plasma/kde-gtk-config/commit/83289058a4332f25c664f2f6b495c72c349e15c0.patch"
)
sha256sums=('ac21ddf13c90a52829a48c51b05408228445ce0999c0221ffcde52977a20075b'
            'SKIP'
            '912aa127a8939e5c864cba8cac99572163f464bf409e8a8aaaec6a0d613e304e'
            '5c3c2760c1290aac172a946a287a2b5f4f7020d7f5d1a20bfede43da0a7c2e51')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>)

prepare() {
    msg "Add kdebug 432712 patch"
    patch -d $pkgname-$pkgver -p1 < kdebug-432712.patch
    msg "Add kdebug 433608 patch; fix in 5.21.2"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433608.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
