# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=filelight
pkgver=20.12.3
pkgrel=1.1
pkgdesc='View disk usage information'
url='https://kde.org/applications/utilities/filelight'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-429263.patch::"https://invent.kde.org/utilities/filelight/-/commit/f133de56682412692a9a28103417e4d26083e5aa.patch"
kdebug-417162.patch::"https://invent.kde.org/utilities/filelight/commit/70795c19dbd1aae5cde3ce7fc870ad8ca4de8ac3.patch")
sha256sums=('206a70d9aa2d5ba025cb5ab84d860843a163919f26517eca5c0108b123be4a5a'
            'SKIP'
            '1d3366dbc6cd9cdf7660fa4fc1a0a6c6203154fe2d48cc0aac423e7b9d75522c'
            '2b51b6cd39da6aeecad50908e70c647b7af4aea6635129bb266b39d1b4e6f94d')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
    msg "Add kdebug 417162 patch; fix in 21.04"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-417162.patch
    msg "Add kdebug 429263 patch; fix in 21.04"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-429263.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
