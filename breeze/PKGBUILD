# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=breeze
pkgver=5.20.0
pkgrel=1.1
arch=(x86_64)
pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(frameworkintegration kdecoration breeze-icons kwayland hicolor-icon-theme)
makedepends=(extra-cmake-modules kcmutils)
optdepends=('breeze-gtk: Breeze widget style for GTK applications'
            'kcmutils: for breeze-settings')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-393238.patch::"https://invent.kde.org/plasma/breeze/-/commit/059ac0b719a885402182206aae9e7189b241cfd9.patch"
kdebug-399680.patch::"https://invent.kde.org/plasma/breeze/-/commit/855d4497a373af3f09c994f0730cd625db7069cc.patch")
sha256sums=('f360c5803c434b8ce771232b030779d8c92b3bcb5b7c940f3524d6e9afb05db1'
            'SKIP'
            '4060c4629c2df4ca50da76d86e457b0583c915c5a526afcfcc2f72a3d5307064'
            'df50434c7a62465368ec7c5aee0d6a057a26a36890a15451942631ff218103bc')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 393238 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-393238.patch
    msg "Add kdebug 399680 patch; fix in 5.20.1"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-399680.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
