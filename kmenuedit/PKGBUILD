# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with (mainly bugfix) patches from upcoming releases
# made on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmenuedit
pkgver=5.12.3
pkgrel=1.1
pkgdesc='KDE menu editor'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(khotkeys)
groups=(plasma)
makedepends=(extra-cmake-modules kdoctools python kdesignerplugin)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-390737.patch::"https://cgit.kde.org/kmenuedit.git/patch/?id=e8e3c0f8e4a122ffc13386ab55b408aba2d29e14")
sha256sums=('2463fdbdbdefb3557bc8e42187663637966e415e431a9775951a5e1a1efac0f4'
            'SKIP'
            'c1aae5f784c658526a74225f972f4d2fbef01bb371ad07bbb4a3405013049a73')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG 390737 patch" # See: https://bugs.kde.org/show_bug.cgi?id=390737
  patch -p1 -i ../kdebug-390737.patch
  }

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
