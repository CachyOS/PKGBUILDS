# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Maintainer : Solomon Choina <shlomochoina@gmail.com>
pkgname=falkon-git
pkgver=3.1.0.200.g501678d0
pkgrel=1
pkgdesc="Cross-platform Qt Web Browser"
arch=('i686' 'x86_64')
url="http://falkon.org"
license=('GPL')
depends=('qt5-webengine' 'pyside2' 'qt5-x11extras' 'qt5-svg' 'openssl>=1.1.0' 'purpose')
makedepends=('git' 'qt5-tools' 'kwallet' 'shiboken2' 'pyside2-tools' 'kio' 'extra-cmake-modules' 'shiboken2' 'pyside2-tools')
conflicts=('qupzilla' 'qupzilla-qt5' 'qupzilla-qt5-git' 'falkon' 'falkon-git-polish-language-pack')
optdepends=(
  'bash-completion: bash completion support'
  'kwallet: kf5 kwallet integration'
  'pyside2: python plugins for falkon'
  'shiboken2: qml plugins for falkon'
  )
source=("git+https://invent.kde.org/network/falkon.git" "po.tar.gz" "poqm.tar.gz")
sha256sums=('SKIP'
            '7c70f78b10829745f2a3125492a38a172bcfba40c392330e14ad166b4a44e708'
            '5bb6f68ce3502ec00fe07e768e09a37bae236338cbc7945b4c53d582910a253d')

pkgver() {
  cd "${pkgname%-*}"
  ( set -o pipefail
    git describe --long --tags | sed 's/^v//;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
    mv $srcdir/po ${pkgname%-*}
    mv poqm ${pkgname%-*}
}

build() {
  cmake -B build -S "${pkgname%-*}" \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release
  cmake --build build
}

package() {
DESTDIR="$pkgdir" cmake --install build
}
