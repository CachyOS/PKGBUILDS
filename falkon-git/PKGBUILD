# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Maintainer : Solomon Choina <shlomochoina@gmail.com>
pkgname=falkon-git
pkgver=22.07.80.74.g0341cc577
pkgrel=1
pkgdesc="Cross-platform Qt Web Browser"
arch=('i686' 'x86_64')
url="http://falkon.org"
license=('GPL')
depends=('qt5-webengine' 'pyside2' 'qt5-x11extras' 'qt5-svg' 'openssl>=1.1.0' 'purpose' 'ki18n')
makedepends=('git' 'qt5-tools' 'kwallet' 'shiboken2' 'pyside2-tools' 'kio' 'extra-cmake-modules' 'shiboken2' 'pyside2-tools' 'subversion')
conflicts=('qupzilla' 'qupzilla-qt5' 'qupzilla-qt5-git' 'falkon' 'falkon-git-polish-language-pack')
optdepends=(
  'bash-completion: bash completion support'
  'kwallet: kf5 kwallet integration'
  'pyside2: python plugins for falkon'
  'shiboken2: qml plugins for falkon'
  )
source=("git+https://invent.kde.org/network/falkon.git#branch=master"
"svn://anonsvn.kde.org/home/kde/trunk/l10n-kf5/*/messages/falkon"
)
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd "${pkgname%-*}"
  ( set -o pipefail
    git describe --long --tags | sed 's/^v//;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  export CFLAGS="-Ofast $CFLAGS"
  export CXXFLAGS="-Ofast $CXXFLAGS"
  cmake -B build -S "${pkgname%-*}" \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=True 
  cmake --build build -- -j$(nproc)
}

package() {
DESTDIR="$pkgdir" cmake --install build
}
