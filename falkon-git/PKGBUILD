# Maintainer: pavbaranov <pavbaranov at gmail dot com>

pkgname=falkon-git
pkgver=3.0.0.120.g162f0990
pkgrel=1
pkgdesc="Cross-platform Qt Web Browser"
arch=('i686' 'x86_64')
url="http://www.qupzilla.com"
license=('GPL')
depends=('qt5-webengine' 'openssl>=1.1.0' 'kio') # 'python-pyside2-git' : uncomment if you want to enable experimental function for add new plugins)
makedepends=('git' 'qt5-tools' 'kwallet' 'extra-cmake-modules') # uncomment 'shiboken2-git' 'pyside2-common-git' : if you want to enable experimental function for add new plugins) #libgnome-keyring : uncomment if you want to enable GNOME keyring
conflicts=('qupzilla' 'qupzilla-qt5' 'qupzilla-qt5-git' 'falkon')
optdepends=(
  'bash-completion: bash completion support'
  'kwallet: kf5 kwallet integration'
#  'libgnome-keyring: gnome keyring integration'
  'pyside2-common-git'
  'shiboken2-git'
  )
source=("git://anongit.kde.org/falkon.git" po.tar.gz)
sha256sums=('SKIP'
            'd0d626024d03f63eaa0d58cc78070d6627cda421d75b98f9fe026d9af35030b4')

pkgver() {
  cd "${pkgname%-*}"
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare(){
  mkdir -p build

  echo 'ecm_install_po_files_as_qm(po)' >> $srcdir/${pkgname%-*}/CMakeLists.txt

  cp -r po falkon/
}

build() {
  cd build
  cmake ../"${pkgname%-*}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_KEYRING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
