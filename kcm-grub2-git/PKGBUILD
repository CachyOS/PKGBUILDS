# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Maintainer : Solomon Choina <shlomochoina@gmail.com>

pkgname=kcm-grub2-git
pkgver=v0.6.4.r75.g8e3c6ec
pkgrel=1
pkgdesc="A KDE Control Module for configuring the GRUB2 bootloader"
arch=('x86_64')
url="https://www.pling.com/p/1127844/"
license=('GPL3')
depends=('imagemagick' 'hwinfo' 'kio')
makedepends=('git' 'qt5-tools')
source=("git+https://invent.kde.org/system/kcm-grub2.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-*}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  mkdir build
  cd build
  cmake ../"${pkgname%-*}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
