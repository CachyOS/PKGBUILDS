# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kwayland-server
pkgver=5.20.0
pkgrel=1.1
pkgdesc='Wayland server components built on KDE Frameworks'
arch=(x86_64)
url='https://kde.org/plasma-desktop'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules plasma-wayland-protocols wayland-protocols kwayland doxygen qt5-tools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-426293.patch::"https://invent.kde.org/plasma/kwayland-server/-/commit/3987c0aecfb2f7e9e1222a61a75ad32a0b592c08.patch")
sha256sums=('e40253814bad7fa4ae829f1d3a752ba9bd89d92c5e89a72c6b857668ed75ecd6'
            'SKIP'
            '264290086344ee782f67617b3ebffe0d80b3d723698fe71bcbec15f97380d470')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 426293 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-426293.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
