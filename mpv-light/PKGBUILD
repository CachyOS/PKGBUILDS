# Maintainer: Damian N. <nycko123 at gmail>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv-light
pkgver=0.32.0
pkgrel=1
pkgdesc='a free, open source, and cross-platform media player (polaur)'
conflicts=('mpv')
arch=('x86_64')
# We link against libraries that are licensed GPLv3 explicitly, libsmbclient
# being one of these. So our package is GPLv3 only as well.
license=('GPL3')
url='https://mpv.io/'
depends=('desktop-file-utils' 'ffmpeg' 'hicolor-icon-theme' 'libjack.so' 'lcms2'
         'libarchive' 'libcaca' 'libgl' 'libva'
         'libxinerama' 'libxkbcommon' 'libxrandr' 'libxss' 'libxv' 'lua52'
         'librubberband.so' 'uchardet' 'vulkan-icd-loader' 'wayland'
         'xdg-utils' 'shaderc' 'mujs' 'libplacebo' 'libplacebo.so')
makedepends=('mesa' 'python-docutils' 'ladspa' 'wayland-protocols'
             'ffnvcodec-headers' 'vulkan-headers' 'waf')
optdepends=('youtube-dl: for video-sharing websites playback')
provides=('libmpv.so')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mpv-player/${pkgname%-*}/archive/v$pkgver.tar.gz")
sha256sums=('805a3ac8cf51bfdea6087a6480c18835101da0355c8e469b6d488a1e290585a5')

build() {
  cd ${pkgname%-*}-${pkgver}

  waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --disable-cdda \
    --enable-dvb \
    --disable-dvdnav \
    --enable-libarchive \
    --enable-libmpv-shared \
    --disable-libsmbclient \
	--disable-build-date

  waf build
}

package() {
  cd ${pkgname%-*}-${pkgver}

  waf install --destdir="$pkgdir"

  install -m0644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv

  install -m0644 TOOLS/lua/* \
    -D -t "$pkgdir"/usr/share/mpv/scripts
}
