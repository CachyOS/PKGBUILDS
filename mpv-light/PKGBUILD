# Maintainer: Damian N. <nycko123 at gmail>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv-light
pkgver=0.32.0
pkgrel=6
_tag='d81133141f052187016a8cb4cf034c35ba39e0ba' # git rev-parse v${pkgver}
pkgdesc='a free, open source, and cross-platform media player (polaur)'
conflicts=('mpv')
arch=('x86_64')
# We link against libraries that are licensed GPLv3 explicitly, libsmbclient
# being one of these. So our package is GPLv3 only as well.
license=('GPL3')
url='https://mpv.io/'
depends=('desktop-file-utils' 'ffmpeg' 'hicolor-icon-theme' 'libjack.so' 'lcms2'
         'libarchive' 'libcaca' 'libgl' 'libva'
         'libxinerama' 'libxkbcommon' 'libxrandr' 'libxss' 'libxv' 'lua52'
         'librubberband.so' 'uchardet' 'vulkan-icd-loader' 'wayland'
         'xdg-utils' 'shaderc' 'mujs' 'libplacebo' 'libplacebo.so')
makedepends=('mesa' 'python-docutils' 'ladspa' 'wayland-protocols'
             'ffnvcodec-headers' 'vulkan-headers' 'waf')
optdepends=('youtube-dl: for video-sharing websites playback')
provides=('libmpv.so')
options=('!emptydirs')
validpgpkeys=('145077D82501AA20152CACCE8D769208D5E31419') # sfan5 <sfan5@live.de>
# source=("$pkgname-$pkgver.tar.gz::https://github.com/mpv-player/${pkgname%-*}/archive/v$pkgver.tar.gz")
# sha256sums=('9163f64832226d22e24bbc4874ebd6ac02372cd717bef15c28a0aa858c5fe592')
source=("git+https://github.com/mpv-player/mpv.git#tag=${_tag}?signed")
sha256sums=('SKIP')

build() {
  # cd ${pkgname%-*}-${pkgver}
  cd ${pkgname%-*}

  waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --disable-cdda \
    --disable-dvb \
    --disable-dvdnav \
    --enable-libarchive \
    --enable-libmpv-shared \
    --disable-libsmbclient \
    --disable-sndio \
    --disable-build-date

  waf build
}

package() {
  # cd ${pkgname%-*}-${pkgver}
  cd ${pkgname%-*}

  waf install --destdir="$pkgdir"

  install -m0644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv

  install -m0644 TOOLS/lua/* \
    -D -t "$pkgdir"/usr/share/mpv/scripts
}
