# Maintainer: Damian N. <nycko123 at gmail>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv-light
pkgver=0.29.1
pkgrel=1
_waf_version=2.0.9
pkgdesc='a free, open source, and cross-platform media player (polaur)'
conflicts=('mpv')
arch=('x86_64')
# We link against libraries that are licensed GPLv3 explicitly, libsmbclient
# being one of these. So our package is GPLv3 only as well.
license=('GPL3')
url='https://mpv.io/'
depends=('desktop-file-utils' 'ffmpeg' 'hicolor-icon-theme' 'jack' 'lcms2'
         'libarchive' 'libcaca' 'libgl' 'libva'
         'libxinerama' 'libxkbcommon' 'libxrandr' 'libxss' 'libxv' 'lua52'
         'rubberband' 'uchardet' 'vulkan-icd-loader' 'wayland'
         'xdg-utils' 'shaderc')
makedepends=('mesa' 'python-docutils' 'ladspa' 'wayland-protocols'
             'ffnvcodec-headers' 'vulkan-headers')
optdepends=('youtube-dl: for video-sharing websites playback')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mpv-player/${pkgname%-*}/archive/v$pkgver.tar.gz"
        "https://waf.io/waf-${_waf_version}"
        '0001-libarchive-add-fallback-for-systems-without-C-UTF-8.patch')
sha256sums=('f9f9d461d1990f9728660b4ccb0e8cb5dce29ccaa6af567bec481b79291ca623'
            '2a8e0816f023995e557f79ea8940d322bec18f286917c8f9a6fa2dc3875dfa48'
            '0f8a1ba2628a50a8637137dfff8f66fdf1e7beb6ff159309842dcb6f39363d5e')

prepare() {
  cd ${pkgname%-*}-${pkgver}

  patch -Np1 < ../0001-libarchive-add-fallback-for-systems-without-C-UTF-8.patch

  install -m755 "${srcdir}"/waf-${_waf_version} waf
}

build() {
  cd ${pkgname%-*}-${pkgver}

  ./waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --disable-cdda \
    --enable-dvb \
    --disable-dvdnav \
    --enable-libarchive \
    --enable-libmpv-shared \
    --disable-libsmbclient \
    --enable-tv \
    --enable-zsh-comp

  ./waf build
}

package() {
  cd ${pkgname%-*}-${pkgver}

  ./waf install --destdir="$pkgdir"

  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
}
