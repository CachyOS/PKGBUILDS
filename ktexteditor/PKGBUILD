# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.54.0
pkgrel=2.1
pkgdesc='Advanced embeddable text editor'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kparts syntax-highlighting libgit2 editorconfig-core-c)
makedepends=(extra-cmake-modules doxygen qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-363492.patch::"https://cgit.kde.org/ktexteditor.git/patch/?id=e1b5f3177d428a70e43f2f1dc8458f8bc15e7791"
kdebug-402594.patch::"https://cgit.kde.org/ktexteditor.git/patch/?id=1e0ace28c795cbe5e88bc1f31ea37a250cd110b0")
sha256sums=('feefc112f4dd0e7f73d2ddc5bcc8e83b11ab0a911d079950afb85281f6e6de8b'
            'SKIP'
            '0db622abbbed6d5914f92cbc5f77668a575fb3bc6c83b317ffee0b37803f2294'
            '9e066a3864fc9aeac7fdcd8f5c21c6f071b54561a6725fc0c2281a874241dd9a')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 363492 patch"
  patch -p1 -i ../kdebug-363492.patch
  msg "Add KDEBUG 402594 patch, fix in 5.55"
  patch -p1 -i ../kdebug-402594.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make 
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
