# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# made upon the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.46.0
pkgrel=1.1
pkgdesc='Advanced embeddable text editor'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kparts syntax-highlighting libgit2 editorconfig-core-c qt5-declarative)
makedepends=(extra-cmake-modules doxygen qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-386897.patch::"https://cgit.kde.org/ktexteditor.git/patch/?id=594c51bbf61dcf7014a44727519afb4a6b58c074")
sha256sums=('0bd58aadcbbf61acea70a89bb91be798990db81f413e20ba177188e046b45efa'
            'SKIP'
            '189f2affbc0a6d95c1a50cba66bd59d80f44204ff60ace19612d230537fafaaf')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG 386897 patch"
  patch -p1 -i ../kdebug-386897.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
