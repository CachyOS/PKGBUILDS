# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.47.0
pkgrel=2.2
pkgdesc='Advanced embeddable text editor'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kparts syntax-highlighting libgit2 editorconfig-core-c qt5-declarative)
makedepends=(extra-cmake-modules doxygen qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-391518.patch::"https://cgit.kde.org/ktexteditor.git/patch/?id=e6f87dd570082eb7f636b92f6ca88d85763ae687"
CVE-2018-10361.patch::"https://cgit.kde.org/ktexteditor.git/patch/?id=c81af5aa"
kdebug-353747.patch::"https://cgit.kde.org/ktexteditor.git/patch/?id=b099af9eb569f4c0765cc4bc1aa69adef8f97772"
kdebug-387362.patch::"https://cgit.kde.org/ktexteditor.git/patch/?id=3f0638e717a990c81fbc248298cc62709e8af851")
sha256sums=('6b4ae2ea3c00dd2d7dc4c53f7760c750f8fee8aece4bcbc938064cadd34c08cc'
            'SKIP'
            '89affeacd71a488a6acc4f24427990ecd4f4026a8111588ae40f0b5d40a57606'
            '3cb3da73c1f5bd7adb943f5da356d73dbc26f2ff8ef0146a1520561468593938'
            '99710b5510540bd1211f514601f313a026efac9e36fd4f4359a971dc82d8d37a'
            '9902cb29609b75424e2e19b0689331cf3c3fd0782bbe560b811fbb8361ada8d1')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add CVE-2018-10361 security patch"
  patch -p1 -i ../CVE-2018-10361.patch
  msg "Add KDEBUG 391518 patch" # Fixed in 5.48
  patch -p1 -i ../kdebug-391518.patch
  msg "Add KDEBUG 353747 patch" # Fixed in 5.47???
  patch -p1 -i ../kdebug-353747.patch
  msg "Add KDEBUG 387362 patch"
  patch -p1 -i ../kdebug-387362.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
