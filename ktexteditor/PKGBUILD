# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.76.0
pkgrel=1.1
pkgdesc='Advanced embeddable text editor'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kparts syntax-highlighting libgit2 editorconfig-core-c)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
commit-5eda60b3.patch::"https://invent.kde.org/frameworks/ktexteditor/-/commit/5eda60b335efcb6976682bf102ef21f47fe943bf.patch"
)
sha256sums=('6f937b7af06562a238f091deef9c4332e94311a697af8466b7f091720eaab2b2'
            'SKIP'
            'cad99a26a024d2ab5533bef69e379d34aad39a7427ce89d609934bf8708b841d')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add bracket matching feature"
    patch -d $pkgname-$pkgver -p1 -i ../commit-5eda60b3.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
