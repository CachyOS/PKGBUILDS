# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.79.0
pkgrel=1.1
pkgdesc='Advanced embeddable text editor'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kparts syntax-highlighting libgit2 editorconfig-core-c)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-433006.patch::"https://invent.kde.org/frameworks/ktexteditor/commit/52b576ed83d7329c28d94ae283481f05dd97cc81.patch"
#commit-deb106b5.patch::"https://invent.kde.org/frameworks/ktexteditor/-/commit/deb106b569b8e52c9419760013231ed342f46c5e.patch"
)
sha256sums=('83b94589d3a23f0b268eb5cfa9544541ac0015ecfab5cf3fd5e95e4499d155b3'
            'SKIP'
            'e7896f9323dd6530bcf34fff4bbf06f8edc3595c9f87ea5908e15a3880808d2f')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 433006 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433006.patch
    #msg "Add enable smart copy cut patch"
    #patch -d $pkgname-$pkgver -p1 -i ../commit-deb106b5.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
