# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kinfocenter
pkgver=5.14.4
pkgrel=1.2
pkgdesc='A utility that provides information about a computer system'
arch=(x86_64)
url='https://www.kde.org/applications/system/kinfocenter/'
license=(LGPL)
depends=(kcmutils pciutils glu libraw1394 kwayland)
makedepends=(extra-cmake-modules plasma-framework kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig} 
        https://sources.archlinux.org/other/artwork/archlinux-artwork-1.6.tar.gz
        kcm-about-distrorc
	kdebug-400732.patch::"https://cgit.kde.org/kinfocenter.git/patch/?id=92f9f21e496f027cc29cda1b73bc749d26d0c46a"
	fix-mem-leak.patch::"https://cgit.kde.org/kinfocenter.git/patch/?id=dd58d2fe5d9e5036c5deafa992ac1256b878e8fb"
	kdebug-364767.patch::"https://cgit.kde.org/kinfocenter.git/patch/?id=9f546137ec6b8b92434136a7f74773e207196f98")
sha256sums=('2cc73fac179bd9bcf5c9ed82c9aec6c861e414d70d0042435882da76c66b09da'
            'SKIP'
            '9c6e7faea0ed66bf6ed64b134fad6316161cf146779e4e2f4cd95c644066aab2'
            '02ae86f5ef8de5fabb97db2043065c9b6b77b7c6a71a1115161e559e39c01451'
            'ac79cb19453bd9ea1bf962acc434afe48dc9c10282f229cebfc7bbf50415d8a4'
            'e0db0c2d5980751e38f49153dd3708a08c182ff30d402f8856c5f68e019cacb9'
            'f058018c1fdaa214b24e37ab8dfd9aa40b2951caa3d4502b00af0c0a78695c88')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG 400732 patch"
  patch -p1 -i ../kdebug-400732.patch
  msg "Fix mem leak"
  patch -p1 -i ../fix-mem-leak.patch
  msg "Add KDEBUG 364767 patch"
  patch -p1 -i ../kdebug-364767.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make 
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# Install Arch logo
  install -Dm644 "$srcdir"/archlinux-artwork-1.6/logos/archlinux-vert-dark.svg \
    "$pkgdir"/usr/share/about-distro/archlinux-logo.svg
  install -Dm644 "$srcdir"/kcm-about-distrorc "$pkgdir"/etc/xdg/kcm-about-distrorc
}
