# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=print-manager
pkgver=19.12.3
pkgrel=1.2
pkgdesc="A tool for managing print jobs and printers"
arch=(x86_64)
url='https://www.kde.org/applications/utilities/'
license=(GPL LGPL FDL)
depends=(kcmutils)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
optdepends=('system-config-printer: auto-detect the printer driver')
groups=(kde-applications kdeutils)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-408512.patch::"https://cgit.kde.org/print-manager.git/patch/?id=9a11a1f4de8a26999485148c5a78e160e8880f17"
kdebug-360199.patch::"https://cgit.kde.org/print-manager.git/patch/?id=faea0b125911820c2c574cf17457f2d15c6100ba")
sha256sums=('74c13802a65136539b4542fec10fb248149a3324e8060e947a8f305ce665269a'
            'SKIP'
            '885fc30e3301541eb58e86fa6ac90109dec2db820cb325d6bf7590fbab7d7355'
            'df781d3ff23d387882b433de3a9de478c2f45c74d1f52f6d193593c18a0f2648')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add kdebug-408512.patch; fix in 20.04"
  patch -p1 -i ../kdebug-408512.patch
  msg "Add kdebug 360199 and 398681 patch; fix in 20.04"
  patch -p1 -i ../kdebug-360199.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
