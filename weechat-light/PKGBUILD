# Maintainer : Damian N. <nycko123 at gmail>
# Maintainer : Jacek Bienias <sp7ezd at gmail.com>
# Contributor : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat-light
replaces=('weechat-python3-light')
_pkgname=weechat
pkgver=2.8
pkgrel=4
pkgdesc="Fast, light and extensible IRC client (curses UI) (polaur)"
conflicts=('weechat')
arch=('x86_64')
url="https://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('asciidoctor' 'cmake' 'lua' 'perl' 'python' 'ruby' 'tcl')
optdepends=('lua: support for lua scripts'
            'perl: support for perl scripts'
            'python: support for perl scripts'
            'ruby: support for ruby scripts'
            'tcl: support for tcl scripts')
source=("${pkgname}-${pkgver}.tar.xz::https://www.weechat.org/files/src/${_pkgname}-${pkgver}.tar.xz"
        "${pkgname}-${pkgver}.tar.xz.asc::https://www.weechat.org/files/src/${_pkgname}-${pkgver}.tar.xz.asc"
	"weechat-cmake-datarootdir.patch::https://github.com/weechat/weechat/commit/683464626d.patch"
	"weechat-fix-bugged-sharedir.patch::https://github.com/weechat/weechat/commit/973a4b2e77.patch")
sha512sums=('3bebb42118fca6b601f1de8f4d52378619a10e5c490f5b46f86d27a9680f00a56d48a1ade729e78d965cae589e6c80647c22414393000c7d3cfcc94a1e41c4df'
            'SKIP'
            '973990a496370c35a4aaf607e660dd28b6bfcd561d7f354f551ad59a1ec84ccb77ad6cc09a2389fa65d5b4059dc1a544b08dc26c02f3d3d4f584c62759109c21'
            '75ad367745a08805f11835f5b451ead8220b7940cf2f792ad77bda017ef25d987d098b111ffd423c38b5f891b13fb45ff688fdda646e77308600e466d4e858da')
validpgpkeys=('A9AB5AB778FA5C3522FD0378F82F4B16DEC408F8') # WeeChat (signing key) <webmaster@weechat.org>

# cmake does not correctly handle CPPFLAGS, so kludge it in anyway:
# https://gitlab.kitware.com/cmake/cmake/-/issues/12928
CFLAGS+=" ${CPPFLAGS} "

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	# https://github.com/weechat/weechat/pull/1461
	# make /usr/share/weechat work, e.g. community/weech      at-matrix
	patch -p1 -i ../weechat-cmake-datarootdir.patch
	patch -p1 -i ../weechat-fix-bugged-sharedir.patch

}

build() {
  mkdir -p build
  cd build
  cmake -Wno-dev "${srcdir}/${_pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
		-DWEECHAT_HOME=~/.config/weechat \
        -DENABLE_MAN=ON \
        -DENABLE_DOC=OFF \
		-DENABLE_GUILE=OFF \
		-DENABLE_ASPELL=OFF \
		-DENABLE_ENCHANT=OFF \
		-DENABLE_PHP=OFF \
		-DENABLE_JAVASCRIPT=OFF \
		-DENABLE_SPELL=OFF \
		-DENABLE_PERL=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
