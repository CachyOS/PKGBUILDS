# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version patched with bugfixes from upcomming editions
# made upon original works of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeclarative
pkgver=5.55.0
pkgrel=1.2
pkgdesc='Provides integration of QML and KDE Frameworks'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kio kpackage libepoxy)
makedepends=(extra-cmake-modules doxygen qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-396011.patch::"https://cgit.kde.org/kdeclarative.git/patch/?id=856672f370fb32c7a3c3d1a13d873fb8b767d0e8"
kdebug-404389.patch::"https://cgit.kde.org/kdeclarative.git/patch/?id=23942e5708316ab678d3c31ab9f2c44440294f88")
sha256sums=('aa51a98e66dcfeb08d26cc79b230fac3eac9235a5d3dee08805cf964f3a17210'
            'SKIP'
            '37f2fa5873d9c8aab90d4df22cfb8800364ca760f35a4709bed9d7f55b8cf271'
            '2f8ab115465acd6ee20f7189868a4bdea68146ac39e3f904a9b0d429c6fb2a3f')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Apply KDEBUG 396011 patch, fixed in 5.56"
  patch -p1 -i ../kdebug-396011.patch
  msg "Apply KDEBUG 404389 patch, fixed in 5.56"
  patch -p1 -i ../kdebug-404389.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
