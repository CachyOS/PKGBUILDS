# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeclarative
pkgver=5.74.0
pkgrel=1.1
pkgdesc='Provides integration of QML and KDE Frameworks'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kio kpackage libepoxy)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-425979.patch::"https://invent.kde.org/frameworks/kdeclarative/commit/88aabf069a0e454777c15227126732a04c8cb8b2.patch")
sha256sums=('387c41626c145ddd1fc76d102b5ceaf37321cd02ca7395a10084cfd009bd0422'
            'SKIP'
            '7441dfea8f929e356532e493ca678016ca07bde5922527f8d27c17ede2693ed6')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 425979 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-425979.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
