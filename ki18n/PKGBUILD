# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ki18n
pkgver=5.45.0
pkgrel=1.1
pkgdesc='Advanced internationalization framework'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-script)
makedepends=(extra-cmake-modules qt5-declarative python-pyqt5 python2-pyqt5 clang doxygen qt5-tools)
optdepends=('python-pykf5: to use the Python 3 bindings' 'python2-pykf5: to use the Python 2 bindings' 'python: to compile .ts files')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-393141.patch::"https://cgit.kde.org/ki18n.git/patch/?id=9c32bdab29b345976eee3b9c1c4bebd76cdcdde2")
sha256sums=('b70e62cd355b02c6160b69b210fd7f4bd44fb341e013db8dc81c744332e55cf6'
            'SKIP'
            '024d6f1bfc55321455d5587940e77d588bcf9a40d515c6f833c4b0c4e68131ae')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 393141 patch" 
  patch -p1 -i ../kdebug-393141.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
