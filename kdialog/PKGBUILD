# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kdialog
pkgver=20.08.1
pkgrel=1.1
pkgdesc="A utility for displaying dialog boxes from shell scripts"
arch=(x86_64)
url="https://www.kde.org/"
license=(LGPL)
depends=(kio)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-426979.patch::"https://invent.kde.org/utilities/kdialog/-/commit/4d64f16ea85a0aaaa9c9c0a1e0ec2691426a75ab.patch")
sha256sums=('089e8a8e85a9021b830e9b8a19ecb1999781915a716c9c2b3c99894a5d1c8dcf'
            'SKIP'
            'ff3fd46f67870f5197d02df98eb354587826e2c1543bb5faf7488e3e0f8ae3d5')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Add kdebug 426979 patch; fix in 20.08.2"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-426979.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
