# Maintainer: pavbaranov <pavbaranov at gmail dot com>

pkgname=amarok-git
pkgver=2.9.0.171.g2f18d0ee71
pkgrel=1
pkgdesc="A media player for KDE, KF5, GIT version"
arch=('x86_64')
url="http://amarok.kde.org"
license=('GPL2' 'LGPL2.1' 'FDL')
depends=(kdnssd kcmutils knewstuff knotifyconfig ktexteditor threadweaver kirigami2 taglib-extras libmtp libmariadbclient libofa ffmpeg)
makedepends=(pkgconfig extra-cmake-modules kdoctools kirigami2 libmygpo-qt5 liblastfm-qt5 loudmouth)
optdepends=(liblastfm-qt5 libmygpo-qt5 loudmouth clamz libmtp ifuse libgpod)
groups=('multimedia')
source=(git://anongit.kde.org/amarok.git)   
sha1sums=('SKIP')

pkgver() {
    cd ${pkgname%-*}
	git describe --always | sed 's/^v//;s/-/./g'
    }

prepare() {
    cd ${pkgname%-*}
    sed -i -e 's|PATH_SUFFIXES lastfm5|PATH_SUFFIXES lastfm|' cmake/modules/FindLibLastFm.cmake
    }

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname%-*} -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DWITH_IPOD=OFF \
    -DMygpo-qt5_DIR=/usr/lib/cmake/mygpo-qt
    
  make
  }

package() {
  cd build
  make DESTDIR=${pkgdir} install
  }
