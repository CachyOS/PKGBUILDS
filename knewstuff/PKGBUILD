# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knewstuff
pkgver=5.74.0
pkgrel=1.2
pkgdesc='Support for downloading application assets from the network'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kio kpackage)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc qt5-declarative)
optdepends=('kirigami2: QML components')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-417510.patch::"https://invent.kde.org/frameworks/knewstuff/commit/28f254aa6bdf5336f9b4a280f33577d5397fdcf8.patch"
kdebug-426732.patch::"https://invent.kde.org/frameworks/knewstuff/-/commit/68824589fe8a1356f06e80098556b77914f96824.patch")
sha256sums=('638ba6cee3091a94a0ef60977b6e22c7b58267ea6fd1bc17d3e7ac47bd9a3d27'
            'SKIP'
            '0111c6942e953192de53128dfe92a2e919dc8a89c819ec53552bd7eb3efc5ceb'
            'c32d157f6aee80766e8f21b7e1bcdc4ddab80255d1f77927bde93459a93ad2f7')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 417510 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-417510.patch
    msg "Add kdebug 426732 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-426732.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
