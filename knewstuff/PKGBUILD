# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knewstuff
pkgver=5.67.0
pkgrel=1.3
pkgdesc='Support for downloading application assets from the network'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kio)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc qt5-declarative)
optdepends=('kirigami2: QML components')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-417802.patch::"https://cgit.kde.org/knewstuff.git/patch/?id=5c7ee50bf900666efa44439a4f4a12ec5df986f9"
kdebug-418191.patch::"https://cgit.kde.org/knewstuff.git/patch/?id=94015abda31bb8c9ed8b87c46f4a685f2ccb5a15"
qt5.15-build-fix.patch::"https://cgit.kde.org/knewstuff.git/patch/?id=cfb84852d2ea7be6e5b653ca1e09ee687d7fe24d"
kdebug-418368.patch::"https://cgit.kde.org/knewstuff.git/patch/?id=b9a1dd14800b45a06fe4e70f1f8079580a2967f1")
sha256sums=('2fadaf5bba791d8b009ab187349175cc6a2e37b968167a3a43f2174cd5e8d10c'
            'SKIP'
            'fee6f33b1125bc62f6c0d9208e5c4aa3f977d60de4ff287afc4e846597f051b6'
            '5f6ea7fa6d29195a93d7757450bbf2b8f2c95677fd20607000e5a4ccf728d928'
            '12e276cfa50bbefafb39c993233ea7f4edd95c026906353d27ffa66b40e8442e'
            'b66989ba0deb42c8c37c2998a0824eefb4e3ad858e38ee886ad0ff78b422161b')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  # patches fixed in 5.68
  msg "Applying kdebug 417802 patch"
  patch -p1 -i ../kdebug-417802.patch
  msg "Applying kdebug 418191 patch"
  patch -p1 -i ../kdebug-418191.patch
  msg "Applying kdebug 418368 patch"
  patch -p1 -i ../kdebug-418368.patch
  # patch for building against of Qt5.15
  patch -p1 -i ../qt5.15-build-fix.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
