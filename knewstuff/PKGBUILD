# Maintainer: pavbaranov <pavbaranov @ gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knewstuff
pkgver=5.71.0
pkgrel=1.1
pkgdesc='Support for downloading application assets from the network'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kio kpackage)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc qt5-declarative)
optdepends=('kirigami2: QML components')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-422476.patch::"https://invent.kde.org/frameworks/knewstuff/commit/a1cc382053837e0884603d05c98ac912a9e42415.patch"
kdebug-422048.patch::"https://invent.kde.org/frameworks/knewstuff/commit/087cf5c476c09bbf3a5f2b7bd57171a3386001ca.patch")
sha256sums=('aba867855d69641f73db30405e787fc9ea22e3386a45be9626ba84cbe208f855'
            'SKIP'
            'dfdf8f9b1582ec138f41ae998e1a8bd720257f0c815b4aa2be4ceb72779e5104'
            '7f5ea3b76d0ba213c89776253828282c7fdb676e0c5f8d920369c1a50de20458')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 422048 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-422048.patch
    msg "Add kdebug 422476 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-422476.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
