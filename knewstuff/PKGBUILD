# Maintainer: pavbaranov <pavbaranov @ gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knewstuff
pkgver=5.72.0
pkgrel=1.1
pkgdesc='Support for downloading application assets from the network'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kio kpackage)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc qt5-declarative)
optdepends=('kirigami2: QML components')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-418036.patch::"https://invent.kde.org/frameworks/knewstuff/commit/fc40f315bfc59947b43cf6ea9286bf8e0265a0b2.patch"
kdebug-369561.patch::"https://invent.kde.org/frameworks/knewstuff/commit/f5fd5458d49f0f0b5e5d682fa13f21636f597166.patch"
kdebug-406993.patch::"https://invent.kde.org/frameworks/knewstuff/commit/91e46e975fa52ecde1fa0bc54e678c1bdeea8ae8.patch")
sha256sums=('04cf371431112b7267f093bff1c8aec56ab80749a1505092f73e8bf390d6bc6a'
            'SKIP'
            '131232a06b3ea43cd477da6608c635fbe57243327d885804349f2c086827016d'
            '926d3b96e3c9c19ea800457f6fb9b9472dafd9da0a961bc75a1bf2100f6bddb4'
            '20010a40f05829459ab7b66c5005f4af39e941628949ad40946df800d34cde24')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 418036 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-418036.patch
    msg "Add kdebug 369561 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-369561.patch
    msg "Add kdebug 406993 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-406993.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
