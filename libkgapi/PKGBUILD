# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=20.12.3
pkgrel=1.1
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://www.kde.org/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kcalendarcore kcontacts kwallet)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
          kdebug-328616.patch::"https://invent.kde.org/pim/libkgapi/commit/a48887dbbc9bdc7da50c1f93c7235c16620bba36.patch")
sha256sums=('fd668411c3d1e9f65237d15d884efd5cfb7cae0a90fa517bcf833f3980653941'
            'SKIP'
            '5d31dc373c5bf3154e4910616726ac5617bc0710db9c0e8f7669ad4a35d6d45a')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87 # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF # Heiko Becker
              )

prepare() {
  msg "Add kdebug 328616 patch; fix in 5.17"
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-328616.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
