# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kate
pkgname=(kwrite kate)
pkgver=20.08.1
pkgrel=1.2
arch=(x86_64)
license=(GPL LGPL FDL)
makedepends=(extra-cmake-modules kdoctools plasma-framework knewstuff kitemmodels ktexteditor threadweaver kactivities kuserfeedback)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgbase-$pkgver.tar.xz"{,.sig}
kdebug-426300.patch::"https://invent.kde.org/utilities/kate/commit/14e0da664a2733dee652795323b128ecbcf85c5c.patch"
kdebug-426042.patch::"https://invent.kde.org/utilities/kate/commit/4eaa3b75bd91febbcf9823aeeb45b91e1158f98a.patch"
kdebug-426605.patch::"https://invent.kde.org/utilities/kate/commit/972903d2620932477c91c62b8b8c56e8d3bf40bd.patch")
sha256sums=('44607f6a1d5ca8bb7173bedbeabef65bb98dde0fd009987bd8139fbb53959146'
            'SKIP'
            'b8a021d8614696ced9d161a41c04c06fb85f258d98ccf49c367b82426303f8cb'
            '4e83a3495c6ad9a7889f67a01f4b4547971b5c27dbb62738cc0b3057a107bf8a'
            '4d0a4eceabf74562263eb388a330e8be566b105c8ec3111fe24a4769b4d5b01e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Add kdebug 426300 patch; fix in 20.12"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-426300.patch
    msg "Add kdebug 426042 patch"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-426042.patch
    msg "Add kdebug 426605 patch; fix in 20.08.2"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-426605.patch
}

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_kwrite() {
  pkgdesc="Text Editor"
  groups=(kde-applications kde-utilities)
  url="https://www.kde.org/applications/utilities/kwrite/"
  depends=(ktexteditor hicolor-icon-theme)

  DESTDIR="$pkgdir" cmake --install build

  find "$pkgdir" -type f -name '*kate*' -exec rm {} \;
  rm -r "$pkgdir"/usr/lib/qt/plugins/ktexteditor \
        "$pkgdir"/usr/share/doc/HTML/*/{kate,katepart} \
        "$pkgdir"/usr/share/katexmltools \
        "$pkgdir"/usr/share/locale/*/LC_MESSAGES/{ktexteditorpreviewplugin,lspclient,tabswitcherplugin}.mo \
        "$pkgdir"/usr/share/plasma/plasmoids
}

package_kate() {
  pkgdesc="Advanced Text Editor"
  groups=(kde-applications kde-utilities)
  url="https://www.kde.org/applications/utilities/kate/"
  depends=(knewstuff ktexteditor threadweaver kactivities kuserfeedback hicolor-icon-theme)
  optdepends=('konsole: open a terminal in Kate'
              'clang: C and C++ LSP support'
              'python-language-server: Python LSP support'
              'texlab: LaTeX LSP support'
              'rust: Rust LSP support')
  DESTDIR="$pkgdir" cmake --install build

  find "$pkgdir" -type f -name '*kwrite*' -exec rm {} \;
  rm -r "$pkgdir"/usr/share/doc/HTML/*/kwrite
}
