# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kate
pkgname=(kwrite kate)
pkgver=18.12.0
pkgrel=1.1
arch=(x86_64)
license=(GPL LGPL FDL)
makedepends=(extra-cmake-modules kdoctools plasma-framework knewstuff ktexteditor threadweaver kitemmodels kactivities)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgbase-$pkgver.tar.xz"{,.sig}
D16641.diff
D17297.diff
D17318.diff
kdebug-397913.patch::"https://cgit.kde.org/kate.git/patch/?id=1f01415ba33d58185ec80762f5eaa9451597c292")
sha256sums=('14991164207cededb14c778837969a391d117eb484a871666598867ae3366696'
            'SKIP'
            '0c0cdcbff9e9ccd7cf36fbf5a26260fd74d5e227511b8baa1da5cecf7ae1385d'
            '8138d4a3c6bf7fad87ebc5f6245de1431b4e164a5034a330295b8852fda745f7'
            '67c6728727ba35e5ff7ae2b46c8313f8e3c78b88779b08dc18939541cfb952f3'
            '86ae71a925cbc69d274a038c4fae698d2c648a9d19eb5c489cd00d4f5aff04a5')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd kate-$pkgver
  msg "Add D16641 patch; release for 19.04"
  patch -p1 -i ../D16641.diff
  msg "Add D17297 patch; release for 19.04"
  patch -p1 -i ../D17297.diff
  msg "Add D17318 patch; release for 19.04"
  patch -p1 -i ../D17318.diff
  msg "Add KDEBUG 397913 patch; fix in 19.04"
  patch -p1 -i ../kdebug-397913.patch
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make 
}

package_kwrite() {
  pkgdesc="Text Editor"
  groups=(kde-applications kdebase)
  url="https://www.kde.org/applications/utilities/kwrite/"
  depends=(ktexteditor kactivities hicolor-icon-theme)

  cd build
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -type f -name '*kate*' -exec rm {} \;
  rm -r "$pkgdir"/usr/lib/qt/plugins/ktexteditor \
        "$pkgdir"/usr/share/doc/HTML/*/{kate,katepart} \
        "$pkgdir"/usr/share/katexmltools \
        "$pkgdir"/usr/share/locale/*/LC_MESSAGES/{kterustcompletion,tabswitcherplugin,ktexteditorpreviewplugin}.mo \
        "$pkgdir"/usr/share/plasma/plasmoids
}

package_kate() {
  pkgdesc="Advanced Text Editor"
  groups=(kde-applications kdebase)
  url="https://www.kde.org/applications/utilities/kate/"
  depends=(knewstuff ktexteditor threadweaver kitemmodels hicolor-icon-theme plasma-framework)
  optdepends=('konsole: open a terminal in Kate')

  cd build
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -type f -name '*kwrite*' -exec rm {} \;
  rm -r "$pkgdir"/usr/share/doc/HTML/*/kwrite
}
