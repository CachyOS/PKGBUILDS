# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kimageformats
pkgver=5.69.0
pkgrel=1.1
pkgdesc='Image format plugins for Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base openexr)
makedepends=(extra-cmake-modules karchive)
optdepends=('karchive: plugin for Krita and OpenRaster images')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
D25937.patch::"https://cgit.kde.org/kimageformats.git/patch/?id=c60e77c048d32ccf743cec695743b77b2b25dc87")
sha256sums=('1604d680b7ea6f1095b979098a5b4e8fd7a09d4070e80fbd75aca7b3820a7768'
            'SKIP'
            '07578d17eac72e6e0571af3fd31cfe985c934ee04f501658fbbc216085472165')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  msg "Add support for modern GIMP images/XCF files; fix in 5.70"
  patch -d $pkgname-$pkgver -p1 -i $srcdir/D25937.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
