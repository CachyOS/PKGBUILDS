# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kactivitymanagerd
pkgver=5.12.4
pkgrel=1.1
pkgdesc="System service to manage user's activities and track the usage patterns"
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kio)
makedepends=(extra-cmake-modules boost python)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
D10388.patch::"https://cgit.kde.org/kactivitymanagerd.git/patch/?id=6e60c6a3e46bf5922380415d376de51909f6b4f0")
sha256sums=('8fdde5f82b3f1ad5dc8bdeade3f196301a6bc1ebcac2a81eca2400f49b8996d2'
            'SKIP'
            '2e1c36ae7e54b342f7bbbf4886561eb9eff1a7b94b79fea6e44293bfbc0ab4ab')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add D10388 patch"
  patch -p1 -i ../D10388.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
