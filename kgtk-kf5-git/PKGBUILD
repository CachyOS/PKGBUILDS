# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Maintainer: pavbaranov <pavbaranov at gmail dot com>
 
pkgname=kgtk-kf5-git
pkgver=r7.34ed414
pkgrel=1
pkgdesc="KGTK Qt5/KF5 port"
arch=('x86_64')
url="https://github.com/sandsmark/kgtk"
license=('GPL2')
depends=(kdelibs4support gtk3 gtk2)
makedepends=(git extra-cmake-modules)
conflicts=(kgtk)
source=("git+https://github.com/sandsmark/kgtk.git")
sha256sums=('SKIP')
 
pkgver() {
  cd ${pkgname%%-*}
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
 
prepare() {
  mkdir -p ${pkgname%%-*}/build
}
 
build() {
  cd ${pkgname%%-*}/build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCQT_QMAKE_EXECUTABLE=/usr/bin/qmake 
  make
}
 
package(){
  cd ${pkgname%%-*}/build
  make DESTDIR="${pkgdir}" install
}
