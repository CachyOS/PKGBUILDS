pkgname=kded_rotation-git
pkgver=r5.be2bec7
pkgrel=1
pkgdesc="KDED module for handling automatic screen rotation"
arch=(x86_64)
url="https://github.com/dos1/kded_rotation"
license=(GPL)
depends=(kcoreaddons qt5-sensors kdbusaddons)
makedepends=(extra-cmake-modules git)
source=('git+https://github.com/dos1/kded_rotation.git')
sha256sums=('SKIP')

pkgver() {
    cd ${pkgname%-*}
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    }

prepare() {
  mkdir -p build
  }

build() {
  cd build
  cmake ../${pkgname%-*} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
  }

package() {
  cd build
  make DESTDIR="$pkgdir" install
  }
