# Maintainer: Piotr Gorski <lucjan.lucjanov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=21.08.0
pkgrel=1.1
pkgdesc='Archiving Tool'
arch=(x86_64)
url='https://apps.kde.org/ark/'
license=(GPL)
depends=(kparts kpty libarchive libzip kitemmodels hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('p7zip: 7Z format support' 'unrar: RAR decompression support' 'unarchiver: RAR format support'
            'lzop: LZO format support' 'lrzip: LRZ format support')
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
kdebug-441813.patch::"https://invent.kde.org/utilities/ark/-/commit/299feb809e3f2f95c8dc6c245671b9aa0b4976ea.patch"
)
sha256sums=('dd7daf89f1821edcb03bbe41344f85198f5709135a8d4d273fe7ba611230f522'
            'SKIP'
            'f27ed5348be1de14b27e46d7e57fd5ffcae10b6c251cc89b22d389378263dd4e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
    msg "Add kdebug 441813 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-441813.patch
}

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

