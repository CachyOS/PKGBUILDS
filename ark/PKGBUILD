# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=21.12.0
pkgrel=1
pkgdesc='Archiving Tool'
arch=(x86_64)
url='https://apps.kde.org/ark/'
license=(GPL)
depends=(kparts kpty libarchive libzip kitemmodels hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('p7zip: 7Z format support' 'unrar: RAR decompression support' 'unarchiver: RAR format support'
            'lzop: LZO format support' 'lrzip: LRZ format support')
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
    kdebug-298248.patch::"https://invent.kde.org/utilities/ark/-/commit/6b3b77e81a05154204ca89140afbec2021ea431b.patch"
    kdebug-443540.patch::"https://invent.kde.org/utilities/ark/commit/7dc57f9c9d9f709ab010c977cb420228d6caae75.patch"
)
sha256sums=('dd647821993fa553d94778ace172418cab55bdca6f35142df9a092a520ec30cf'
            'SKIP'
            '15f9efcae0969430504f0abaa3bf73a8efea1ff52c542f2fc607657d5837da9b'
            'ec4aa6fa8c0e801e73ee7c8b04b6535fefd2b05eca8f5d4b295d392f69c612ee')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

              
prepare() {
    msg "Add kdebug 298248 patch; fix in 22.04"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-298248.patch
    msg "Add kdebug 443540 patch; fix in 21.12.1"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-443540.patch
}

              
build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

