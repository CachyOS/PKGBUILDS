# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=17.12.2
pkgrel=1.2
pkgdesc='Archiving Tool'
arch=(x86_64)
url='https://kde.org/applications/utilities/ark/'
license=(GPL)
depends=(kparts kpty libarchive libzip kitemmodels hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=('p7zip: 7Z format support' 'unrar: RAR format support')
groups=(kde-applications kdeutils)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-390690.patch::"https://cgit.kde.org/ark.git/patch/?id=2af8d35e36"
kdebug-382601.patch::"https://cgit.kde.org/ark.git/patch/?id=92fe1c13bf")
sha256sums=('0bbd672ac232b6eb7669c8cd1125424e7a0cb3b5e4216e0063d03f1f5c050676'
            'SKIP'
            'fcc360253177831cc9846e59ddc1ece09ee3464cdd1fa8a9dd992a881f496e73'
            'b4c859098cd48f67b2d3d3ef9aa59109829d97e5cbe0239af4f9520c1b9afe57')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 390690 patch" # See: https://bugs.kde.org/show_bug.cgi?id=390690 - fixed in 17.12.3
  patch -p1 -i ../kdebug-390690.patch
  msg "Add KDEBUG 382601 patch" # See: https://bugs.kde.org/show_bug.cgi?id=382601
  patch -p1 -i ../kdebug-382601.patch
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

