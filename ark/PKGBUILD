# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=21.08.3
pkgrel=1.2
pkgdesc='Archiving Tool'
arch=(x86_64)
url='https://apps.kde.org/ark/'
license=(GPL)
depends=(kparts kpty libarchive libzip kitemmodels hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('p7zip: 7Z format support' 'unrar: RAR decompression support' 'unarchiver: RAR format support'
            'lzop: LZO format support' 'lrzip: LRZ format support')
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
    kdebug-444965.patch::"https://invent.kde.org/utilities/ark/commit/70ead5fe6d1005fbcbce252030bfb351f1c5e26f.patch"
    kdebug-445610.patch::"https://invent.kde.org/utilities/ark/commit/1f3a3b554056d4d3cdec651b4add086c0dcbe42b.patch"
)
sha256sums=('671858504c628991bf579b1a20edd822f6bb235b4fced618cf3906a919da3df3'
            'SKIP'
            '2786c84d4c629d6e72f852f3d90c5d9f4196262c4b76a3476ceb9e3e34660b96'
            '055e0dd7679f903a401c9e864a28882a5e77cd5893ed7e7fc2b1f157be8a7b72')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
              
              
prepare() {
    msg "Add kdebug 444965 patch; fix in 5.12"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-444965.patch
    msg "Add kdebug 445610 patch; fix in 5.12"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-445610.patch
}

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

