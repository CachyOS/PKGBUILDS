# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=20.08.3
pkgrel=1.1
pkgdesc='Archiving Tool'
arch=(x86_64)
url='https://kde.org/applications/utilities/ark/'
license=(GPL)
depends=(kparts kpty libarchive libzip kitemmodels hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('p7zip: 7Z format support' 'unrar: RAR decompression support' 'unarchiver: RAR format support'
            'lzop: LZO format support' 'lrzip: LRZ format support')
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
memory-leaks-fix.patch::"https://invent.kde.org/utilities/ark/-/commit/8ac8238b707f4a5dce57415587a87f3963baa19d.patch"
add-zstd-support.patch::"https://invent.kde.org/utilities/ark/-/commit/635df78f227c34afab4c1d9e5134fb81d22b1b82.patch")
sha256sums=('fedfb2fb9b9d5d7741e7166d2e13049d48a90501f22d046f9ae01469c5957c0e'
            'SKIP'
            '2cb812e5c7330e97d511992a36acd501898dc8f56bbba0ce1325703919b62ba3'
            'b329fd2db60b437603df62c809c155b8a7e3cbc7c181fddad4eaba5c711b9ba7')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Add memory leaks fix"
    patch -d $pkgname-$pkgver -p1 -i ../memory-leaks-fix.patch
    msg "Add support for zstd"
    patch -d $pkgname-$pkgver -p1 -i ../add-zstd-support.patch
}

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

