# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=krename
pkgver=5.0.0
pkgrel=2.1
pkgdesc="A very powerful batch file renamer"
arch=(x86_64)
url="http://www.krename.net"
license=(GPL)
depends=(kio kjs exiv2 podofo taglib hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
kdebug-391291.patch::"https://cgit.kde.org/krename.git/patch/?id=d9f23bd5dc9e87c9613136708dd93ff92ba9bc23")
sha256sums=('0a61761853787fd46b35f3a733cf87cde00de5df631728332a64c38c670bd28c'
            'SKIP'
            'd752b931dbd1118f06d05ed2ef8c4e14f10c239690bb8e39030a36bbbc2e9a81')
validpgpkeys=(D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heirecka@exherbo.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 391291 patch"
  patch -p1 -i ../kdebug-391291.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
