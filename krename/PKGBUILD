# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with upcomming bugfixes 
# made upon the original work by:
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=krename
pkgver=5.0.0
pkgrel=5.1
pkgdesc="A very powerful batch file renamer"
arch=(x86_64)
url="https://www.krename.net"
license=(GPL)
depends=(kio kjs exiv2 podofo taglib hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        krename-exiv2-0.27.patch
        kdebug-391291.patch::"https://invent.kde.org/utilities/krename/commit/d9f23bd5dc9e87c9613136708dd93ff92ba9bc23.patch")
sha256sums=('0a61761853787fd46b35f3a733cf87cde00de5df631728332a64c38c670bd28c'
            'SKIP'
            'c7e1663ef8dfb0c9a00db7c0e9815cc8fbdffbf1129236c1ce659c37d6c38089'
            'a10b06646a54aeddacf04355b33c4c78a3aa79ce6d6be2e8a00dff504f6ca293')
validpgpkeys=(D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heirecka@exherbo.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../krename-exiv2-0.27.patch # Fix build with exiv2 0.27
  msg "Applying kdebug 391291 patch"
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-391291.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
