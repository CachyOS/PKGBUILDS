# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# Build against the work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# So now, this version is exactly the same as 5.12.2 in oficial repository

pkgname=kdeplasma-addons
pkgver=5.12.4
pkgrel=1.1
pkgdesc='All kind of addons to improve your Plasma experience'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-workspace)
makedepends=(extra-cmake-modules kdoctools kross python kdesignerplugin qt5-webengine)
optdepends=('kross: comic applet'
            'purpose: Quickshare applet'
            'quota-tools: disk quota applet'
            'qt5-webengine: dictionary applet')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
D12134.patch::"https://cgit.kde.org/kdeplasma-addons.git/patch/?id=faa26503b68f29d2ce4d0f4c25116a7904d8f8c9"
D12287.patch::"https://cgit.kde.org/kdeplasma-addons.git/patch/?id=7a0b14bf13137b925be3a35c50889b7fef0faf77"
)
sha256sums=('e5b1d5280dda7bb652f59251cdb1846d62719589e78cab94d445642275edfb3d'
            'SKIP'
            'abc93c616e2341974d29668b59112d4a30bceb179c77e279bf39e6055f1f526f'
            '76226bbdb3e4113ca93ad3793213d870a4ea8babc4c39b1e910ca94a2e9a7296')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add D12134 patch"
  patch -p1 -i ../D12134.patch

  msg "Add D12287 patch"
  patch -p1 -i ../D12287.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
