https://bugs.kde.org/show_bug.cgi?id=342224
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=karchive
pkgver=5.55.0
pkgrel=1.1
pkgdesc='Qt 5 addon providing access to numerous types of archives'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules doxygen qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-404240.patch::"https://cgit.kde.org/karchive.git/patch/?id=3d5713ba37609c5b0a05c1819310338e8643a3b6")
sha256sums=('8475efa46cdc054d9fb6336e42c6075fb037921a9147d4e5aa564a5e58b79fd2'
            'SKIP'
            'cc4e680ff83b650c44d92b50562af82c91f1d7e79ae9f87c36d31f401819c445')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply KDEBUG 404240 patch; fix in 5.56"
  patch -p1 -i ../kdebug-404240.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
