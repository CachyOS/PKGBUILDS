# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami2
pkgver=5.68.0
pkgrel=1.2
pkgdesc='A QtQuick based components set'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(qt5-quickcontrols qt5-quickcontrols2 qt5-graphicaleffects)
makedepends=(extra-cmake-modules qt5-tools qt5-doc qt5-svg kpackage doxygen)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-417956.patch::"https://cgit.kde.org/kirigami.git/patch/?id=4d84d3f1d93fe7cc7db7530d13094a48745ef59c"
kdebug-419124.patch::"https://cgit.kde.org/kirigami.git/patch/?id=2e9355a6e584f392d19ba870054c5fc590782921")
sha256sums=('ad5f78afc916e9cb26f23918a6eb1983d4a57aa7e4f7314a8c23fb81e0fcaf4b'
            'SKIP'
            'f20a374eca54079aabd109e5638fe2ca2211e0049061f5a42663c54cd12ead9b'
            'a11751ae80f7458faf9b2e5b55681ba41e88915d6fbac8a2500d0814a93c6fb5')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Applying kdebug 417956 patch"
  patch -p1 -i ../kdebug-417956.patch
  msg "Applying kdebug 419124 patch"
  patch -p1 -i ../kdebug-419124.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

