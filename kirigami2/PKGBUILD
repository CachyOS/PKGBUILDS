# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami2
pkgver=5.56.1
pkgrel=0
pkgdesc='A QtQuick based components set'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(qt5-quickcontrols qt5-quickcontrols2 qt5-graphicaleffects)
makedepends=(extra-cmake-modules qt5-tools qt5-svg kpackage doxygen)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-402280.patch::"https://cgit.kde.org/kirigami.git/patch/?id=1da28ae4e63c67d42d7098cac60d9c088837ff60")
sha256sums=('c809cb550264389ad37911755b9b4123c5a234660f965da9d76d72aa6731f85a'
            'SKIP'
            '8ea5bc8920b7e79959f58f75c72678a2e95ffbdd135959ecd7ccd5ad61d29fc5')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Apply kdebug 402280 patch; fix in 5.57"
  patch -p1 -i ../kdebug-402280.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

