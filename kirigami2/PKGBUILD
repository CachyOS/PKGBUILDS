# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami2
pkgver=5.81.0
pkgrel=1.1
pkgdesc='A QtQuick based components set'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(qt5-quickcontrols qt5-quickcontrols2 qt5-graphicaleffects)
makedepends=(extra-cmake-modules qt5-tools qt5-doc qt5-svg kpackage doxygen)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-434079.patch::"https://invent.kde.org/frameworks/kirigami/commit/29dabab2f536827bc1ecec63f5a0b0a76f0809e5.patch"
)
sha256sums=('d6572cfd6effc60429fb780fd0436fa484c42a01b30c9cf0319c68b15c158cad'
            'SKIP'
            '4f064a01eedd4b759bca1b3d23ab8cf0bd33ba5d0b6a6a8e6e8270d3d7978763')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 434079 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-434079.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

