# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami2
pkgver=5.75.0
pkgrel=1.1
pkgdesc='A QtQuick based components set'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(qt5-quickcontrols qt5-quickcontrols2 qt5-graphicaleffects)
makedepends=(extra-cmake-modules qt5-tools qt5-doc qt5-svg kpackage doxygen)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-427165.patch::"https://invent.kde.org/frameworks/kirigami/-/commit/69435062a8abc00dfcb01fae3ce96711d9f453f5.patch"
kdebug-427661.patch::"https://invent.kde.org/frameworks/kirigami/-/commit/3afa72baf69d70cc6b7a39d49294828801a7bac2.patch")
sha256sums=('cde28a432e2ce56aae6f29eb557dc838af6846eb7dcba587113f73ebf11fca7d'
            'SKIP'
            'bef83fb9920262756fb5747e6273d9d7ceb0a1677ebae4c716978ec473a5b63b'
            '4c956e4fe820cbec4d5c1f82f874ccb496146e350f37c730596dd8f870498cc6')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 427165 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-427165.patch
    msg "Add kdebug 427661 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-427661.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

