# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami2
pkgver=5.79.0
pkgrel=1.3
pkgdesc='A QtQuick based components set'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(qt5-quickcontrols qt5-quickcontrols2 qt5-graphicaleffects)
makedepends=(extra-cmake-modules qt5-tools qt5-doc qt5-svg kpackage doxygen)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
commit-d19e1ede.patch::"https://invent.kde.org/frameworks/kirigami/-/commit/d19e1edea4f77d95608b076a8696efc0cb5fdecd.patch"
kdebug-433372.patch::"https://invent.kde.org/frameworks/kirigami/commit/d59709be6dc4053736fb1726e0f60977f543bdce.patch"
kdebug-428972.patch::"https://invent.kde.org/frameworks/kirigami/commit/772080cfd435d8f31e02e1487b8410b28d4908d2.patch"
kdebug-413841.patch::"https://invent.kde.org/frameworks/kirigami/commit/09e473deb88f8a6b17d0f007685463df0c6bcffa.patch"
#kdebug-426770.patch::"https://invent.kde.org/frameworks/kirigami/commit/7db7e3f4130a73050086f98deebfdcad6de132ae.patch"
)
sha256sums=('f7d39c9dbc20ce0fda1adfe27745ffcd8ec02ca79e8f344efc5b858fd1312b07'
            'SKIP'
            'b9e26974780f466f680dd1a1dc6b2bb43d7c48da82d93e75c006097b373e774a'
            'dbf7b592952a3b49e78e5ece7855ef18f4073cfe9c39c4ca3e355bc7281be17a'
            '7b5a4ce9325daf9ecc3e27ed8ec2ebb43ab1cb59254bb75c89451a19e5fb5e74'
            '31ed962ce94f9cee591fca71a3f166525009574e8976db98f9c473259caff289')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
    msg "Make Platform Theme share data when possible"
    patch -d $pkgname-$pkgver -p1 -i ../commit-d19e1ede.patch
    msg "Add kdebug 433372 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433372.patch
    msg "Add kdebug 428972 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-428972.patch
    msg "Add kdebug 413841 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-413841.patch
#    msg "Add kdebug 426770 patch"
#    patch -d $pkgname-$pkgver -p1 -i ../kdebug-426770.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

