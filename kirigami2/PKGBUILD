# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami2
pkgver=5.74.0
pkgrel=1.3
pkgdesc='A QtQuick based components set'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(qt5-quickcontrols qt5-quickcontrols2 qt5-graphicaleffects)
makedepends=(extra-cmake-modules qt5-tools qt5-doc qt5-svg kpackage doxygen)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
display-a-seperator-between-breadcrums-and-the-icons-to-the-left.patch::"https://invent.kde.org/frameworks/kirigami/-/commit/57bf842b414a69df37afa57c715d24455ba4684e.patch"
kdebug-425670.patch::"https://invent.kde.org/frameworks/kirigami/commit/6eaefba5001445fa7e198e4caa0bde738240d66f.patch"
kdebug-426738.patch::"https://invent.kde.org/frameworks/kirigami/commit/50c1156166d38ce1b6172d7b7131132a2cdb9fdb.patch")
sha256sums=('9706915f219ef9394b030dbb24974d4fddef091008954cf75cf97cc97586e35f'
            'SKIP'
            '36de6bdcb67c97a15b3bb970add967b1432b2c396df0c2ff6bbc2d0a06114af5'
            '34837e1bf8a83dbb24ef1a051f2eb8cbb140533968a358ee8f24973ff41dae89'
            '195612a3f0cac07f93e3907ffbdd0167ab6bba2254db36afa936aed2d4f40b14')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
    msg "Add display-a-seperator-between-breadcrums-and-the-icons-to-the-left patch"
    patch -d $pkgname-$pkgver -p1 -i ../display-a-seperator-between-breadcrums-and-the-icons-to-the-left.patch
    msg "Add kdebug 425670 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-425670.patch
    msg "Add kdebug 426738 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-426738.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

