# Maintainer: pavbaranov <pavbaranov @ gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami2
pkgver=5.71.0
pkgrel=1.3
pkgdesc='A QtQuick based components set'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(qt5-quickcontrols qt5-quickcontrols2 qt5-graphicaleffects)
makedepends=(extra-cmake-modules qt5-tools qt5-doc qt5-svg kpackage doxygen)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-421848.patch::"https://invent.kde.org/frameworks/kirigami/commit/bace64a0965f3769b21953d398b639443c4708af.patch"
kdebug-422965.patch::"https://invent.kde.org/frameworks/kirigami/commit/fd6e61386fef38dcf3d4dea58ba8d86c6eb971c9.patch")
sha256sums=('f323efb96a809dc9e572a0e68e04c4f485fc27f9ae65ffa3988830e348151356'
            'SKIP'
            '9aa72841cfa57e4c985f54281656e12695a8212a2f686955b7c98d49ad0ed668'
            '458bda339d48461d023d648b7a556a6395edec51abae147b62216aa5ea8be020')
validpgpkeys=('53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB') # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 421848 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-421848.patch
    msg "Add kdebug 422965 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-422965.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

