# Maintainer: pavbaranov <pavbaranov at gmail dot com>

pkgname=plasma-nx-audio-applet
pkgver=r20.c06a670
pkgrel=1
pkgdesc="NX Desktop audio applet for Plasma"
arch=('any')
url="http://www.nx-desktop.org/"
license=('GPL3')
depends=(plasma-workspace)
makedepends=(git qt5-tools extra-cmake-modules qt5-svg)

source=("git+https://github.com/nx-desktop/nx-audio-applet.git")
sha256sums=('SKIP')

pkgver() {
  cd nx-audio-applet
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  mkdir build
  cd build
  cmake ../nx-audio-applet \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
