# Maintainer: pavbaranov <pavbaranov@gmail.com>
# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
 
pkgname=kooka-git
pkgver=0.61.243.g723404f
pkgrel=1
pkgdesc='The scanning application and libkscan library for Plasma 5'
arch=('x86_64')
url="https://userbase.kde.org/Kooka"
license=('GPL3')
depends=('kio' 'sane' 'libpaper')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
optdepends=('ocrad' 'gocr')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
replaces=("${pkgname%-*}")
source=('git+git://anongit.kde.org/kooka.git')
sha256sums=('SKIP')

pkgver() {
  cd kooka
  git describe --long --tags | sed 's/^kooka.//;s/-/./g'
  }

prepare() {
  [ -d build ] || mkdir build
  }
    
build() {
  cd build
  cmake ../kooka \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
    
  make
  }
 
package() {
    cd build
    make DESTDIR="${pkgdir}/" install
  }
