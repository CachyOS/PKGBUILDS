# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kauth
pkgver=5.74.0
pkgrel=1.1
pkgdesc='Abstraction to system policy and authentication features'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kcoreaddons polkit-qt5)
makedepends=(extra-cmake-modules qt5-tools qt5-doc clang python-pyqt5 doxygen sip)
optdepends=('python-pyqt5: for the Python bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-415938.patch::"https://invent.kde.org/frameworks/kauth/commit/0068b2176afeb8661b8b26950c9bee79c8fb86ec.patch")
sha256sums=('e0aa6206c4bf9220d1c4882b8b77f9cf9202646b64b8a249a60a9d4b272e3bfc'
            'SKIP'
            'b2e32ba561e49575c32bedf78f9f5137773853aba1b179d8a3c6983d78f2f84b')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 415938 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-415938.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
