# Maintainer: pavbaranov <pavbaranov at gmail dot com>
#
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwin
pkgver=5.18.2
pkgrel=1.2
pkgdesc='An easy to use, but flexible, composited Window Manager'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kscreenlocker xcb-util-cursor plasma-framework kcmutils breeze kinit qt5-sensors qt5-script)
makedepends=(extra-cmake-modules qt5-tools kdoctools)
optdepends=('qt5-virtualkeyboard: virtual keyboard support for kwin-wayland')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-389665.patch::"https://cgit.kde.org/kwin.git/patch/?id=8180662233b92f58c28523d178030b6f007b84f5"
kdebug-274931.patch::"https://cgit.kde.org/kwin.git/patch/?id=41b02f235626b35344d3cb7268c6c16dde01f37a")
install=$pkgname.install
sha256sums=('9535d7c71ea4561a8b492ca111d62c5efeea2907070f8f510764fe5c2b29fc5a'
            'SKIP'
            '677d4ae8125f1ae364b40a077dfacfa4ad8a4f63b14216b0cca54fb70b739145'
            '3a8f922d152fb1f53bbc814d5536d90b03a99534cf998abbc3d910053f7a0438')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply kdebug-274931 patch"
  patch -p1 -i ../kdebug-274931.patch
  msg "Apply kdebug-389665.patch; fix in 5.18 (unknown bugfix version)"
  patch -p1 -i ../kdebug-389665.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
