# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwin
pkgver=5.19.5
pkgrel=2.2
pkgdesc='An easy to use, but flexible, composited Window Manager'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kscreenlocker xcb-util-cursor plasma-framework kcmutils kwayland-server breeze qt5-sensors qt5-script)
makedepends=(extra-cmake-modules qt5-tools kdoctools)
optdepends=('qt5-virtualkeyboard: virtual keyboard support for kwin-wayland')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-424571.patch::"https://invent.kde.org/plasma/kwin/-/commit/83aa8dccd25f0000ec8b98b699ff36486f14373d.patch"
kdebug-382136.patch::"https://invent.kde.org/plasma/kwin/commit/0d3c20d3866b4ed53a13b06d1aa37d3a974ba0f4.patch"
add-effect-that-animates-full-screen-changes.patch::"https://invent.kde.org/plasma/kwin/-/commit/e14a0111651fbe460c1887572836dee78c48e87d.patch"
kdebug-315829.patch::"https://invent.kde.org/plasma/kwin/commit/4e481d0e66768cd71904be92f9d3de2bed4be20c.patch"
)
install=$pkgname.install
sha256sums=('36afa37b773d31c0bcb8f4e32fa1f06cc33e15ad00e729ba36f120bbe034903b'
            'SKIP'
            'c68acbcc96c49753d45abf426caae03774829601574a8ab8f3561c8ee1c5920e'
            'dee864b4b58a376cc641d57cb8ccf974bf19695c7decdd8863547ea8f038ff7e'
            '6e3afddcdc283fc57e09e0934f056e69e5eccce85aa8230647317695115dba95'
            '670bd2fbe937500dd7222a16b4a6ab1f798eba9b1df4d19dd4dc790c5b1bc154')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
              
prepare() {
    msg "Add kdebug 424571 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-424571.patch
    msg "Add kdebug 382136 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-382136.patch
    msg "Add add-effect-that-animates-full-screen-changes patch; will be in 5.20"
    patch -d $pkgname-$pkgver -p1 -i ../add-effect-that-animates-full-screen-changes.patch
    msg "Add kdebug 315829 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-315829.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
