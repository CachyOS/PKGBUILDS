# Maintainer: pavbaranov <pavbaranov@gmail.com> 
# Contributor: worldofpeace
#for this version based on the original version make for Arch Linux by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwin
pkgver=5.12.2
pkgrel=1.3
pkgdesc='An easy to use, but flexible, composited Window Manager'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kscreenlocker xcb-util-cursor plasma-framework kcmutils breeze kinit qt5-sensors)
makedepends=(extra-cmake-modules qt5-tools kdoctools python qt5-virtualkeyboard)
optdepends=('qt5-virtualkeyboard: virtual keyboard support for kwin-wayland')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        D9848.diff
        D10100.diff
        D10180.diff
        D10181.diff
        D10281.diff
        D9638.diff
        kdebug-390113.patch::"https://cgit.kde.org/kwin.git/patch/?id=338c7362c9fbb37f1fb8beb9a50644b585d8135e"
	
kdebug-386231.patch::"https://cgit.kde.org/kwin.git/patch/?id=911176a887be639a1e123a3c633fb05870fda33d")
sha256sums=('7c92745d7c8b36cac596ba1913b3c672034e830f82b85ccf5f52762deb8cde52'
            'SKIP'
            'bf2a8d0a34e4079f11f83de611f3d547265888532215e6fd229f3cc12f3b97c3'
            '0aeeaff76df00460edf9cf4734fe6f96082d32b41e242b333160a7a34323e0db'
            '1eb9e53f847f63b4ac3935ca525d3c18ba0d28540e1026cdcc0e48755c80f1ac'
            '327670f2895e0711856a6069ac5e4a751a9b8a09c5f37a09117ba2cdfbb0f013'
            '6fa338210de655c47078423c69b2bed7456960fea4efa488b99e6cd39f5089b4'
            '95feb31a5942bbeb585fbb4c923b4aa2786dd39adfa67474a2c75af4de2603c0'
            '500be855eb5ee0b7d233faf8eb3fc6b77d5c22504e615fa31bdb7c93998949b3'
            '8e2c40810223d25774826a36a1a670b5beaa9875c5ae8439d8c516bff3818c8b')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver

  msg "Phabricator D9848 ("new blur") patch"
     patch -p1 -i ../D9848.diff  # See https://phabricator.kde.org/D9848
  msg "Phabricator D10100 ("permissions fix") patch"
     patch -p1 -i ../D10100.diff # See https://phabricator.kde.org/D10100
  msg "Phabricator D10180 ("higher default blur") patch"
     patch -p1 -i ../D10180.diff # See https://phabricator.kde.org/D10180
  msg "Phabricator D10181 ("removed fastblur")  patch"
     patch -p1 -i ../D10181.diff # See https://phabricator.kde.org/D10181
  msg "Phabricator D10281 ("noise blur")  patch"
     patch -p1 -i ../D10281.diff # See https://phabricator.kde.org/D10281
  msg "Phabricator D9638 ("new slide effect") patch"
     patch -p1 -i ../D9638.diff # See https://phabricator.kde.org/D9638
  msg "KDEBUG 390113 patch" # See: https://bugs.kde.org/show_bug.cgi?id=390113 Fixed in 5.12.3
     patch -p1 -i ../kdebug-390113.patch
  msg "KDEBUG 386231 patch" # See: https://bugs.kde.org/show_bug.cgi?id=386231 Fixed-in 5.12.3
     patch -p1 -i ../kdebug-386231.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
