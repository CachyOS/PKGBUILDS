# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=milou
pkgver=5.19.5
pkgrel=1.1
pkgdesc="A dedicated search application built on top of Baloo"
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(krunner kitemmodels)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-360359.patch::"https://invent.kde.org/plasma/milou/commit/3d62efff0aa271aa31f45f7706784e55c436d938.patch"
kdebug-421426.patch::"https://invent.kde.org/plasma/milou/commit/1cb6012b3ae8de035e4a964ef168d2ea941f4c21.patch")
sha256sums=('edc6e5c646a98f6f3eada802995c1308c84a5ae7aef467e9d4ce1deaf8a9040e'
            'SKIP'
            '9a1de9c276425cf15ca8e6c7fa25f07b6e639169c9e2cba68f4272ef94040d55'
            '961e47cdda397a4aa5f385a80ffb92a53d8a50a7b1f3340398e40c421c9cf3d4')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 360359 patch; fix in 5.20"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-360359.patch
    msg "Add kdebug 421426 patch; fix in 5.20"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-421426.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
