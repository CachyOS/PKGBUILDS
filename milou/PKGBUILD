# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=milou
pkgver=5.18.4.1
pkgrel=1.1
pkgdesc="A dedicated search application built on top of Baloo"
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(krunner kitemmodels)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-418109.patch::"https://cgit.kde.org/milou.git/patch/?id=92ecad19708db75fb35d912e172f026dc2d41606")
sha256sums=('51008aaac65876c83346261c9b89637ad2ac613e3b57bdb9b936d3dc661e4014'
            'SKIP'
            '9af1fe9de7ade9b0bde49cf0326eb2e7492d8a0f084ad3ec167256302a11bf02')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  msg "Apply kdebug 418109 patch; fix in 5.18.5"
  patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-418109.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
