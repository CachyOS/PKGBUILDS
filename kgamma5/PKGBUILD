# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kgamma5
pkgver=5.20.3
pkgrel=1.1
pkgdesc="Adjust your monitor's gamma settings"
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kconfigwidgets)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-398803.patch::"https://invent.kde.org/plasma/kgamma5/-/commit/c290f5a6b4bca08ef450fffc11b519052783418f.patch")
sha256sums=('6b1b1fbf0ee1af586c8d2d4168ef26e3b3499a1fbb87a0355684c0a5f27187dd'
            'SKIP'
            'cc148bd0f88a0212ecad0ff741c376b520c852538a87c1afe30dbdd1fd88c634')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 398803 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-398803.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
