# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ksysguard
pkgver=5.18.2
pkgrel=1.1
pkgdesc='Track and control the processes running in your system'
arch=(x86_64)
url='https://userbase.kde.org/KSysGuard'
license=(LGPL)
depends=(knewstuff libksysguard kinit)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('nvidia-utils: NVIDIA GPU usage')
groups=(plasma)
install=$pkgname.install
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-384515.patch::"https://cgit.kde.org/ksysguard.git/patch/?id=7a103a45e3496b57ed76f99e89ea18cbabdba8c7")
sha256sums=('53549f51cabf31bbf17c96492eae9025fad32c6659c412fe5c2eb5a4efbcf4e1'
            'SKIP'
            '1cf31f0006f71b42e0dcb5ba37646aa5d8df6abe3ef3f3838f158b80b4e74748')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Applying KDEBUG 384515 patch; fix in 5.19.0; it's important only for 12+ core CPU owners"
  patch -p1 -i ../kdebug-384515.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
