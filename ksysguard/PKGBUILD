# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ksysguard
pkgver=5.19.3
pkgrel=1.1
pkgdesc='Track and control the processes running in your system'
arch=(x86_64)
url='https://userbase.kde.org/KSysGuard'
license=(LGPL)
depends=(libksysguard kinit)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('nvidia-utils: NVIDIA GPU usage')
groups=(plasma)
install=$pkgname.install
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-329324.patch::"https://invent.kde.org/plasma/ksysguard/commit/8a64cd93e939063e1e592772a889b904222b974c.patch"
kdebug-423260.patch::"https://invent.kde.org/plasma/ksysguard/commit/3cd5873692cf58e7d046a402d125476538596811.patch"
kdebug-346228.patch::"https://invent.kde.org/plasma/ksysguard/commit/566d5faba288c9abdbc8436fea43d4c7207ea41e.patch"
)
sha256sums=('2f46c3b5aa22f2c676e6e1bf48a68326fc3febe8cdcc1a2083a7324e4ab00ede'
            'SKIP'
            '8addefe1a2edb5b2ba2fdea9bbdeb5fa668fb5d9408e413fecac9319ce765c1b'
            '067e4889bf5662b288205ba9f062f418a23e9b402fda319d335ba8a83e13c841'
            'b7eb193fa24f0e799c0778734faf64a5ed357ad5ffa29ffc1904c655d1725b07')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 329324 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-329324.patch
    msg "Add kdebug 423260 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-423260.patch
    msg "Add kdebug 346228 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-346228.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
