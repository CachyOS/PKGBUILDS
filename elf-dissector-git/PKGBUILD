# Maintainer: pavbaranov <pavbaranov at gmail dot com>

pkgname=elf-dissector-git
pkgver=r757.f1065fc
pkgrel=1
pkgdesc='Tools for inspecting, analyzing and optimizing ELF files'
arch=(x86_64)
url='https://invent.kde.org/sdk/elf-dissector.git'
license=(GPL)
depends=(kitemmodels libdwarf)
makedepends=(extra-cmake-modules git)
source=("git+https://invent.kde.org/sdk/elf-dissector.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname%-*}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../${pkgname%-*} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
