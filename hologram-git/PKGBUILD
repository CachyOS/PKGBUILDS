# Maintainer: pavbaranov <pavbaranov at gmail dot com>

pkgname=hologram-git
pkgver=r180.b877776
pkgrel=1
pkgdesc="Kirigami video player"
arch=('x86_64')
license=('FDL1.2' 'GPL2' 'LGPL')
url='https://invent.kde.org/ahiemstra/hologram'
makedepends=('git' 'extra-cmake-modules')
depends=('kirigami2' 'baloo')
source=('git+https://invent.kde.org/ahiemstra/hologram.git')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cmake -B build -S ${pkgname%-*} \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
