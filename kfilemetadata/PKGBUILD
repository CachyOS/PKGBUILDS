# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfilemetadata
pkgver=5.51.0
pkgrel=1.1
pkgdesc="A library for extracting file metadata"
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(ki18n karchive exiv2 poppler-qt5 taglib ffmpeg ebook-tools)
optdepends=('catdoc: Extract text from Office 98 files')
makedepends=(extra-cmake-modules catdoc doxygen qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
D16165.patch::"https://cgit.kde.org/kfilemetadata.git/patch/?id=5eee9ac75b7d6bb19795c2d3b964fe05fd8fc47c")
sha256sums=('d6db35f1ff5df56f495f713b0cc437625099758e0e5ee6a2ccf53e156adbde13'
            'SKIP'
            'eb0e195bbdac905a443a90c1032750c833bda018daa63a2750929c9999cbf2d8')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir build
  
  cd $pkgname-$pkgver
  msg "Add patches for KDEBUGs: 352856, 353848, 361259, 375131"
  patch -p1 -i ../D16165.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
