# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Maintainer: kitsunyan <`echo a2l0c3VueWFuQGFpcm1haWwuY2MK | base64 -d`>

_pkgname=krita
pkgname="${_pkgname}-minimal"
pkgver=4.2.8.2
pkgrel=1
pkgdesc='Edit and paint images (less KDE dependencies)'
arch=(x86_64)
url='https://krita.org'
license=(GPL3)
depends=(karchive kcompletion kcoreaddons ki18n kitemmodels kitemviews
         qt5-svg qt5-multimedia qt5-x11extras quazip gsl libraw exiv2 openexr fftw curl
         boost-libs giflib hicolor-icon-theme desktop-file-utils)
makedepends=(extra-cmake-modules kdoctools python boost eigen vc poppler-qt5 opencolorio
             python-pyqt5 libheif sip python-sip)
optdepends=('poppler-qt5: PDF filter'
            'ffmpeg: to save animations'
            'opencolorio: for the LUT docker'
            "krita-plugin-gmic: G'MIC plugin"
            'python-pyqt5: for the Python plugins'
            'libheif: HEIF filter')
provides=("${_pkgname}=${pkgver}")
conflicts=(calligra-krita krita-l10n "${_pkgname}")
source=("https://download.kde.org/stable/krita/4.2.8/${_pkgname}-$pkgver.tar.gz"{,.sig}
        'replace-code.patch'
        'krita-pyqt5-sip5.patch')
sha256sums=('80f741fa11245ff20a99f529f5f62def17791f3ad2c9aec5c5cf5440ae1fcba7'
            'SKIP'
            '9e8b41e1a5a7dffe2360386e4049392d83331094037f41fbdc8027d8e7c842a0'
            '68ad86663cabc05eca84bbb3fab367e86febddfb408e90d0733ecb0ccdf499eb')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD') # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build
  cd "${_pkgname}-$pkgver"
  patch -Np1 -i ../replace-code.patch
  patch -Np1 -i ../krita-pyqt5-sip5.patch
}

build() {
  cd build
  cmake "../${_pkgname}-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_DISABLE_FIND_PACKAGE_KF5KIO=TRUE \
    -DCMAKE_DISABLE_FIND_PACKAGE_KF5Crash=TRUE \
    -DBUILD_TESTING=OFF
  make -j4
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
