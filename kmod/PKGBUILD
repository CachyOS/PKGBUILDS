# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=kmod
pkgver=29
pkgrel=3.1
pkgdesc="Linux kernel module management tools and library"
arch=('x86_64')
url='https://git.kernel.org/pub/scm/utils/kernel/kmod/kmod.git'
license=('GPL2')
depends=('glibc' 'zlib' 'openssl' 'xz' 'zstd')
checkdepends=('linux-headers' 'libelf')
options=('strip' 'debug')
provides=('module-init-tools=3.16' 'libkmod.so')
conflicts=('module-init-tools')
replaces=('module-init-tools')
validpgpkeys=('EAB33C9690013C733916AC839BA2A5A630CBEA53')  # Lucas DeMarchi
source=("https://www.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar."{xz,sign}
        '0001-libkmod-module-check-new_from_name-return-value-in-g.patch'
        '0002-libkmod-add-a-library-notice-log-level-print.patch'
        "0003-libkmod-Set-builtin-to-no-when-module-is-created-fro.patch"
        "0004-libkmod-Prefer-builtin-index-over-builtin.alias.patch"
        "0005-depmod-Do-not-duplicate-builtin-index.patch"
        "0006-depmod-Stop-opening-modules.modinfo-once-per-module.patch"
        "0007-test-initstate-Check-for-negative-value-on-error.patch"
        "0008-libkmod-module-Fix-return-code-for-kmod_module_new_f.patch"
        "0009-libkmod-Add-helper-function-to-iterate-lookup-option.patch"
        "0010-libkmod-Update-docs-about-indexes-order.patch"
        "0011-libkmod-Add-lookup-from-module-name.patch"
        "0012-modinfo-Update-help-message-with-modulename.patch"
        "0013-modinfo-Allow-to-force-arg-as-module-name.patch"
        "0014-libkmod-Fix-use-of-sizeof-instead-of-ARRAY_SIZE.patch"
        "0015-docs-Add-missing-functions-to-documentation.patch"
        "0016-modprobe-Rename-rmmod_do_deps_list.patch"
        "0017-depmod-Add-support-for-excluding-a-directory.patch"
        "0018-modprobe-fix-the-NULL-termination-of-new_argv.patch"
        "0019-modprobe-remove-unneeded-variable-str_start.patch"
        'depmod-search.conf'
        'depmod.hook' 'depmod.script')
md5sums=('e81e63acd80697d001c8d85c1acb38a0'
         'SKIP'
         'e21279b7af431f2696c58b0f41aa4f48'
         'ea29e099d5f28c196012c5fe808cd8de'
         '29e51b26ba830251801c0a191b951e80'
         'afde1757c0332bbecad8ce69b05d4ec9'
         '5c0840a204fd88353ef55a691ab8f05f'
         'd655b12b95ab4223d2b3213d264b38f0'
         '1be394ad9981766b966f6ee4e2735375'
         'a3376537ba128b89cd0bbf827691bcfe'
         '8179ed7cc88a7a3493dcc0135a0fe3b4'
         'a9bf27dd26b7e955c08cee48520cfad0'
         'a94e7c4470aa7d4ae6cd68acd02b208e'
         'bb5aa28cef0522665177f369b810aa9c'
         '19e3a124f56c65d26b7635ecc7e102c4'
         '190f6f7408eefe0e44342e3b9ea7b37e'
         '2f9d62c4affbd908c5411ade2611a9a2'
         '64afbde6a00133fe823e776c0ec5fdcf'
         '1cade7983d785627c9ee11fdca4e8c55'
         '3450fa8738f3ade9690d38aab5a6a05c'
         '3416d3cd40f2594a1d6535100412c29b'
         'dd62cbf62bd8f212f51ef8c43bec9a77'
         'e179ace75721e92b04b2e145b69dab29'
         'b00253ca0d4ebfb2414e4596597bdebd')

prepare() {
  cd $pkgname-$pkgver

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --sysconfdir=/etc \
    --with-xz \
    --with-zlib \
    --with-zstd \
    --with-openssl

  make
}

check() {
  # As of kmod v20, the test suite needs to build some kernel modules, and thus
  # needs headers available in order to run. We depend on linux-headers, but
  # this is really only to try and make sure that *some* useable tree of kernel
  # headers exist. The first useable tree we find is good enough, as these
  # modules will never be loaded by tests.

  local kdirs=(/usr/lib/modules/*/build/Makefile)
  if [[ ! -f ${kdirs[0]} ]]; then
    printf '==> Unable to find kernel headers to build modules for tests\n' >&2
    return 1
  fi

  local kver kdir=${kdirs[0]%/Makefile}
  IFS=/ read _ _ _ kver _ <<<"$kdir"

  make -C "$pkgname-$pkgver" check KDIR="$kdir" KVER="$kver"
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d

  for tool in {ins,ls,rm,dep}mod mod{probe,info}; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/usr/lib/depmod.d/search.conf"

  # hook
  install -Dm644 "$srcdir/depmod.hook" "$pkgdir/usr/share/libalpm/hooks/60-depmod.hook"
  install -Dm755 "$srcdir/depmod.script" "$pkgdir/usr/share/libalpm/scripts/depmod"
}

# vim: ft=sh syn=sh et
