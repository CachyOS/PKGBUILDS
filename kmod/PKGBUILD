# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=kmod
pkgver=29
pkgrel=3.2
pkgdesc="Linux kernel module management tools and library"
arch=('x86_64')
url='https://git.kernel.org/pub/scm/utils/kernel/kmod/kmod.git'
license=('GPL2')
depends=('glibc' 'zlib' 'openssl' 'xz' 'zstd')
checkdepends=('linux-headers' 'libelf')
options=('strip' 'debug')
provides=('module-init-tools=3.16' 'libkmod.so')
conflicts=('module-init-tools')
replaces=('module-init-tools')
validpgpkeys=('EAB33C9690013C733916AC839BA2A5A630CBEA53')  # Lucas DeMarchi
source=("https://www.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar."{xz,sign}
        '0001-libkmod-module-check-new_from_name-return-value-in-g.patch'
        '0002-libkmod-add-a-library-notice-log-level-print.patch'
        "0003-libkmod-Set-builtin-to-no-when-module-is-created-fro.patch"
        "0004-libkmod-Prefer-builtin-index-over-builtin.alias.patch"
        "0005-depmod-Do-not-duplicate-builtin-index.patch"
        "0006-depmod-Stop-opening-modules.modinfo-once-per-module.patch"
        "0007-test-initstate-Check-for-negative-value-on-error.patch"
        "0008-libkmod-module-Fix-return-code-for-kmod_module_new_f.patch"
        "0009-libkmod-Add-helper-function-to-iterate-lookup-option.patch"
        "0010-libkmod-Update-docs-about-indexes-order.patch"
        "0011-libkmod-Add-lookup-from-module-name.patch"
        "0012-modinfo-Update-help-message-with-modulename.patch"
        "0013-modinfo-Allow-to-force-arg-as-module-name.patch"
        "0014-libkmod-Fix-use-of-sizeof-instead-of-ARRAY_SIZE.patch"
        "0015-docs-Add-missing-functions-to-documentation.patch"
        "0016-modprobe-Rename-rmmod_do_deps_list.patch"
        "0017-depmod-Add-support-for-excluding-a-directory.patch"
        "0018-modprobe-fix-the-NULL-termination-of-new_argv.patch"
        "0019-modprobe-remove-unneeded-variable-str_start.patch"
        "0020-modprobe-Fix-holders-removal.patch"
        "0021-modprobe-move-check-for-remove_holders-to-caller.patch"
        "0022-modprobe-Make-rmmod_do_module-contain-all-the-remova.patch"
        'depmod-search.conf'
        'depmod.hook' 'depmod.script')
md5sums=('e81e63acd80697d001c8d85c1acb38a0'
         'SKIP'
         'beead29e9aa5ad0fdef560f4b7480660'
         '060d341198a433a05d4fad166b9ab746'
         '484cbcc4f95dac360e3086d75ae5594e'
         '9a111fee82eb2415da410d5d7c8f2934'
         '630af5f261c3ed711e9645bcd776e2e9'
         '0b9ba28946a87b36543621bcf04e7d28'
         '1eb0e04a44ae0b502b2f7260f046050d'
         '9dc763116d209ebd401ee200e7c6afc7'
         '5de7958230a49c0c043a9ba6211e5bdb'
         'cbfb600dc3fae122e1dc8ffa9d856a40'
         'a9586d9a4720472e0053c27924c0d2cc'
         '79bac270d653b136557bf40fe5a66d2a'
         '3f99307b57963da8afd32333aa4a5f2b'
         'f2e55b5f50959363a134b76a2605e6fa'
         '18ae32ac9e62e9b2d47acb7cb8bf2807'
         '10fea8ab8922f5f47f06115ad55460fc'
         'df1b220a31c8c30419df967a016ed520'
         '81daccde75f92b860810e1f53c4b6894'
         'd667c29e883cb783ac83af7727e92fe8'
         '32c0749ff59d7b07056d74f8caa56233'
         '785c3ede4d836087628b9a7bdae5cdb8'
         '067183acbd232e6084b3444b800ab31f'
         'dd62cbf62bd8f212f51ef8c43bec9a77'
         'e179ace75721e92b04b2e145b69dab29'
         'b00253ca0d4ebfb2414e4596597bdebd')

prepare() {
  cd $pkgname-$pkgver

  local src
   for src in "${source[@]}"; do
     src="${src%%::*}"
     src="${src##*/}"
     [[ $src = *.patch ]] || continue
     echo "Applying patch $src..."
     patch -Np1 < "../$src"
   done
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --sysconfdir=/etc \
    --with-xz \
    --with-zlib \
    --with-zstd \
    --with-openssl

  make
}

check() {
  # As of kmod v20, the test suite needs to build some kernel modules, and thus
  # needs headers available in order to run. We depend on linux-headers, but
  # this is really only to try and make sure that *some* useable tree of kernel
  # headers exist. The first useable tree we find is good enough, as these
  # modules will never be loaded by tests.

  local kdirs=(/usr/lib/modules/*/build/Makefile)
  if [[ ! -f ${kdirs[0]} ]]; then
    printf '==> Unable to find kernel headers to build modules for tests\n' >&2
    return 1
  fi

  local kver kdir=${kdirs[0]%/Makefile}
  IFS=/ read _ _ _ kver _ <<<"$kdir"

  make -C "$pkgname-$pkgver" check KDIR="$kdir" KVER="$kver"
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d

  for tool in {ins,ls,rm,dep}mod mod{probe,info}; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/usr/lib/depmod.d/search.conf"

  # hook
  install -Dm644 "$srcdir/depmod.hook" "$pkgdir/usr/share/libalpm/hooks/60-depmod.hook"
  install -Dm755 "$srcdir/depmod.script" "$pkgdir/usr/share/libalpm/scripts/depmod"
}

# vim: ft=sh syn=sh et
