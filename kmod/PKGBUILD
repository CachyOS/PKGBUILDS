# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=kmod
pkgver=28
pkgrel=1.2
pkgdesc="Linux kernel module management tools and library"
arch=('x86_64')
url='https://git.kernel.org/pub/scm/utils/kernel/kmod/kmod.git'
license=('GPL2')
depends=('glibc' 'zlib' 'openssl' 'xz' 'zstd')
checkdepends=('linux-headers' 'libelf')
options=('strip' 'debug')
provides=('module-init-tools=3.16' 'libkmod.so')
conflicts=('module-init-tools')
replaces=('module-init-tools')
validpgpkeys=('EAB33C9690013C733916AC839BA2A5A630CBEA53')  # Lucas DeMarchi
source=("https://www.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar."{xz,sign}
        'depmod-search.conf'
        'depmod.hook' 'depmod.script'
        '0001-testsuite-Add-facility-to-skip-tests.patch'
        '0002-testsuite-Automatically-skip-tests-that-fail-when-sy.patch'
        '0003-Fix-modinfo-F-always-shows-name-for-built-ins.patch'
        '0004-libkmod-Fix-documentation-on-config-precedence-order.patch'
        '0005-depmod-fix-precedence-order.patch'
        '0006-Support-usr-local-for-configuration-files.patch'
        '0007-populate-modules-Use-more-bash-more-quotes.patch')
md5sums=('0a2b887b1b3dfb8c0b3f41f598203e56'
         'SKIP'
         'dd62cbf62bd8f212f51ef8c43bec9a77'
         'e179ace75721e92b04b2e145b69dab29'
         '18fb3d1f6024a5a84514c8276cb3ebff'
         '6bc5929131ee08944c32254c6b1e9a6c'
         'b513432772f5a4004b2b54e4186fbcac'
         '0faeb7f475dcae5f5aa840e2f1ac34d0'
         'b5a3ff477428293cd390f76309dfef7d'
         '7236133904f7ddaf4c2fbdfec86dfc95'
         '149e5df768dd6fd8f8cfc69561601733'
         '72f9850861bfc466a41cac2d2d62f509')

prepare() {
  cd "$pkgname-$pkgver"

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --sysconfdir=/etc \
    --with-xz \
    --with-zlib \
    --with-zstd \
    --with-openssl

  make
}

check() {
  # As of kmod v20, the test suite needs to build some kernel modules, and thus
  # needs headers available in order to run. We depend on linux-headers, but
  # this is really only to try and make sure that *some* useable tree of kernel
  # headers exist. The first useable tree we find is good enough, as these
  # modules will never be loaded by tests.

  local kdirs=(/usr/lib/modules/*/build/Makefile)
  if [[ ! -f ${kdirs[0]} ]]; then
    printf '==> Unable to find kernel headers to build modules for tests\n' >&2
    return 1
  fi

  local kver kdir=${kdirs[0]%/Makefile}
  IFS=/ read _ _ _ kver _ <<<"$kdir"

  make -C "$pkgname-$pkgver" check KDIR="$kdir" KVER="$kver"
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # extra directories
  install -dm755 "$pkgdir"/{etc,usr/lib}/{depmod,modprobe}.d

  for tool in {ins,ls,rm,dep}mod mod{probe,info}; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/usr/lib/depmod.d/search.conf"

  # hook
  install -Dm644 "$srcdir/depmod.hook" "$pkgdir/usr/share/libalpm/hooks/60-depmod.hook"
  install -Dm755 "$srcdir/depmod.script" "$pkgdir/usr/share/libalpm/scripts/depmod"
}

# vim: ft=sh syn=sh et
