# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=sddm-kcm
pkgver=5.19.5
pkgrel=1.1
pkgdesc='KDE Config Module for SDDM'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(GPL)
depends=(sddm knewstuff)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-407686.patch::"https://invent.kde.org/plasma/sddm-kcm/commit/8acacca14eba2c445b0e9dd8a9b4193cb48cfb7d.patch")
sha256sums=('3ba2c6821fc847fca2cb8123748bf05cdd9c45557a0ecab5dc43a37a5ebde738'
            'SKIP'
            '350e60befe3a2a75fd105f0603cd35ee5fe247ec578aa7ccad999bbbcc18842d')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 407686 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-407686.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

