# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=sddm-kcm
pkgver=5.14.4
pkgrel=1.1
pkgdesc='KDE Config Module for SDDM'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(GPL)
depends=(sddm libxcursor knewstuff)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-399526.patch::"https://cgit.kde.org/sddm-kcm.git/patch/?id=b3928bb58819c4306a23918762be7cd5aa663979"
kdebug-400424.patch::"https://cgit.kde.org/sddm-kcm.git/patch/?id=de088bdca917e833176e9beff7407b47ffed8e58")
sha256sums=('9fbd6af14d2c3b8be917c94a3c09f38a8de2730250d51edd2f6820f481d3b10f'
            'SKIP'
            'fecb2c50eaad9e9730eac15f0334bc83d0622f540dc7b3daffae6e9b6673aca9'
            '3afb1487f674c6dab3e6efab6a04abd534fee04e2e71b1433aee48eb46ddb82f')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add kdebug 399526 patch"
  patch -p1 -i ../kdebug-399526.patch
  msg "Add kdebug 400424 patch" #fix in 5.15.0
  patch -p1 -i ../kdebug-400424.patch 
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

