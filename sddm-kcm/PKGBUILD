# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=sddm-kcm
pkgver=5.12.4
pkgrel=1.1
pkgdesc='KDE Config Module for SDDM'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(GPL)
depends=(sddm libxcursor knewstuff)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
D12323.patch::"https://cgit.kde.org/sddm-kcm.git/patch/?id=ebdfab7e08671b5165f52118cabdf4d1d6c14bb2")
sha256sums=('072a8f71a381fff763c62e9a09752167a4cb3a0dca63674387c0464b7281a82e'
            'SKIP'
            'f5fdd41464e7d4b48ea1566b0980d405a63e3a1d9ea1d2927033b2b97b927974')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add D12323 patch"
  patch -p1 -i ../D12323.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

