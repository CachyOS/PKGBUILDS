# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with upcomming bug fixes
# made upon the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=sddm-kcm
pkgver=5.18.0
pkgrel=1.1
pkgdesc='KDE Config Module for SDDM'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(GPL)
depends=(sddm libxcursor knewstuff)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-414396.patch::"https://cgit.kde.org/sddm-kcm.git/patch/?id=cc33ec575c5e2142dd1a6c3a2563859a59e3fcd5")
sha256sums=('56de2fdd02f87e93a91dfa5083e47ffeb7479b40cd1044bbe40fa1dbe758a24b'
            'SKIP'
            'a84e2871d1bbf9f2b5e9cea8bc13248e8f0c4f894cca54ef1b125bb10a43247c')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply KDEBUG 342722 and 414396 patch; fix in 5.18.1"
  patch -p1 -i ../kdebug-414396.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

