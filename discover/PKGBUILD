# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=discover
pkgver=5.23.5
pkgrel=1.1
pkgdesc='KDE and Plasma resources management GUI'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(knewstuff kitemmodels kcmutils kidletime qt5-graphicaleffects appstream-qt archlinux-appstream-data
         hicolor-icon-theme kirigami2 discount kuserfeedback)
makedepends=(extra-cmake-modules plasma-framework packagekit-qt5 flatpak fwupd)
optdepends=('packagekit-qt5: to manage packages from Arch Linux repositories' 'flatpak: Flatpak packages support'
            'fwupd: firmware update support')
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
        kdebug-445226.patch::"https://invent.kde.org/plasma/discover/commit/0cd9956adb0086c3587569ece14a1384c1ee23fd.patch"
        kdebug-445142.patch::"https://invent.kde.org/plasma/discover/commit/dea79d2ef4c56018166e0097443bdb6c7d427f14.patch"
        # kdebug-427244.patch::"https://invent.kde.org/plasma/discover/commit/f34cdad8473cb326ebf631be220ae7ac1c8afc19.patch"
        # kdebug-444954.patch::"https://invent.kde.org/plasma/discover/commit/375aae3b2975bf2115b5eb55b116d5a736327e80.patch"
)
sha256sums=('f92397fe32b607a507a4b3a8ee52f37358ea75b1a9b3c1bca0f54dc1af3cf7cf'
            'SKIP'
            'c9950cef625b8791a5197463f5ba91b9e82ff312b0148f09f76e67b6cfa81010'
            '9e01c7ca97f4f7edd639598f8206ef663488607f82c532ce9962812faf011ea6')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  msg "Add kdebug 445226 patch; fix in 5.24"
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-445226.patch
  msg "Add kdebug 445142 patch; fix in 5.24"
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-445142.patch
  # msg "Add kdebug 427244 patch; fix in 5.24"
  # patch -d $pkgname-$pkgver -p1 -i ../kdebug-427244.patch
  # msg "Add kdebug 444954 patch; fix in 5.24"
  # patch -d $pkgname-$pkgver -p1 -i ../kdebug-444954.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DWITH_KCM=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
