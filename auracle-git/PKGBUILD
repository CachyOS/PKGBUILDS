pkgname=auracle-git
_pkgname=auracle
pkgver=r367.bcd5cee
pkgrel=1
pkgdesc='A flexible client for the AUR - fixed compilation with old abseil-cpp 20200225.2 => from forked repo'
arch=('x86_64' 'i686')
url="https://github.com/falconindy/auracle.git"
license=('MIT')
depends=('pacman' 'libcurl.so')
makedepends=('meson' 'git' 'perl')
checkdepends=('gtest' 'gmock')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://github.com/inglor/auracle.git#branch=update-abseil-wrap")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$_pkgname"

  local meson_args=(
    --prefix=/usr
    --buildtype=plain
    --default-library=static
  )

  [[ -d build ]] && meson_args+=(--wipe)

  meson build "${meson_args[@]}"

  ninja -C build
}

# check() {
#   meson test -C "$_pkgname/build"
# }

package () {
  cd "$_pkgname"

  DESTDIR=$pkgdir ninja -C build install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: et ts=2 sw=2
