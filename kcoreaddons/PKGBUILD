# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcoreaddons
pkgver=5.52.0
pkgrel=1.1
pkgdesc='Addons to QtCore'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base gamin python2)
makedepends=(extra-cmake-modules qt5-tools clang python-pyqt5 doxygen sip)
optdepends=('python-pyqt5: for the Python bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-400610.patch::"https://cgit.kde.org/kcoreaddons.git/patch/?id=eb916c305a5cd8683e7e8f955740a7c810220e19")
sha256sums=('49aaae09f4196f2aef7b78ea2ee4d290bbedd9fe3831680197e3a767697c78d3'
            'SKIP'
            '38f792d90bd66719a88716341a61f370c7cc29371f6323bdc38a5f104e2d4cf8')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG 400610 patch"
  patch -p1 -i ../kdebug-400610.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -D_KDE4_DEFAULT_HOME_POSTFIX=4 \
    -DBUILD_TESTING=OFF \
	-DGENERATE_PYTHON_MODULES=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
