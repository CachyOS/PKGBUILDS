# Maintainer: pavbaranov <pavbaranov @ gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=20.04.2
pkgrel=1.1
pkgdesc="KDE screenshot capture utility"
arch=(x86_64)
url='https://kde.org/applications/utilities/org.kde.spectacle'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose knewstuff kwayland qt5-tools)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kipi-plugins: export to various online services')
groups=(kde-applications kde-graphics)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-407489.patch::"https://invent.kde.org/graphics/spectacle/commit/889d407bd49b1f53e9dde101bc7d05e6ffc14d95.patch")
sha256sums=('39f16ca9c30de73d11dfc0da902c712bf260a1cd69c87bc950db896db8188866'
            'SKIP'
            '65fb7acc8cbfded57912df1f655b7fbd83187dd48f05d3de7471283bca783565')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Add kdebug 407489 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-407489.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
   -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

