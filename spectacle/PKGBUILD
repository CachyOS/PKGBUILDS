# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=21.08.3
pkgrel=1.1
pkgdesc='KDE screenshot capture utility'
arch=(x86_64)
url='https://apps.kde.org/spectacle/'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose knewstuff kwayland qt5-tools kimageannotator)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kipi-plugins: export to various online services')
groups=(kde-applications kde-graphics)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
    kdebug-432753.patch::"https://invent.kde.org/graphics/spectacle/commit/741b6fc4f06751caf14a6165c66f0900fd5fd353.patch"
    kdebug-443564.patch::"https://invent.kde.org/graphics/spectacle/commit/6188c743e47761251243a4b1e879e13c24bd0160.patch"
    kdebug-437368.patch::"https://invent.kde.org/graphics/spectacle/commit/731f8b16c6b5b01c2c3f785eba7a7ea9f4b682ac.patch"
    kdebug-431257.patch::"https://invent.kde.org/graphics/spectacle/commit/03026df2adbe589228c81e5a8a7b0ed5c87eeba3.patch"
    kdebug-392057.patch::"https://invent.kde.org/graphics/spectacle/commit/2fd91b51a0ce2d2cbc9f1725373fe2abeb96f19c.patch"
)
sha256sums=('bec40c64c11c930f637b949226e755e527c65708da865f13770434e18b293750'
            'SKIP'
            '923460fbd277af5fdd5b07b9bc04ad9e363cedd900fe658dfd3595b7c32bd611'
            'bef4a0f6270c790992a696ee911377bb63f7f59576f262758599a0f4290dea67'
            '3096796f8f8c96a0a96367334fa7dfee30d9b223cd778da543dd3f3883aa39c8'
            '3afe784b9fb346dd1e831828d175071fd9ce2484677c3e258c6fa83c47fb9cb3'
            '7a28bcdaf68099c287c454dda9b4d31ad7b058e03d3e1e72da0c5522d4bc4740')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
    msg "Add kdebug 432753 patch; fix in 21.12"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-432753.patch
    msg "Add kdebug 443564 patch; fix in 21.12"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-443564.patch
    # msg "Add kdebug 437368 patch; fix in 21.12"
    # patch -d $pkgname-$pkgver -p1 -i ../kdebug-437368.patch
    # msg "Add kdebug 431257 patch; fix in 22.04"
    # patch -d $pkgname-$pkgver -p1 -i ../kdebug-431257.patch
    # msg "Add kdebug 392057 patch; fix in 22.04"
    # patch -d $pkgname-$pkgver -p1 -i ../kdebug-392057.patch
}
              
build() {
  cmake -B build -S $pkgname-$pkgver \
   -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

