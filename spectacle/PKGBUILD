# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=20.08.3
pkgrel=1.1
pkgdesc="KDE screenshot capture utility"
arch=(x86_64)
url='https://kde.org/applications/utilities/org.kde.spectacle'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose knewstuff kwayland qt5-tools)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kipi-plugins: export to various online services')
groups=(kde-applications kde-graphics)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
#kdebug-372464.patch::"https://invent.kde.org/graphics/spectacle/commit/fa60f99470485143015b547b03ecba7811569480.patch"
kdebug-425995.patch::"https://invent.kde.org/graphics/spectacle/commit/0b3bb8a60ae70115d86b60d43a3df2ddcbded46c.patch"
#kdebug-425573.patch::"https://invent.kde.org/graphics/spectacle/commit/1e7b22b8c60575d4a12c267db818f8a15f17d500.patch"
commit-0f4e2979.patch::"https://invent.kde.org/graphics/spectacle/-/commit/0f4e2979cd35cae5ae2a7353ab6050de2cac415b.patch"
commit-f4dbec3f.patch::"https://invent.kde.org/graphics/spectacle/-/commit/f4dbec3f8ffdd7feb5538837e1d12bf75b1c1107.patch"
)
sha256sums=('843367e7e7b0f81c9f88a2a37e5d68a993bf2244a7139d7e7d674698a65ebc99'
            'SKIP'
            'ad3b371f072c15ce512370808db61ed92152aea03882e5cca87867ce1f1a04f4'
            'd66d35b47448dd918e5d7715ab9288fdda820ef5d21b5ec53187a755bf4da3ae'
            '8c45b99ef77b1dc70c764fe193777f6aef17bdf37578814be6851f74ae20c61c')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
#    msg "Add kdebug 372464 patch; fix in 20.12"
#    patch -d $pkgname-$pkgver -p1 -i ../kdebug-372464.patch
    msg "Add kdebug 425995 patch; fix in 20.12"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-425995.patch
#    msg "Add kdebug 425573 patch; fix in 20.12"
#    patch -d $pkgname-$pkgver -p1 -i ../kdebug-425573.patch
    msg "Add set copySaveLocation to false by default patch; fix in 20.12"
    patch -d $pkgname-$pkgver -p1 -i ../commit-0f4e2979.patch
    msg "Add allow to use Immediete Shuter mode patch; fix in 20.12"
    patch -d $pkgname-$pkgver -p1 -i ../commit-f4dbec3f.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
   -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

