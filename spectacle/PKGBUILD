# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=18.12.0
pkgrel=1.2
pkgdesc="KDE screenshot capture utility"
arch=(x86_64)
url='https://www.kde.org/applications/graphics/'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose knewstuff qt5-quickcontrols)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kipi-plugins: export to various online services')
conflicts=(kdegraphics-ksnapshot kscreengenie)
replaces=(kdegraphics-ksnapshot kscreengenie)
groups=(kde-applications kdegraphics)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
mem-leak-fix.patch
kdebug-394503.patch::"https://cgit.kde.org/spectacle.git/patch/?id=958fdc0a2532e30e7edfd1ec71f63fd3c87b5d35")
sha256sums=('ac10857ef87ea81147ad039d8632ec915960ebc3d69c3cdc86bd5f243cd8da5a'
            'SKIP'
            'ee8b0c67eb8d5ed0f00eb8a1380a41cf7049d5490056d4ab3b86235370e8eda5'
            '14a3b09a5bc1a2b4005f503355ff9128d437ced4c85fd4510cae8730d152cafc')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add potetnial memory leak fix patch"
  patch -p1 -i ../mem-leak-fix.patch
  msg "Add KDEBUG 394503 and 374009 patch"
  patch -p1 -i ../kdebug-394503.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_INSTALL_LIBDIR=lib \
   -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

