# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=21.04.3
pkgrel=1.1
pkgdesc='KDE screenshot capture utility'
arch=(x86_64)
url='https://apps.kde.org/spectacle/'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose knewstuff kwayland qt5-tools kimageannotator)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kipi-plugins: export to various online services')
groups=(kde-applications kde-graphics)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
kdebug-435083.patch::"https://invent.kde.org/graphics/spectacle/commit/84ca3c8057856addd8361c2a802fc3f4d728a7e2.patch"
commit-5cfa679b.patch::"https://invent.kde.org/graphics/spectacle/-/commit/5cfa679b234fc037601da73f4e27be9586c247c1.patch"
)
sha256sums=('5715ace40aaacb576d87674c032dc3b329c7e10bf1deb313f7825808a2b25a20'
            'SKIP'
            'd8b503ba8693012d2e07dbd078bb7283ed1f28d02df606abcf8ebbbca82817c9'
            'cc579e662e062944d86c95a6f3a567337517f6be4a1ab339410ec49e431d7811')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
    msg "Add kdebug 435083 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-435083.patch
    msg "Add commit 5cfa679b patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../commit-5cfa679b.patch
}
build() {
  cmake -B build -S $pkgname-$pkgver \
   -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

