# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=18.08.3
pkgrel=1.1
pkgdesc="KDE screenshot capture utility"
arch=(x86_64)
url='https://www.kde.org/applications/graphics/'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose knewstuff qt5-quickcontrols)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kipi-plugins: export to various online services')
conflicts=(kdegraphics-ksnapshot kscreengenie)
replaces=(kdegraphics-ksnapshot kscreengenie)
groups=(kde-applications kdegraphics)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
mem-leak-fix.patch::"https://cgit.kde.org/spectacle.git/patch/?id=9ab5a5c6f8ba7daf8467ab2cc52177827f2104f3")
sha256sums=('e9f7d09e2b1eb4d8d919a57b461e37e3f5b8d0ef152206e55deef28af2aa7946'
            'SKIP'
            '1b8894328bb7736ecc058fe91a5d53e3e63f7c6e3d523d494bce86f2621fe11e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../mem-leak-fix.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_INSTALL_LIBDIR=lib \
   -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

