# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kscreen
pkgver=5.20.1
pkgrel=1.1
pkgdesc="KDE's screen management software"
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(libkscreen kdeclarative qt5-graphicaleffects qt5-sensors)
makedepends=(extra-cmake-modules plasma-framework kcmutils)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-427930.patch::"https://invent.kde.org/plasma/kscreen/-/commit/49c5d9c5db553a745534099484a6f3e7c6b7bcf4.patch")
sha256sums=('37e2d32d1d0b707f28dfb77084a34af3f21602112819c468f1db74b44df3cdf7'
            'SKIP'
            '6517b6e51ab5a0c2a5def4e5d5021c1406cdfd805d1eab85d321bd28a1d0b070')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 427930 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-427930.patch
}
              
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
