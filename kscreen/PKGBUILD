# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kscreen
pkgver=5.18.5
pkgrel=1.1
pkgdesc="KDE's screen management software"
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(libkscreen kdeclarative qt5-graphicaleffects qt5-sensors)
makedepends=(extra-cmake-modules plasma-framework kcmutils)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-385672.patch::"https://cgit.kde.org/kscreen.git/patch/?id=538116fd109613284889134ae7ecbc2ba7bfc281"
kdebug-397586.patch::"https://cgit.kde.org/kscreen.git/patch/?id=b3b45721ee17828d5711ff88d5f1777b65c2834a"
kdebug-419625.patch::"https://cgit.kde.org/kscreen.git/patch/?id=cfd95e6bff4bfeadf95a7a475693b30ddfe30431")
sha256sums=('9b6238447a4a38babdff482724ae3d33786b211e8b4224aaadafaad7435f6ba2'
            'SKIP'
            'c1172cbb2cf943d446c0cccb0b468dbab099237796d63ba8ae537c67577c43bb'
            '6512376ed1eb03741442ec443f420fc69fa5e90cfaea1274ab627dca7f72d90f'
            '54f6bba09b3d53725d6aac818abad53049f90bdbb47446eb2d45355d1965513d')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  msg "Add kdebug 385672 patch; fix in 5.19.0"
  patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-385672.patch
  msg "Add kdebug 397586 patch; fix in 5.19.0"
  patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-397586.patch
  msg "Add kdebug 419625 patch; fix in 5.19.0"
  patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-419625.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
