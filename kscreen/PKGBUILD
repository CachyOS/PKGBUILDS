# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kscreen
pkgver=5.20.4
pkgrel=1.1
pkgdesc="KDE's screen management software"
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(libkscreen kdeclarative qt5-graphicaleffects qt5-sensors)
makedepends=(extra-cmake-modules plasma-framework kcmutils)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-427930.patch::"https://invent.kde.org/plasma/kscreen/-/commit/49c5d9c5db553a745534099484a6f3e7c6b7bcf4.patch"
kdebug-398803.patch::"https://invent.kde.org/plasma/kscreen/commit/b90337a30aec0e8c230e0520fb8591b68cd19fa4.patch")
sha256sums=('4063fae5cb40a22a98fd0cc217e9b0ea4aef6518203c4bbe2664d5d01dfb9d9c'
            'SKIP'
            '828cf0a4a05ba377edf494ab8121f0d6c34f71f9590908a68835cf6c47ccc94c'
            'b8bf48c5218fd72e8ac855bf96e0afd4c20952059098fb32db18fcd11b1a48e9')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 427930 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-427930.patch
    msg "Add kdebug 398803 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-398803.patch
}
              
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
