# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-cli-tools
pkgver=5.20.4
pkgrel=1.1
pkgdesc="Tools based on KDE Frameworks 5 to better interact with the system"
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kdesu kcmutils kactivities)
makedepends=(extra-cmake-modules kdoctools plasma-workspace)
optdepends=('plasma-workspace: for kcmshell5')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-398803.patch::"https://invent.kde.org/plasma/kde-cli-tools/commit/f3bccba4cfdff3bf624fef16df0031fd29c85528.patch")
sha256sums=('55f35158715bafdd51e448a2760327ed4f91c54fcd3da807dec2736d077b16a3'
            'SKIP'
            '03a27d13fa4b49037e77b8a2c8762e2a014edcd98f77c42e49dbeea819d8bdec')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
              
prepare() {
    msg "Add kdebug 398803 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-398803.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  ln -s /usr/lib/kf5/kdesu "$pkgdir"/usr/bin/
}
