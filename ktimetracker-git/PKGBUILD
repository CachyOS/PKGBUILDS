# Maintainer: pavbaranov <pavbaranov at gmail dot com>

pkgname=ktimetracker-git
pkgver=v5.0.1.r16.gb74f738
pkgrel=1
pkgdesc="Todo management and time tracker"
arch=(x86_64)
url="https://invent.kde.org/pim/ktimetracker.git"
license=(LGPL)
depends=(kcalcore kidletime kio hicolor-icon-theme)
makedepends=(extra-cmake-modules git)
source=('git+https://invent.kde.org/pim/ktimetracker.git')
md5sums=('SKIP')

pkgver() {
  cd "${pkgname%-*}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname%-*} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
