# Maintainer: pavbaranov <pavbaranov at gmail dot com>

pkgname=qnetload-git
pkgver=r125.44e1d1b
pkgrel=1
pkgdesc="Replament of xnetload"
arch=('x86_64')
url="https://github.com/cpina/qnetload"
license=('GPL3')
depends=('qt5-base')
makedepends=('git')
source=('git+https://github.com/cpina/qnetload.git')
md5sums=('SKIP')

pkgver() {
  cd "${pkgname%-*}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  mkdir -p build
  }

build() {
  cd build

  cmake ../${pkgname%-*}/src \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release 
  make
  }

package() {
  cd build

  make DESTDIR="$pkgdir" install
  }
