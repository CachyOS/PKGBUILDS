# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.56.0
pkgrel=1.1
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet desktop-file-utils)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-182367.patch::"https://cgit.kde.org/kio.git/patch/?id=2ae8a0c374cd175380945fc7fae1a38957272435"
D17065.patch::"https://cgit.kde.org/kio.git/patch/?id=8fb6431784e2db2368e2f6b951084201fddafd46"
kdebug-125102.patch::"https://cgit.kde.org/kio.git/patch/?id=f34c2cf0abf12a96806f9eaac5153dd764ea2215"
)
sha256sums=('d6fb0bdae9454cd67de19806e338fdcb72e8678a27e95fad3626491d8b1a4cd4'
            'SKIP'
            '42aa13431cf736c6ec4904d873c5568cd0558b305885ce13ec5fef0969b48ab3'
            '064b966bd50afb460969a1c3f53195090b1e1ff7b63ceadafb87a0f26f8b984f'
            'd755bea5fe74596b91daa6a170fa42cf6488e31db63706b833c7d41f9b271167')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Apply KDEBUG 182367 patch"
  patch -p1 -i ../kdebug-182367.patch
  msg "Fix memory leak - add D17065 patch"
  patch -p1 -i ../D17065.patch
  msg "Apply KDEBUG 125102 patch; fix in 5.57"
  patch -p1 -i ../kdebug-125102.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make -j5
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
