# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.71.0
pkgrel=1.1
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet ktextwidgets kded)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries' 'kio-fuse: to mount remote filesystems via FUSE')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-398944.patch::"https://invent.kde.org/frameworks/kio/commit/6cb0e3c3b155bea462204d6981068d22e840fdf7.patch")
sha256sums=('b972c8dede50be3e89babb5a536054759db2a87003e6df770c598c7c1c94b8d6'
            'SKIP'
            '6295ab660f6beabf4bd3bb2284ae9858570800dcbaaef4b288daad5536ea3db9')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add Arch Wiki to krunner sercher aka kdebug 398944 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-398944.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
