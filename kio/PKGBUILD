# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.55.0
pkgrel=1.2
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet desktop-file-utils)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-182367.patch::"https://cgit.kde.org/kio.git/patch/?id=2ae8a0c374cd175380945fc7fae1a38957272435"
D17065.patch::"https://cgit.kde.org/kio.git/patch/?id=8fb6431784e2db2368e2f6b951084201fddafd46"
kdebug-403074.patch::"https://cgit.kde.org/kio.git/patch/?id=2bba2c0795d77e893fdbea1da9dc3d79e45594d0"
kdebug-399380.patch::"https://cgit.kde.org/kio.git/patch/?id=c063b1ce848987c9d236d6ec3d9cf4f8c5f11627"
)
sha256sums=('2ac7944c97d6278e183193f61d31dd34430589093b09ad73b4e997bbe1b16ccc'
            'SKIP'
            '42aa13431cf736c6ec4904d873c5568cd0558b305885ce13ec5fef0969b48ab3'
            '064b966bd50afb460969a1c3f53195090b1e1ff7b63ceadafb87a0f26f8b984f'
            '10b02049a8f11884af1bc8905cfb79719a415b537e71fc4deb4e32657faa9c5c'
            '4ed58fede6213680bd6bef5f9fce7a7c0fea583d8534a2b85be551ee61de5c3e')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Apply KDEBUG 182367 patch"
  patch -p1 -i ../kdebug-182367.patch
  msg "Fix memory leak - add D17065 patch"
  patch -p1 -i ../D17065.patch
  msg "Apply KDEBUG 403074 patch; fix in 5.56"
  patch -p1 -i ../kdebug-403074.patch
  msg "Apply KDEBUG 399380 patch; fix in 5.56"
  patch -p1 -i ../kdebug-399380.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
