# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.72.0
pkgrel=1.1
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet ktextwidgets kded)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries' 'kio-fuse: to mount remote filesystems via FUSE')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-420966.patch::"https://invent.kde.org/frameworks/kio/-/commit/9f5f8540112e1178aa54a350f29b542c5de7df68.patch"
kdebug-424281.patch::"https://invent.kde.org/frameworks/kio/commit/a82d5712403de7817833b59d8089dfeea91a07b2.patch")
sha256sums=('0ab32bff63f542598e95be64a793eefe8f51b041fbb447256a4aa1c756214ba2'
            'SKIP'
            'e17efc562875d1ffbbb179db0952136242c59e769400d149270e1a78a6fbb2df'
            '5d14bfa0c25cbc53df21536720cc2bdfd0c3371259b23cdd05b189619cf38159')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 420966 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-420966.patch
    msg "Add kdebug 424281 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-424281.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
