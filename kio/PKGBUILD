# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# made on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.44.0
pkgrel=1.1
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet desktop-file-utils)
makedepends=(extra-cmake-modules kdoctools python doxygen qt5-tools)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-387387.patch::"https://cgit.kde.org/kio.git/patch/?id=032709c17f004d2fca50ff4bc485785519cf58ad"
D11107.patch::"https://cgit.kde.org/kio.git/patch/?id=a9625a3be6b18d92ce92b277c279160fdd625c0e")
sha256sums=('7a8fc6f0af101c4a16270d328806fbf96ee8855e756b033d6a08ce744e7071b7'
            'SKIP'
            '08e466ed4d721b9baa0575eef904c41698a62eabadad139897c0009228942f27'
            '0d0f4f730785dcb222ea9fa8a6c930e0765397f528141cef0f99dc6fd8e8c227')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 387387 patch" # See: https://bugs.kde.org/show_bug.cgi?id=387387 : 
  patch -p1 -i ../kdebug-387387.patch
  msg "Add D11107 patch" # See: https://phabricator.kde.org/D11107
  patch -p1 -i ../D11107.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
