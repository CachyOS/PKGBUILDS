# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with patches from upcoming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.70.0
pkgrel=3.2
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet ktextwidgets)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries' 'kio-fuse: to mount remote filesystems via FUSE')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-421213.patch::"https://cgit.kde.org/kio.git/patch/?id=8769b6360d87c1b688eac4d0ce97594351bad13c"
kdebug-421374.patch::"https://cgit.kde.org/kio.git/patch/?id=6452a34cf01d03d3167b38bc28a2fe8e13569021"
kdebug-418443.patch::"https://cgit.kde.org/kio.git/patch/?id=1cac602d9966f82a2e85440d74d9cc3ad014a1aa")
sha256sums=('0a99acc697f07eb3c27176c26f8904cb409628bfca46e2a1b0633cdcf84e4ce8'
            'SKIP'
            '2f29d954618db1b58b8c6407d5979e553957a397d0958b23ce5908e1b84f5df1'
            'a9ad7092aeb8d166c4f179e099a19b61b0e8f8b803aff89c4757216a0c577e61'
            'd3de85b5d66e52863a1a442f4d4a4f13445c4d9d667a9bab237168db78422e1a')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  msg "Add kdebug 421213 patch"
  patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-421213.patch
  msg "Add kdebug 421374 patch"
  patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-421374.patch
  msg "Add kdebug 418443 patch"
  patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-418443.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
