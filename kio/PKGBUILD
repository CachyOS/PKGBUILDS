# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with upcoming fixes
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.67.0
pkgrel=1.1
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet ktextwidgets)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries' 'kio-fuse: to mount remote filesystems via FUSE')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
D25117.patch::"https://cgit.kde.org/kio.git/patch/?id=8a834785c875b82a5ab460a652425109f7ac41df"
D27002.patch::"https://cgit.kde.org/kio.git/patch/?id=213ed50634c02d8ec4aa91f0c307cfdd6f78170d"
)
sha256sums=('47e2c266a59015d7bb83ea0cf8a035f85f871ae650a67abd3b95d5eb75ff54a0'
            'SKIP'
            'b763b44c5c1cc87282c4b6347dcdb7532b702ef29e4daf10416a80bac665af1c'
            '5a8eaa24f5b7c91339e2eb352e3b48daa7de6ef0df092603038f925bb03789f6')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply D25117 patch; fix in 5.68"
  patch -p1 -i ../D25117.patch
  msg "Apply D27002 patch; fix in 5.68"
  patch -p1 -i ../D27002.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
