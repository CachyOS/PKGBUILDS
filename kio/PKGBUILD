# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.45.0
pkgrel=1.3
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet desktop-file-utils)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
D11993.patch::"https://cgit.kde.org/kio.git/patch/?id=e504bc1fd56412ee7e9748a0dfafa537977ec1b5"
kdebug-209559.patch::"https://cgit.kde.org/kio.git/patch/?id=b49e4dd526f6f3495072d96a151ac439a811cb5c"
kdebug-199994.patch::"https://cgit.kde.org/kio.git/patch/?id=5a2c49ae1580ad4fe23255a68b6b2712db1979f2"
D12339.patch::"https://cgit.kde.org/kio.git/patch/?id=73d2036b09a9953ea4b1c5156c5578241b8a0d78")
sha256sums=('3e7512e819fe1831b15981641dba26a7dcbbdbccd79cbf076ceedae8e4116b15'
            'SKIP'
            '121e2c6a513b63d1af28687d93f33bc65d07857dc0f8941b490804a2daae0598'
            '6232ca93c39a71221efeac506b3da7a376e9946ca463ac031b8c9c473df5fffe'
            '2ead1e83e44df6caa31e45a665db8ec960a0bc8ed0f2f7e1219d0cbddee85ef0'
            '7ed621d9cabe15bc287a83a165829493f0af45b75e6ac32f0a0d46cd59eaaf47')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 354338, 338502, 196508, 1777743, 96638 patch" # Fixed in 5.46
  patch -p1 -i ../D11993.patch
  msg "Add KDEBUG 209559 patch" # Fixed in 5.46
  patch -p1 -i ../kdebug-209559.patch
  msg "Add KDEBUG 199994 patch" # Fixed in 5.46
  patch -p1 -i ../kdebug-199994.patch
  msg "Add D12339 patch" # Don't stat MTP
  patch -p1 -i ../D12339.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
