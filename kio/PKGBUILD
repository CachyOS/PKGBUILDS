# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.77.0
pkgrel=1.1
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet ktextwidgets kded)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries' 'kio-fuse: to mount remote filesystems via FUSE')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
#kdebug-182089.patch::"https://invent.kde.org/frameworks/kio/-/commit/e4032084407d4d0fc2be22fb5a4a4b74200e0d11.patch"
kdebug-319700.patch::"https://invent.kde.org/frameworks/kio/commit/61912633cfc7792b4a3e905ea601318d9e3e85d2.patch"
kdebug-322837.patch::"https://invent.kde.org/frameworks/kio/commit/c5dbb7c5452f1af637540efdf09061765c0ffb2d.patch"
kdebug-423765.patch::"https://invent.kde.org/frameworks/kio/commit/1534dd1391f947f37c0cadc30b875b62425063c8.patch"
)
sha256sums=('237c85c9dbcfcd366290512005b0e8498c10f3b300524633bbc9319a8327a14f'
            'SKIP'
            'c7d3c5dde173383411ba89057120a5a470e57fd3dfe3b8f6f820cf45bb3e344e'
            '1d358db42b749b00b75b7522ffe255eac26f182b9aadf0e9b2f478b46a688b7a'
            'f6175d811f1b69485092b9eab144150a7823655f711e24dccb2e3f41bc41b8ef')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
#    msg "Add kdebug 182089 patch; fix in 5.78"
#   patch -d $pkgname-$pkgver -p1 -i ../kdebug-182089.patch
    msg "Add kdebug 319700 patch; fix in 5.78"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-319700.patch
    msg "Add kdebug 322837 patch; fix in 5.78"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-322837.patch
    msg "Add kdebug 423765 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-423765.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
