# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.79.0
pkgrel=1.2
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet ktextwidgets kded)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'kio-fuse: to mount remote filesystems via FUSE')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-432610.patch::"https://invent.kde.org/frameworks/kio/commit/fc4d1cfc66e17f885ca4c22535031886832be685.patch"
kdebug-433127.patch::"https://invent.kde.org/frameworks/kio/commit/b33307901f30ec8d724a7e424838165746563d06.patch"
)
sha256sums=('767c67aab4e1a43d1d2ebf74f9deab21791150cc5f533be561dc1346d16e25e6'
            'SKIP'
            'c9542e1e2d27fdc769e9748415496af3a2145b5911ac3301a02ab66503383fa2'
            '0341b32208d5f7c40ba8e4696d086c64087499ca107053ce0f6a93500c8511c2')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Prepare previewjob.cpp for kdebug-433127.patch"
    sed -i "567 s/Name) ||/Name)/" $pkgname-$pkgver/src/widgets/previewjob.cpp
    sed -i "568 s/    thumb/|| thumb/" $pkgname-$pkgver/src/widgets/previewjob.cpp
    msg "Add kdebug 432610 patch; fix in 5.80"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-432610.patch
    msg "Add kdebug 433127"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433127.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
