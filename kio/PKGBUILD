# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.80.1
pkgrel=1.2
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet ktextwidgets kded)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'kio-fuse: to mount remote filesystems via FUSE')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-431638.patch::"https://invent.kde.org/frameworks/kio/-/commit/75222ea5d076bec2ed907ffeb65eaa90a522a02a.patch"
kdebug-433127.patch::"https://invent.kde.org/frameworks/kio/commit/b33307901f30ec8d724a7e424838165746563d06.patch"

)
sha256sums=('d971ce61a022f9e4d2d6be9c66917be271d0fb212795a151150e208e64cd5a28'
            'SKIP'
            '46e3b2bd665652afddf9944cddb8ab293902bfdcdcc9d64c656c40ddfe4677cc'
            '80a0119e83e184c402e5145804e7777198053e2a9b539352e66112025e74dba5')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Prepare previewjob.cpp for kdebug-433127.patch"
    sed -i "567 s/Name) ||/Name)/" $pkgname-$pkgver/src/widgets/previewjob.cpp
    sed -i "568 s/    thumb/|| thumb/" $pkgname-$pkgver/src/widgets/previewjob.cpp
    msg "Add kdebug 433127"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433127.patch

    msg "Add kdebug 431638 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-431638.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
