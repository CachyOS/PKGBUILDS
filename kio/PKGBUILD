# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with patches from upcoming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.69.0
pkgrel=1.2
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet ktextwidgets)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries' 'kio-fuse: to mount remote filesystems via FUSE')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-205954.patch::"https://cgit.kde.org/kio.git/patch/?id=a2335335ecfe4b211a2898594a21d52c79107b7a"
kdebug-411919.patch::"https://cgit.kde.org/kio.git/patch/?id=ae0beded1fd9533f0526e06ee52e892558051410"
kdebug-413091.patch::"https://cgit.kde.org/kio.git/patch/?id=570b48e656c792dfd9fd2314ab77b84afca87da0")
sha256sums=('09e4d37058bb5b5993536ea6b010c605b9d2f732e141e9903e2d5f64fea20de6'
            'SKIP'
            '43d721f56123c8aeab5d9d5f0a13f63e3b0f083338617b67acc605486c43bb12'
            '525ccda9de0176ad421ece884432d0e045beeef7cf0482770d2c08ae98a5ff81'
            '8f9c090dd791f808fd7c6f63fd2ab4138707cbff1b1e8e356edc4496a800a020')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Applying KDEBUG 205954 patch; fix in 5.70"
  patch -p1 -i ../kdebug-205954.patch
  msg "Applying KDEBUG 411919 patch; fix in 5.70"
  patch -p1 -i ../kdebug-411919.patch
  msg "Applying KDEBUG 413091 patch; fix in 5.70" # you should also build baloo-widgets from POLAUR
  patch -p1 -i ../kdebug-413091.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
