# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kservice
pkgver=5.75.0
pkgrel=1.1
pkgdesc='Advanced plugin and service introspection'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(ki18n kconfig kcrash kdbusaddons)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-398803.patch::"https://invent.kde.org/frameworks/kservice/-/commit/0112e0829cd53a0f152474cf93018f213d03f427.patch")
sha256sums=('17ceccf833d8da807dfaee2c3eb5c0ca7ee6b876c2454b5d6914a91eeb0a7f80'
            'SKIP'
            '5600394a6e5c7f3d856c3dbbcc67b59e375fd5382d9c4d9be84469a11cffea44')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 398803 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-398803.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
