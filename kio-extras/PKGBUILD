# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-extras
pkgver=20.08.3
pkgrel=1.1
pkgdesc="Additional components to increase the functionality of KIO"
arch=(x86_64)
url='https://www.kde.org/applications/internet/'
license=(LGPL)
depends=(kio kdnssd libssh smbclient libmtp phonon-qt5 syntax-highlighting kdsoap-ws-discovery-client libxcursor)
makedepends=(extra-cmake-modules kdoctools gperf taglib libappimage openexr kpty kactivities-stats)
optdepends=('qt5-imageformats: thumbnails for additional image formats' 'perl: info kioslave'
            'kimageformats: thumbnails for additional image formats' 'taglib: audio file thumbnails'
            'libappimage: AppImage thumbnails' 'icoutils: Windows executable thumbnails'
            'openexr: EXR format thumbnails' 'kpty: comic book thumbnails' 'kactivities-stats: recently used kioslave')
groups=(kde-applications kde-network)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-425789.patch::"https://invent.kde.org/network/kio-extras/commit/3440a169ba8e67fd674be48357dfeaf96a2dabfe.patch"
kdebug-420074.patch::"https://invent.kde.org/network/kio-extras/commit/015a265d32bc216d198410f11d63cbf84245c124.patch"
#commit-78a8c7d3.patch::"https://invent.kde.org/network/kio-extras/-/commit/78a8c7d3d5621fe1e716604f8a2177d565f6af95.patch"
)
sha256sums=('2c0001a2e864173988c5b0916de4511ff4c5ea711e96e928f9299fc4b44af344'
            'SKIP'
            'ef7a6105f0cb862252717901c96aaa48c3c85375f4bf8d5be1f4f7660e1672ef'
            'be40a87ceba9b68200e4d79a3b803a69e19f2fc9551508cee826853286e792e4')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Add kdebug 425789 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-425789.patch
    msg "Add kdebug 420074 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-420074.patch
#    msg "Make thumbnails faster patch"
#    patch -d $pkgname-$pkgver -p1 -i ../commit-78a8c7d3.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DLIBAPPIMAGE_LIBRARIES=libappimage.so \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
