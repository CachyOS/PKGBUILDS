# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-extras
pkgver=21.04.3
pkgrel=1.1
pkgdesc="Additional components to increase the functionality of KIO"
arch=(x86_64)
url='https://www.kde.org/'
license=(LGPL)
depends=(kio kdnssd libssh smbclient libmtp phonon-qt5 syntax-highlighting kdsoap-ws-discovery-client libxcursor)
makedepends=(extra-cmake-modules kdoctools gperf taglib libappimage openexr kactivities-stats)
optdepends=('qt5-imageformats: thumbnails for additional image formats' 'perl: info kioslave'
            'kimageformats: thumbnails for additional image formats' 'taglib: audio file thumbnails'
            'libappimage: AppImage thumbnails' 'icoutils: Windows executable thumbnails'
            'openexr: EXR format thumbnails' 'kactivities-stats: recently used kioslave')
groups=(kde-applications kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
kdebug-411919.patch::"https://invent.kde.org/network/kio-extras/-/commit/6ac8fd72edbb67d4a32cdfc455df8633db9ea2e0.patch"
)
sha256sums=('4af9f8a10e0c16006d97cf63439f91c05a04533ec6ee2ec0223dbfcc1d39d62d'
            'SKIP'
            'a8c0cae46ab8d1a275d92e17697defa929eebaf5c90ab1d6bf0f0f1f66c65d9b')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
    msg "Preparing kdebug 411919 patch"
    sed -i "43,53d" ../kdebug-411919.patch
    msg "Add kdebug 411919 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-411919.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DLIBAPPIMAGE_LIBRARIES=libappimage.so \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
