# Maintainer: <pavbaranov at gmail dot com>

pkgname=jreen-qt5-git
pkgver=1.3.0.2.gc04c229
pkgrel=1
epoch=1
pkgdesc='Free and Opensource Jabber library, written in C++ using cross-platform framework Qt. - development version'
arch=('x86_64')
url='https://github.com/euroelessar/jreen'
license=('GPL2')
depends=('gsasl' 'qt5-base' 'speex')
makedepends=('git' 'cmake')
provides=('jreen' 'jreen-git')
conflicts=('jreen' 'jreen-git' 'jreen-qt5')
source=("git+https://github.com/euroelessar/jreen.git")
md5sums=('SKIP')


pkgver() {
  cd jreen
  git describe --long --tags | sed 's/^v//;s/-/./g'
}

prepare() {
  mkdir -p build
  }
  
build() {
  cd build
  cmake ../jreen \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
