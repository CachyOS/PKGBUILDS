# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with fixes from upcomming releases
# made on original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet
pkgver=5.53.0
pkgrel=1.1
pkgdesc='Secure and unified container for user passwords'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(knotifications kiconthemes kservice gpgme)
makedepends=(extra-cmake-modules kdoctools boost doxygen qt5-tools)
optdepends=('kwalletmanager: Configuration GUI')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
D16520.patch::"https://phabricator.kde.org/file/data/hbvvgeg2pny3nkxm4qdi/PHID-FILE-c44xp4ewzox5iackifgj/D16520.diff")
sha256sums=('80f1d9dd7157147f4371e966f1d01772a7912914ba994f90593382fe4561bb8c'
            'SKIP'
            '6ed033977dd000fa7a85fc16325777ce1dc2d957da226c1194fba37a3f2fe6b1')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../D16520.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
