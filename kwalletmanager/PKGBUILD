# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwalletmanager
pkgver=20.04.2
pkgrel=1.1
pkgdesc='Wallet management tool'
arch=(x86_64)
url='https://www.kde.org/applications/system/kwalletmanager/'
license=(LGPL)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kcmutils kdoctools)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-423747.patch::"https://invent.kde.org/utilities/kwalletmanager/-/commit/9cf4ab73cd9b6eb38ddf8f55dca797510de513f0.patch")
sha256sums=('0f4d29c5b8d2e8fe05506eecf36761fdb36546e385d99bb2fbc596bc7bee8295'
            'SKIP'
            '6c6817e8f7ac69b2329653ee77921e90a78e0dea63d1e4d110fc75cbef7926af')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Add kdebug 423747 patch; fix in 20.04.3"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-423747.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

