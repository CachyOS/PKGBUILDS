# Maintainer: Damian N. <nycko123 at gmail>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=5.2.0
pkgrel=1.1
pkgdesc="A powerful BitTorrent client for KDE"
arch=(x86_64)
url='https://www.kde.org/applications/internet/ktorrent/'
license=(GPL2)
depends=(libktorrent kcmutils knotifyconfig kross phonon-qt5)
makedepends=(extra-cmake-modules kdoctools boost plasma-workspace kdnssd taglib syndication kplotting kdewebkit geoip)
optdepends=('plasma-workspace: shutdown plugin' 'kdnssd: zeroconf plugin' 'taglib: mediaplayer plugin' 'geoip: info widget plugin'
            'syndication: syndication plugin' 'kdewebkit: search plugin, syndication plugin' 'kplotting: statistics plugin')
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
         ktorrent-taglib-link.patch::"https://invent.kde.org/network/ktorrent/-/commit/f4c1c91d.patch"
         Fix-starting-in-tray-by-removing-redundant-show-in-main.patch::"https://invent.kde.org/network/ktorrent/-/commit/0a9cdd1be67700511af50e2393152eb0ea298323.patch")
sha256sums=('d1031d745c1ace02d11b59c426610aee779d208e3d1ae81f8559c265173732ff'
            'SKIP'
            '0a894fe4b7c929451c7947d00e767560a99e3d53f5d7d0ac20e5ae8347ee69ee'
            '30f2b1675ae554fe7bdb7accbe5b69d985bd8426fbb50c9a89b6fd4a8d2c0e06')
validpgpkeys=(1EE5A3205904BAA2B88C0A9D24FD31940095C0E1) # Andrius Å tikonas <andrius@stikonas.eu>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../ktorrent-taglib-link.patch # Fix linking to taglib
  patch -d $pkgname-$pkgver -p1 -i ../Fix-starting-in-tray-by-removing-redundant-show-in-main.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DWITH_SYSTEM_GEOIP=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
