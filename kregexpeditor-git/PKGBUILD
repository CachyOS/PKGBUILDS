# Maintainer: pavbaranov <pavbaranov at gmail dot com>

pkgname=kregexpeditor-git
pkgver=r654.9c1811e
pkgrel=1
pkgdesc="GUI for creating and editing regular expressions"
arch=(x86_64)
url="https://invent.kde.org/utilities/kregexpeditor.git"
license=('GPL')
depends=('hicolor-icon-theme' 'ktextwidgets')
makedepends=('git' 'extra-cmake-modules' 'python' 'kdoctools')
source=('git+https://invent.kde.org/utilities/kregexpeditor.git')
md5sums=('SKIP')

pkgver() {
  cd "${pkgname%-*}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
	mkdir build
}

build() {
	cd build
	cmake ../${pkgname%-*} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DKDE_INSTALL_LIBDIR=lib \
		-DBUILD_TESTING=OFF
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
