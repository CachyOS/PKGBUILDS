# Maintainer: pavbaranov <pavbaranov at gmail dot com>

pkgname=plasma-nx-notifications-applet
pkgver=r20.aa5ae59
pkgrel=1
pkgdesc="NX Desktop notifications applet for Plasma"
arch=('x86_64')
url="http://www.nx-desktop.org/"
license=('GPL3')
depends=(plasma-framework)
makedepends=(git qt5-tools extra-cmake-modules qt5-svg)

source=("git+https://github.com/nx-desktop/nx-notifications-applet.git")
sha256sums=('SKIP')

pkgver() {
  cd nx-notifications-applet
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  mkdir build
  cd build
  cmake ../nx-notifications-applet \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
