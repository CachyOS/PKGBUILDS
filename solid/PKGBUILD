# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=solid
pkgver=5.44.0
pkgrel=1.1
pkgdesc='Hardware integration and detection'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-declarative' 'media-player-info' 'upower' 'udisks2')
makedepends=('extra-cmake-modules' 'qt5-tools' 'doxygen')
groups=('kf5')
#source=("$pkgname-5.43.0.rc1::git://anongit.kde.org/$pkgname.git#tag=v5.43.0-rc1")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
kdebug-391738.patch::"https://cgit.kde.org/solid.git/patch/?id=3b642556c2039fac6443c4b956c66ff176f65f15")
sha256sums=('01e3b5c35373abd62f6d6951b8798d5a9f460b68302103832d59665d2a29bc23'
            'SKIP'
            'e0d1b3d7a9ddcc27f3b5d80e114379afc78cf30c8cb4fb30e95ec84af30cc45f')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 391738 patch" # See: https://bugs.kde.org/show_bug.cgi?id=391738
  patch -p1 -i ../kdebug-391738.patch
  }

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
