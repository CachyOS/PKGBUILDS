# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=solid
pkgver=5.43.0
pkgrel=1.1
pkgdesc='Hardware integration and detection'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-declarative' 'media-player-info' 'upower' 'udisks2')
makedepends=('extra-cmake-modules' 'qt5-tools' 'doxygen')
groups=('kf5')
#source=("$pkgname-5.43.0.rc1::git://anongit.kde.org/$pkgname.git#tag=v5.43.0-rc1")
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
kdebug-389479.patch)
sha256sums=('699f6bf5d502ec43783366bff508433682a85f02d9bc595fb3d9d0da86f8c260'
            'SKIP'
            '3fdc11744cab61216ba6f1bd9eecb1e066d7b989161b66f159fff452c4e51973')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 389479 patch" # See: https://bugs.kde.org/show_bug.cgi?id=389479
  patch -p1 -i ../kdebug-389479.patch
  }

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
