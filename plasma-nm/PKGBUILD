# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-nm
pkgver=5.20.5
pkgrel=1.1
pkgdesc='Plasma applet written in QML for managing network connections'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(GPL2)
depends=(plasma-workspace modemmanager-qt networkmanager-qt qca)
makedepends=(extra-cmake-modules openconnect)
optdepends=('openconnect: Cisco AnyConnect VPN plugin')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-398803.patch::"https://invent.kde.org/plasma/plasma-nm/commit/e77940757740d0d9079db6d4cebeda89dc132fb0.patch")
sha256sums=('468df2bf99907382125a4822322d23c71e1a59f792eba497d89483dc94180e18'
            'SKIP'
            '37bba83cce56534883f84c3eb677a47eb756b86ec4f346e54ede79f5f9732a15')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 398803 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-398803.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
