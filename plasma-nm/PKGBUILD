# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with upcomming patches
# made on original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-nm
pkgver=5.14.4
pkgrel=1.3
pkgdesc='Plasma applet written in QML for managing network connections'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(GPL2)
depends=(plasma-workspace modemmanager-qt networkmanager-qt qca mobile-broadband-provider-info)
makedepends=(extra-cmake-modules kdoctools openconnect kdesignerplugin)
optdepends=('openconnect: Cisco AnyConnect VPN plugin')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-401020.patch::"https://cgit.kde.org/plasma-nm.git/patch/?id=4b549b2d37d8f942b6f821077b87baede447f791"
kdebug-379756.patch::"https://cgit.kde.org/plasma-nm.git/patch/?id=d3b466a2da7706bfe0ba4b6ec80890ecded667b4")
sha256sums=('b3cfa41e0116b896a121ffe5ae0d6bc4cbc0f599d89e7fddff0feb8595525292'
            'SKIP'
            '7dd9228297b89aa93193827cdbbec4a6e2ae1c7246548924e4edea26a18d3129'
            '80b56b2788f016bad57f9fdd24312e0b772a5e405e94c122b343023f6caf39c0')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG 401020 patch" # Fix in 5.15.0
  patch -p1 -i ../kdebug-401020.patch
  msg "Add KDEBUG 379756 patch"
  patch -p1 -i ../kdebug-379756.patch
} 

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make 
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
