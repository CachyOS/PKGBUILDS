# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-nm
pkgver=5.21.4
pkgrel=1.1
pkgdesc='Plasma applet written in QML for managing network connections'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(GPL2)
depends=(plasma-workspace modemmanager-qt networkmanager-qt qca)
makedepends=(extra-cmake-modules openconnect)
optdepends=('openconnect: Cisco AnyConnect VPN plugin')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-435430.patch::"https://invent.kde.org/plasma/plasma-nm/commit/64dc6234b6980172bb53084c16a0e4e693d6011a.patch"
)
sha256sums=('e3d01fdd78ee6d1b0bc9753e1386ffb03c318498feac856fae377a397d6ec1bf'
            'SKIP'
            '46b89433fc0ffd156c2998f97ae5a79282ddbee00e8c87266f7149caa8c604be')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 435430 patch; fix in 5.21.5"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-435430.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
