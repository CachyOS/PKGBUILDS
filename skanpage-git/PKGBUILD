# Maintainer: pavbaranov <pavbaranov at gmail dot com>

pkgname=skanpage-git
pkgver=r48.b265d4c
pkgrel=1
pkgdesc='Multipage document scanner application'
arch=('x86_64')
url='https://invent.kde.org/sars/skanpage.git'
license=('GPL')
depends=('libksane' 'kxmlgui')
#optdepends=('')
makedepends=('git' 'extra-cmake-modules')
source=("git+https://invent.kde.org/astippich/skanpage.git")
# Alternatywnie rozwijany skanpage z OCR jest pod: https://invent.kde.org/smitpatil/skanpage
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cmake -B build -S ${pkgname%-*} \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release 
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
