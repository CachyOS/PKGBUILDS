# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knotifications
pkgver=5.54.0
pkgrel=1.1
pkgdesc='Abstraction for system notifications'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(libcanberra libdbusmenu-qt5 kwindowsystem kconfig kcodecs kcoreaddons qt5-speech)
makedepends=(extra-cmake-modules qt5-tools doxygen)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
D18526.patch::"https://cgit.kde.org/knotifications.git/patch/?id=874cb8e5a728f65f2639a81b88d5d82d08f7e8c3")
sha256sums=('d6c7ea727ce6464c7aa65eadad1b81cc03906c978373a0991b9003a7f8a6efa9'
            'SKIP'
            'a6d7f503de8991c064d070d3f9af34536f33683e3a9ac0c258034465514372b2')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Fix memory leak when passing icon data to java"
  patch -p1 -i ../D18526.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make -j5
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
