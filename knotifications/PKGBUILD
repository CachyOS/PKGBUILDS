# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knotifications
pkgver=5.78.0
pkgrel=1.1
pkgdesc='Abstraction for system notifications'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(libcanberra libdbusmenu-qt5 kwindowsystem kconfig kcoreaddons qt5-speech)
makedepends=(extra-cmake-modules qt5-tools qt5-doc doxygen)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-423757.patch::"https://invent.kde.org/frameworks/knotifications/commit/d1bb15dc4892d26201d21f12362979736df1d50c.patch")
sha256sums=('397fc21bbbaf235fd092f3bde731d2c0f1736ed4595e4a4cfa4b1e4328ee2339'
            'SKIP'
            '5aec24a236561bbc31be6f9a768990e9dc298b065e6bf115ebcee82ae58c2209')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 423757 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-423757.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
