# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-widgets
pkgver=20.04.1
pkgrel=1.1
pkgdesc="Widgets for Baloo"
arch=(x86_64)
url='https://community.kde.org/Baloo'
license=(LGPL)
depends=(baloo)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-413091.patch::"https://cgit.kde.org/baloo-widgets.git/patch/?id=50fdd3e154ffac3b4b42cc03735255aca8272ae8")
sha256sums=('00f271d90c92ac4be978270fee323a8e8617cb975e0ea7d7ecb1b00d773a9bf4'
            'SKIP'
            'f5449697f2d67be34c07581b6fc3fb011d59a5c9e203aaf57d89b9ef3be8dd47')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  msg "Applying kdebug 413091 patch" # You should to build KIO from POLAUR, too
  patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-413091.patch
  sed -i 's/KF5_MIN_VERSION "5.70.0"/KF5_MIN_VERSION "5.69.0"/g' ${srcdir}/${pkgname}-${pkgver}/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

