# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin
pkgver=21.08.3
pkgrel=1.2
pkgdesc="KDE File Manager"
arch=(x86_64)
url="https://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(baloo-widgets knewstuff kio-extras kcmutils kparts kactivities kuserfeedback)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kde-cli-tools: for editing file type options' 'ffmpegthumbs: video thumbnails' 'kdegraphics-thumbnailers: PDF and PS thumbnails'
            'konsole: terminal panel' 'purpose: share context menu')
groups=(kde-applications kde-system)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
    kdebug-241227.patch::"https://invent.kde.org/system/dolphin/commit/996e430b62075c5b69571f141456dbe5f2956679.patch"
    kdebug-401170.patch::"https://invent.kde.org/system/dolphin/commit/6f632283d022aaf9d8e2bda81a5409aa36242d4d.patch"
    kdebug-440663.patch::"https://invent.kde.org/system/dolphin/-/commit/5e84fffd6ed97a173d7250e7563998ea5dc395a0.patch"
    kdebug-441192.patch::"https://invent.kde.org/system/dolphin/commit/73082d2e42180d3795014738499109c839705622.patch"
)
sha256sums=('3b17bfd81dcb04d37810cea0d52d098ce5244289a2eb0173f575291ca97bd9a7'
            'SKIP'
            '16c7db777e068269e20e5c08e1f4509f3fd1c5faef6a996f253792b8adeaf51c'
            '4c7ea5abc5f17106c066e2cece0caed5471b5aab753bc76c47d7d53425ae830d'
            '7d8709d3e507223d41870539c37f8f9b67974d1edb69d7a401c4dcdd9d33db2b'
            '10559dfda7e9b41bb918c3dbb9314317b2e596db44043ff3fed24be7f2f4905a')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87 # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker

prepare() {
    msg "Add kdebug 241227 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-241227.patch
    msg "Add kdebug 401170 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-401170.patch
    msg "Add kdebug 440663 patch; fix in 21.12"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-440663.patch
    msg "Add kdebug 441192 patch; fix in 21.12"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-441192.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
