# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin
pkgver=21.04.3
pkgrel=1.3
pkgdesc="KDE File Manager"
arch=(x86_64)
url="https://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(baloo-widgets knewstuff kio-extras kcmutils kparts kinit kactivities kuserfeedback)
makedepends=(extra-cmake-modules kdoctools packagekit-qt5)
optdepends=('kde-cli-tools: for editing file type options' 'ffmpegthumbs: video thumbnails' 'kdegraphics-thumbnailers: PDF and PS thumbnails'
            'konsole: terminal panel' 'purpose: share context menu' 'packagekit-qt5: service menu installer')
groups=(kde-applications kde-system)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-421347.patch::"https://invent.kde.org/system/dolphin/commit/4f4e3d392c9645e3d43c362af72e2066430890f6.patch"
kdebug-269987.patch::"https://invent.kde.org/system/dolphin/commit/5a0da4a9c8d10dc1921077d84bdabf05d20150b0.patch"
kdebug-412902.patch::"https://invent.kde.org/system/dolphin/commit/6cec386192e58089b4572996a265c9696cdcb3c0.patch"
kdebug-241227.patch::"https://invent.kde.org/system/dolphin/commit/996e430b62075c5b69571f141456dbe5f2956679.patch"
kdebug-367583.patch::"https://invent.kde.org/system/dolphin/commit/6b64c7ff56e0025608d03f21e64f47431e23ef23.patch"
kdebug-437080.patch::"https://invent.kde.org/system/dolphin/commit/98e1fb66d054d188a7c07683e7e6768b84f63605.patch"
commit-152a7b4f.patch::"https://invent.kde.org/system/dolphin/-/commit/152a7b4fcbc06ab536083abe0a7f63c1722960fb.patch"
commit-aba4462e.patch::"https://invent.kde.org/system/dolphin/-/commit/aba4462e0238d6075e8822d56a78372eacfa7d2e.patch"
kdebug-433247.patch::"https://invent.kde.org/system/dolphin/commit/baaf0c29b857997720b9cb0d584a44306963fb97.patch"
kdebug-401170.patch::"https://invent.kde.org/system/dolphin/commit/6f632283d022aaf9d8e2bda81a5409aa36242d4d.patch"
)
sha256sums=('e3a1560c216efaa80c3ec99138c4657e9602e17aea250a418b18c2fc85295264'
            'SKIP'
            '2985c54722d7d047783f05eb07e4a09a544e90a6826d76eaea07605b3935d812'
            'db896784510a66cc8d21a8f5a247a76a733a15fb914aa7224448c69cfd86c9bb'
            '489892136437ce2b4594836d1b2743fad8355284f545320052d492781b752323'
            '16c7db777e068269e20e5c08e1f4509f3fd1c5faef6a996f253792b8adeaf51c'
            'd11ae5b310ca3d76d1df62c8f8bd09ca259a1ee386e82944ffb62c2f18d1c8b1'
            'e739750419de1adf1ce84edb1d34945cc15fc5ca0e9d5f11371f5342c3aa4c93'
            '148567c1bf79ae64d75eadb9f1d60bf27f18b32d1ee70f43ec410cf2a63cb4f8'
            '8a9efaae57276e33d52c9a442aedd94acbf0ad4e3a039b2acfc17d5f4680587b'
            '3ea08b6ee771941f3de3b342aa98759d7d70c9cd2f277ab4326e86d0b9dad31a'
            '4c7ea5abc5f17106c066e2cece0caed5471b5aab753bc76c47d7d53425ae830d')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87 # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker

prepare() {
    msg "Add kdebug 421347 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-421347.patch
    msg "Add kdebug 269986 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-269987.patch
    msg "Add kdebug 412902.patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-412902.patch
    msg "Add kdebug 241227 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-241227.patch
    msg "Add kdebug 367583 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-367583.patch
    msg "Add kdebug 437080 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-437080.patch
    msg "Add commit 152a7b4f patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../commit-152a7b4f.patch
    msg "Add commit aba4462e patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../commit-aba4462e.patch
    msg "Add kdebug 433247 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-433247.patch
    msg "Add kdebug 401170 patch; fix in 21.08"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-401170.patch


}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
