# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin
pkgver=20.04.2
pkgrel=1.1
pkgdesc="KDE File Manager"
arch=(x86_64)
url="https://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(baloo-widgets knewstuff kio-extras kcmutils kparts kinit phonon-qt5)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kde-cli-tools: for editing file type options' 'ffmpegthumbs: video thumbnails' 'kdegraphics-thumbnailers: PDF and PS thumbnails'
            'konsole: terminal panel' 'purpose: share context menu')
groups=(kde-applications kdebase)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-362834.patch::"https://cgit.kde.org/dolphin.git/patch/?id=5351db95c6460b568052f23399e0ea9b4a02d9b4"
D28370.patch::"https://cgit.kde.org/dolphin.git/patch/?id=4440e961a07aaee5b5da7c8967ff05de14808ad4"
kdebug-420512.patch::"https://cgit.kde.org/dolphin.git/patch/?id=b0ad83eeeed9f484bc034103d6e3df1893b034dd"
D26362.patch::"https://cgit.kde.org/dolphin.git/patch/?id=196f4553e68a89d0162685f2df9a32a58e3ff479"
kdebug-420340.patch::"https://cgit.kde.org/dolphin.git/patch/?id=dcd8c646a566eff1ff2c487fb2e7abe9a53a23cb"
kdebug-420354.patch::"https://cgit.kde.org/dolphin.git/patch/?id=e076d8668f673e09ddd01b3367e413a494d6d2bb"
kdebug-190580.patch::"https://cgit.kde.org/dolphin.git/patch/?id=e6ea3ab4c41dcc115143a237aafd3a1152849433"
kdebug-405226.patch::"https://cgit.kde.org/dolphin.git/patch/?id=1d72710039b547d91b7138d3899e783e26aac849"
kdebug-276167.patch::"https://cgit.kde.org/dolphin.git/patch/?id=a291c5999035bb17fe764c1910c3e78ba041f8ac"
kdebug-423326.patch::"https://invent.kde.org/system/dolphin/commit/37df39b93bf23b89ca760d4dd793788833d9a3e1.patch"
#kdebug-415281.patch::"https://invent.kde.org/system/dolphin/commit/c81927651a6f50af860b21d1be5c04bb4295b7cc.patch"
)
sha256sums=('37e4c1510227112365418cacccabc6d535fb0880e182a2341bfe00d585d14fb2'
            'SKIP'
            '0b520b799927b95ec489503aaacbb1082d34d1de66cf4ffb994eb8f13d03a163'
            'e15296867f47170d7d8c1553e92ceec4e0759f37bc09f82af808423090685409'
            '24602f89f1de84c32a8feed65aef012d5c948bcda0dff9687106077821e1a1a8'
            'dca31d4bd20fc39cb927e285fc00a6469e6cc20628ad7b54e56010e73710088d'
            '047ea771b3fd2f01d23759a0b79f863c99fdbf56007f98d184522ca545eb6097'
            '888ed0f1bd2ef12abc178749aaa42e6a5c9196e0ccd923187db42663b34200d0'
            '870e44a448c6123f139adc8339f467a3d77581f6a604bac7813860081694c6de'
            'b872c0c143a989bdde0c67cef36c80debf64cf3097c1780291116dc71ac1507c'
            'a734b048e9148ffc99cbba40a0c599225cc6661aa43afc1b0827c99ef4a23636'
            '19fbcf99e575d80fcee141ce4858e1e1e1b25df83d6708e084491001c7291fb7')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add kdebug 362834 patch; fix in 20.08.0"
  patch -p1 -i ../kdebug-362834.patch
  msg "Add D28370 patch; fix in 20.08.0"
  patch -p1 -i ../D28370.patch
  msg "Add kdebug 420512 patch"
  patch -p1 -i ../kdebug-420512.patch
  msg "Add D26362 patch; fix in 20.08.0"
  patch -p1 -i ../D26362.patch
  msg "Add kdebug 420340 patch; fix in 20.08.0"
  patch -p1 -i ../kdebug-420340.patch
  msg "Add kdebug 420354 patch; fix in 20.08.0"
  patch -p1 -i ../kdebug-420354.patch
  msg "Add kdebug 190580 and 158090 patches; fix in 20.08.0"
  patch -p1 -i ../kdebug-190580.patch
  msg "Add kdebug 405226 patch"
  patch -p1 -i ../kdebug-405226.patch
  msg "Add kdebug 276167 patch; fix in 20.08"
  patch -p1 -i ../kdebug-276167.patch
  msg "Add kdebug 423326 patch; fix in 20.04.3"
  patch -p1 -i ../kdebug-423326.patch
 # msg "Add kdebug 415281 patch; fix in 20.08"
 # patch -p1 -i ../kdebug-415281.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
