# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin
pkgver=20.04.2
pkgrel=1.1
pkgdesc="KDE File Manager"
arch=(x86_64)
url="https://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(baloo-widgets knewstuff kio-extras kcmutils kparts kinit phonon-qt5)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kde-cli-tools: for editing file type options' 'ffmpegthumbs: video thumbnails' 'kdegraphics-thumbnailers: PDF and PS thumbnails'
            'konsole: terminal panel' 'purpose: share context menu')
groups=(kde-applications kdebase)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-362834.patch::"https://invent.kde.org/system/dolphin/-/commit/5351db95c6460b568052f23399e0ea9b4a02d9b4.patch"
kdebug-28370.patch::"https://invent.kde.org/system/dolphin/-/commit/4440e961a07aaee5b5da7c8967ff05de14808ad4.patch"
kdebug-420512.patch::"https://invent.kde.org/system/dolphin/-/commit/b0ad83eeeed9f484bc034103d6e3df1893b034dd.patch"
kdebug-26362.patch::"https://invent.kde.org/system/dolphin/-/commit/196f4553e68a89d0162685f2df9a32a58e3ff479.patch"
kdebug-420340.patch::"https://invent.kde.org/system/dolphin/-/commit/dcd8c646a566eff1ff2c487fb2e7abe9a53a23cb.patch"
kdebug-420354.patch::"https://invent.kde.org/system/dolphin/-/commit/e076d8668f673e09ddd01b3367e413a494d6d2bb.patch"
kdebug-190580.patch::"https://invent.kde.org/system/dolphin/-/commit/e6ea3ab4c41dcc115143a237aafd3a1152849433.patch"
kdebug-405226.patch::"https://invent.kde.org/system/dolphin/-/commit/1d72710039b547d91b7138d3899e783e26aac849.patch"
kdebug-276167.patch::"https://invent.kde.org/system/dolphin/-/commit/a291c5999035bb17fe764c1910c3e78ba041f8ac.patch"
kdebug-423326.patch::"https://invent.kde.org/system/dolphin/-/commit/37df39b93bf23b89ca760d4dd793788833d9a3e1.patch"
#kdebug-415281.patch::"https://invent.kde.org/system/dolphin/commit/c81927651a6f50af860b21d1be5c04bb4295b7cc.patch"
)
sha256sums=('37e4c1510227112365418cacccabc6d535fb0880e182a2341bfe00d585d14fb2'
            'SKIP'
            'e58d7dc34c58c5359dd684141e7cafd5bb9b32825563bf32fc9385990c205c8c'
            '047c183dbe11ff502ea60a01c544a360a1a7b4d1c5e4d0b1547cea1af8be6265'
            '3c97c39f9925366f6e835dfed18936cbc5ec19fc105d9043fb84f4018062e469'
            '16126890679ec0a2b63af5d37e7a58a827ca556f0af5e9fc47741cf374064c1b'
            '868d20b54c4975b0ff634fcbb0fb7812b786af469f419c40c7d9b90d4fc505cc'
            '7ee9a44abcb8ad02dd769669263741af5e2cbe79b5e8a7d815d67f052b7c12fd'
            'a926bb0d5ab852fe950e400d01dfc579151fa7c064f71f412f2e677848d461b2'
            '84723027c5829c409df3778617a0c2fed57e4e13277d22853d7a420e01badeb4'
            '4848b7108d7cd7c372247f94e967bef6f9dfbe8e54fba64983a8ee0b081c0273'
            '19fbcf99e575d80fcee141ce4858e1e1e1b25df83d6708e084491001c7291fb7')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add kdebug 362834 patch; fix in 20.08.0"
  patch -p1 -i ../kdebug-362834.patch
  msg "Add D28370 patch; fix in 20.08.0"
  patch -p1 -i ../kdebug-28370.patch
  msg "Add kdebug 420512 patch"
  patch -p1 -i ../kdebug-420512.patch
  msg "Add D26362 patch; fix in 20.08.0"
  patch -p1 -i ../kdebug-26362.patch
  msg "Add kdebug 420340 patch; fix in 20.08.0"
  patch -p1 -i ../kdebug-420340.patch
  msg "Add kdebug 420354 patch; fix in 20.08.0"
  patch -p1 -i ../kdebug-420354.patch
  msg "Add kdebug 190580 and 158090 patches; fix in 20.08.0"
  patch -p1 -i ../kdebug-190580.patch
  msg "Add kdebug 405226 patch"
  patch -p1 -i ../kdebug-405226.patch
  msg "Add kdebug 276167 patch; fix in 20.08"
  patch -p1 -i ../kdebug-276167.patch
  msg "Add kdebug 423326 patch; fix in 20.04.3"
  patch -p1 -i ../kdebug-423326.patch
 # msg "Add kdebug 415281 patch; fix in 20.08"
 # patch -p1 -i ../kdebug-415281.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
