# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin
pkgver=20.04.0
pkgrel=1.3
pkgdesc="KDE File Manager"
arch=(x86_64)
url="https://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(baloo-widgets knewstuff kio-extras kcmutils kparts kinit phonon-qt5)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kde-cli-tools: for editing file type options' 'ffmpegthumbs: video thumbnails' 'kdegraphics-thumbnailers: PDF and PS thumbnails'
            'konsole: terminal panel' 'purpose: share context menu')
groups=(kde-applications kdebase)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-362834.patch::"https://cgit.kde.org/dolphin.git/patch/?id=5351db95c6460b568052f23399e0ea9b4a02d9b4"
D28370.patch::"https://cgit.kde.org/dolphin.git/patch/?id=4440e961a07aaee5b5da7c8967ff05de14808ad4"
kdebug-420512.patch::"https://cgit.kde.org/dolphin.git/patch/?id=b0ad83eeeed9f484bc034103d6e3df1893b034dd"
D26362.patch::"https://cgit.kde.org/dolphin.git/patch/?id=196f4553e68a89d0162685f2df9a32a58e3ff479"
)
sha256sums=('3c5bb9dc0b5ba55c54eb1c13b123b34f3437c22424142cb7d1957b6ebe4ad5ba'
            'SKIP'
            '0b520b799927b95ec489503aaacbb1082d34d1de66cf4ffb994eb8f13d03a163'
            'e15296867f47170d7d8c1553e92ceec4e0759f37bc09f82af808423090685409'
            '24602f89f1de84c32a8feed65aef012d5c948bcda0dff9687106077821e1a1a8'
            'dca31d4bd20fc39cb927e285fc00a6469e6cc20628ad7b54e56010e73710088d')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add kdebug 362834 patch; fix in 20.08.0"
  patch -p1 -i ../kdebug-362834.patch
  msg "Add D28370 patch; fix in 20.08.0"
  patch -p1 -i ../D28370.patch
  msg "Add kdebug 420512 patch"
  patch -p1 -i ../kdebug-420512.patch
  msg "Add D26362 patch; fix in 20.08.0"
  patch -p1 -i ../D26362.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
