# Maintainer Nelson89 <daniel dot karasinski at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the orginal works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=gwenview
pkgver=20.12.2
pkgrel=1.1
pkgdesc="A fast and easy to use image viewer"
url='https://kde.org/applications/graphics/gwenview/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-graphics)
depends=(kactivities baloo libkipi libkdcraw kparts kitemmodels cfitsio phonon-qt5 purpose perl)
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
            'kimageformats: support for dds, xcf, exr, psd, and more image formats'
            'kipi-plugins: export to various online services'
            'kamera: import pictures from gphoto2 cameras')
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-414769.patch::"https://invent.kde.org/graphics/gwenview/commit/354e8e9967738437b67d59bd0348b753dc858448.patch"
kdebug-393314.patch::"https://invent.kde.org/graphics/gwenview/commit/cc09a2a7ad63a6ad19cbc759dbcf5eda050b66ee.patch"
kdebug-348746.patch::"https://invent.kde.org/graphics/gwenview/commit/275e9288c8b587078b3c4ff75633157a44197eba.patch"
)
sha256sums=('b6eca42c1c627fda083a8db32cbf785e1778e937af4b65a1d912d9b8ad942633'
            'SKIP'
            '7391b3c220fb28b7767f462e989106512f3707b96d6ff0dc296537f8b82baeaf'
            '001ec22608e025a0ba022d43220b54a3cd35eaec491abb351817c0093d087f8e'
            'a4a534b914d6c4e8014c8d286f5c346beb8d1d961cfaf100c7d42072e71c6403')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Added kdebug 414769 patch; fix in 21.04"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-414769.patch
    msg "Added kdebug 393314 patch; fix in 21.04"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-393314.patch
    msg "Added kdebug 348746 patch; fix in 21.04"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-348746.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
