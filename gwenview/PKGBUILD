# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=gwenview
pkgver=20.08.3
pkgrel=1.1
pkgdesc="A fast and easy to use image viewer"
url='https://kde.org/applications/graphics/gwenview/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-graphics)
depends=(kactivities baloo libkipi libkdcraw kparts kitemmodels cfitsio phonon-qt5 purpose perl)
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
            'kimageformats: support for dds, xcf, exr, psd, and more image formats'
            'kipi-plugins: export to various online services'
            'kamera: import pictures from gphoto2 cameras')
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-425479.patch::"https://invent.kde.org/graphics/gwenview/commit/f5cb19e887902599a31e0b8169a6dfd3a57069cf.patch"
kdebug-274901.patch::"https://invent.kde.org/graphics/gwenview/commit/02b3411c1c522b613ff0f8fd306518a5576d46d0.patch"
kdebug-427569.patch::"https://invent.kde.org/graphics/gwenview/commit/e737554c9faff3be27083497c26675155bcf8122.patch"
)
sha256sums=('acc76c7b80f71ce47fb747c81cd062b1581ccc441f025e023bedc193feb8bc26'
            'SKIP'
            '430f6b6227e522681584c210ce8eb3a41a6c8637d372e71b73016554d7ec1769'
            'c36144bf06451b73c20492f48ccbfba65600d279b63ef16b6cd57e421fce9b06'
            '1dd4cb8449bdbb2cf11d6aa75db37ae64144dd9284125a251712e50d461b270d')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Add kdebug 425479 patch; fix in 20.12"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-425479.patch
    msg "Add kdebug 274901 patch; fix probably in 20.12"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-274901.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
