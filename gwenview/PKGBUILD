# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=gwenview
pkgver=19.12.3
pkgrel=1.1
pkgdesc="A fast and easy to use image viewer"
url='https://kde.org/applications/graphics/gwenview/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(kactivities baloo libkipi libkdcraw kparts kitemmodels cfitsio phonon-qt5 purpose)
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
            'kimageformats: support for dds, xcf, exr, psd, and more image formats'
            'kipi-plugins: export to various online services'
            'kamera: import pictures from gphoto2 cameras')
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-414694.patch::"https://cgit.kde.org/gwenview.git/patch/?id=db6ca614a41eb880c2eee9d34101b17400dd5ca1"
port-deprected-signal.patch::"https://cgit.kde.org/gwenview.git/patch/?id=b1ae993cac2d5b3607a8752305aa590d701ed9cb")
sha256sums=('b453cd55b7409bf8e4446a1b714dc66e73a0376d2da65b184b82f786767164e7'
            'SKIP'
            '866f4b79891e7f6c79eea8759073da9ce8d21779cdd7e6a9ab8092174865b5bd'
            '52c98701193fa57660b0bdd3304f76b265039a33d600e54848fa44daa15ef610')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply kdebug-414694.patch"
  patch -p1 -i ../kdebug-414694.patch
  msg "Port deprected signal patch; for Qt5.15"
  patch -p1 -i ../port-deprected-signal.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
