# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with upcomming fixes
# made upon original work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=gwenview
pkgver=19.04.1
pkgrel=2.1
pkgdesc="A fast and easy to use image viewer"
url='https://kde.org/applications/graphics/gwenview/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(kactivities baloo libkipi libkdcraw kparts kitemmodels cfitsio phonon-qt5)
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
            'kimageformats: support for dds, xcf, exr, psd, and more image formats'
            'kipi-plugins: export to various online services'
            'kamera: import pictures from gphoto2 cameras')
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        gwenview-exiv2-0.27.1.patch::"https://cgit.kde.org/gwenview.git/patch/?id=172560b8"
        kdebug-331435.patch::"https://cgit.kde.org/gwenview.git/patch/?id=1ae75b39c952eb2a41ed6292a6ee27fc26666e29"
        correct-kdebug-331435.patch::"https://cgit.kde.org/gwenview.git/patch/?id=3a5cd96bfc92741e6fb6285b516934ffb76ae56f"
        kdebug-346510.patch::"https://cgit.kde.org/gwenview.git/patch/?id=a985ad14dc073653e996deeee7550dbb58653ce2")
sha256sums=('636498100284be86194d328c40ed70166cc96a5fc7665090e4a1ca9538b2f13c'
            'SKIP'
            'a564cc34544769bf4c25e9c7e5a70248b1c1af33e4d06df65badb70ab72acadf'
            'c5eceaabb86d7872e0fdb15b484b7716076dee746ad19416dc64f1f2dd13df0d'
            '97ca34b56115aa28bd9ac696f0b1d03ed5dbad1ab4e345bd4a0206fa428a8c56'
            'e27c890005a2e4a15c5cbf86d4d07745b5310c10e4159e432a8e7303320fb8c0')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../gwenview-exiv2-0.27.1.patch # Fix build with exiv2 0.27.1
  msg "Apply KDEBUG 331435 patch; fix in 19.08.0"
  patch -p1 -i ../kdebug-331435.patch
  patch -p1 -i ../correct-kdebug-331435.patch
  msg "Apply KDEBUG 346510 patch; fix in 19.08.0"
  patch -p1 -i ../kdebug-346510.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
    -DBUILD_TESTING=OFF
  make -j4
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
