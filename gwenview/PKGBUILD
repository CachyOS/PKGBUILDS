# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# especialy for CachyOS and only for version with bugfixes and improves from upcomming version
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=gwenview
pkgver=22.04.3
pkgrel=1.2
pkgdesc='A fast and easy to use image viewer'
url='https://apps.kde.org/gwenview/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-graphics)
depends=(kactivities baloo libkdcraw kparts kitemmodels cfitsio phonon-qt5 purpose perl kimageannotator)
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
            'kimageformats: support for dds, xcf, exr, psd, and more image formats'
            'kamera: import pictures from gphoto2 cameras')
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
kdebug-408551.patch::"https://invent.kde.org/graphics/gwenview/-/commit/8f3bf87bcda2f678a15a48a5cefdca912d80dff8.patch" #fix in 22.08
)
sha256sums=('8c793929f2ad3050d906e185b6753acffd9481dc619156844d681ac0ba57bc05'
            'SKIP'
            '094ae3187d2350a414b1fd77baa06afc288a4096bf2c17538173f55025e65273')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
#options=(debug)

prepare() {
cd $pkgname-$pkgver
### Patching sources
        local src
        for src in "${source[@]}"; do
          src="${src%%::*}"
          src="${src##*/}"
          [[ $src = *.patch ]] || continue
          echo "Applying patch $src..."
          patch -Np1 < "../$src"
        done
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
