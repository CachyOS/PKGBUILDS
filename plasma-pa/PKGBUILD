# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-pa
pkgver=5.18.2
pkgrel=1.1
pkgdesc='Plasma applet for audio volume management using PulseAudio'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-workspace libcanberra-pulse pulseaudio perl)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-417607.patch::"https://cgit.kde.org/plasma-pa.git/patch/?id=606c6499750a1d776d73b052eab828dc5eedca07"
kdebug-416331.patch::"https://cgit.kde.org/plasma-pa.git/patch/?id=552b0384ffd205a148df62239058cee9a4a6702e")
sha256sums=('350531cb0e37cdcf024632edeb2b973c206d880c062f33524b367eb550736418'
            'SKIP'
            '36e2d6ec93b9ea6e7cb609cddef6bd829f77f1ce60b1f14542d7e7cc48789b08'
            'c227e8e2f9402f67b37b7a08040e68407078cf81bb243699339255a5b805b36d')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Applying kdebug 416331 patch"
  patch -p1 -i ../kdebug-416331.patch
  msg "Applying kdebug 417607 patch; fix in 5.19.0"
  patch -p1 -i ../kdebug-417607.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
