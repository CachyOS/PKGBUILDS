# Maintainer: pavbaranov <pavbaranov @ gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-pa
pkgver=5.19.3
pkgrel=1.1
pkgdesc='Plasma applet for audio volume management using PulseAudio'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-workspace libcanberra-pulse pulseaudio perl)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-422612.patch::"https://invent.kde.org/plasma/plasma-pa/commit/6611ff9e89ecedbedb38c630ece042d57832f72c.patch"
kdebug-422537.patch::"https://invent.kde.org/plasma/plasma-pa/commit/3f2040d4e13381713b94317b017f42b9ab31b8de.patch")
sha256sums=('8cf420b64f2deb11c869367bc5362730ddc5bc3bffc52e47d511b0d86349ef2b'
            'SKIP'
            'acc95da4b4acce1c090e32cca2f13f65850b05a7c3d39fced7fe9ac54e42f171'
            '0ce5bb0b4fa381ee1170921f68454ea4227844c2f2b813a8fed7c69449969091')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
prepare() {
    msg "Add kdebug 422612 patch; fix in 5.20"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-422612.patch
    msg "Add kdebug 422537 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-422537.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
