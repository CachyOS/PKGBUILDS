# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version patched with bugfixes from upcomming editions
# made upon original works of:
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-pa
pkgver=5.15.1
pkgrel=1.1
pkgdesc='Plasma applet for audio volume management using PulseAudio'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-workspace libcanberra-pulse pulseaudio)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-397954.patch::"https://cgit.kde.org/plasma-pa.git/patch/?id=00026cee9ec8c5d04067cf892b46e5a5507b7f6d"
D19013.patch::"https://cgit.kde.org/plasma-pa.git/patch/?id=6e53d78136dff5f5bb395c2cc4ec93d2fbc0b1a0")
sha256sums=('206a7301e33e990a5efca379809c061961335e00fbfff252e0192f55a018b1dc'
            'SKIP'
            '6bb2e01c00e9dc87681746640b55620ed8f02d20f4d9ff92b692bb063a87ff93'
            '490b96dc69d90043c62b5a8b0ddfea50efeb86c3a087519086f65825ff4460a7')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply KDEBUG 397954 patch; fix in 5.16"
  patch -p1 -i ../kdebug-397954.patch
  msg "Apply D19013 patch; sense for those who use this plasmoid outside of the tray"
  patch -p1 -i ../D19013.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
