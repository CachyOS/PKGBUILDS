# Maintainer: Nelson89 <daniel dot karasinski at gmail dot com>
# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-pa
pkgver=5.22.5
pkgrel=1.1
pkgdesc='Plasma applet for audio volume management using PulseAudio'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-workspace pulseaudio perl)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
# Plasma 5.23 patches:
kdebug-398476.patch::"https://invent.kde.org/plasma/plasma-pa/commit/6bebee67110d48adcd2a46cd9a1ae8c31247e17b.patch"
)
sha256sums=('40ade6a4f7b19ae378d0db621f238f5c197d7a1a78379b5c92e9a1a88929b5ab'
            'SKIP'
            '4a8471693475be49a43be726cbc52a01fb42ff3170ee6f157f74d04417f27c2f')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
              
prepare() {
    msg "Add kdebug-398476 patch; fix in 5.23"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-398476.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
