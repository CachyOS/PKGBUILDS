# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with some patches
# I'd like to thanks demmm from KaOS team and aarnt from Octopi for this bugfix https://github.com/aarnt/octopi/issues/506
# Maintainer: MatMoul <matmoul@gmail.com>

#_pkggit=octopi
#_gitcommit=fd0307f0334ed7c2ff1baf24a2247ffd3595a35f
#_gitsha256='2cf985ecbff4db8039bbc6b0c27547bbad20867e1303008ee473861a033d6881'

pkgbase=octopi
pkgname='octopi'
pkgver=0.13.0
pkgrel=1.1
pkgdesc="This is Octopi, a powerful Pacman frontend using Qt libs"
arch=('x86_64')
url="https://tintaescura.com/projects/octopi/"
license=('GPL2')
depends=('alpm_octopi_utils' 'pkgfile' 'qtermwidget' 'sudo')
makedepends=('qt5-tools')
optdepends=('octopi-notifier-qt5: Notifier for Octopi using Qt5 libs'
            'octopi-notifier-frameworks: Notifier for Octopi with Knotifications support'
            'pacaur: for AUR support'
            'paru: for AUR support'
            'pikaur: for AUR support'
            'trizen: for AUR support'
            'yay: for AUR support'
            'pacmanlogviewer: to view pacman log files')
provides=('octopi' 'octopi-repoeditor' 'octopi-cachecleaner')
conflicts=('octopi-dev' 'octopi-git')
source=("octopi-${pkgver}-${pkgrel}.tar.gz::https://codeload.github.com/aarnt/octopi/tar.gz/refs/tags/v0.13.0"
#Fixes for https://github.com/aarnt/octopi/issues/506
"https://raw.githubusercontent.com/KaOSx/apps/master/octopi/mainwindow.diff"
"https://raw.githubusercontent.com/KaOSx/apps/master/octopi/qtsingleapplication.diff"
)
sha256sums=('3ee7f9fb22ccf72269576cc20aa488956d722ce0da1137977042b911e23b3bf4'
            '0717adb662fa0a6187f51d338ef5c9818c597f27246ba8a60b851bb07558ecaf'
            '8af93462eddbbc97b222839c47ca4f9c9de5c5a0912b5f760d44b45330b5de78')

_subdirs=(helper repoeditor cachecleaner sudo)

prepare() {
  cd "${pkgname}-${pkgver}"
  cp resources/images/octopi_green.png resources/images/octopi.png

  ### Patching sources
        local src
        for src in "${source[@]}"; do
          src="${src%%::*}"
          src="${src##*/}"
          [[ $src = *.diff ]] || continue
          echo "Applying patch $src..."
          patch -Np1 < "../$src"
        done

}

build() {
  cd "${pkgname}-${pkgver}"

  echo "Starting build..."
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS="${CFLAGS}" QMAKE_CXXFLAGS="${CXXFLAGS}" QMAKE_LFLAGS="${LDFLAGS}" octopi.pro
  make

  for _subdir in ${_subdirs[@]}; do
    pushd $_subdir
    echo "Building octopi-$_subdir..."
    qmake-qt5 PREFIX=/usr QMAKE_CFLAGS="${CFLAGS}" QMAKE_CXXFLAGS="${CXXFLAGS}" QMAKE_LFLAGS="${LDFLAGS}" "octopi-$_subdir.pro"
    make
    popd
  done
}

package() {
  cd "${pkgname}-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
  
  for _subdir in ${_subdirs[@]}; do
    pushd $_subdir
    make INSTALL_ROOT="${pkgdir}" install
    popd
  done
}
