# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcomming releases
# made on the original PKGBUILD by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kget
pkgver=17.12.2
pkgrel=1.1
pkgdesc='Download Manager'
url='https://kde.org/applications/internet/kget/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdenetwork)
depends=(kcmutils knotifyconfig qca-qt5 qgpgme kdelibs4support hicolor-icon-theme)
makedepends=(extra-cmake-modules boost libktorrent kdoctools kdesignerplugin)
optdepends=('libktorrent: torrent support')
conflicts=(kdenetwork-kget)
replaces=(kdenetwork-kget)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-390612.patch::"https://cgit.kde.org/kget.git/patch/?id=ed694ff5f5c8418a93d59bae6b68e11b9816c1f3")
sha256sums=('c9158149689473d82a45aeab309b1fe3bf64dd3b401c6adf87919ed7fbf7a5ba'
            'SKIP'
            'b32cdf6f24fb9f9957f7dbbba45c0a13b4207aa92f72ebae72701a339b740307')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

	cd $pkgname-$pkgver
	msg "Add KDEBUG-390612 patch" # See: https://bugs.kde.org/show_bug.cgi?id=390612 Fixed in 17.12.3
	patch -p1 -i ../kdebug-390612.patch
}

build() {
  cd build
  cmake ../kget-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
