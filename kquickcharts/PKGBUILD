# Maintainer: pavbaranov <pavbaranov @ gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kquickcharts
pkgver=5.71.0
pkgrel=1.1
pkgdesc='A QtQuick plugin providing high-performance charts'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-quickcontrols2)
makedepends=(extra-cmake-modules)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-423164.patch::"https://invent.kde.org/frameworks/kquickcharts/-/commit/3385823262fae172c5e7ab75429048b1ea132de7.patch")
sha256sums=('a1befe13903676a9779030b02b91da9889540e689e1f6a0afd54ff484109642a'
            'SKIP'
            'd7ec19332d5f9456eba64e0df0a6428c26ab80e280f4a6ed42bfa5a11751ad0c')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 423164 patch"
    patch -d $pkgname-$pkgver -p1 -i $srcdir/kdebug-423164.patch
    }
    
build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
