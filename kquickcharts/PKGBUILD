# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kquickcharts
pkgver=5.74.0
pkgrel=1.1
pkgdesc='A QtQuick plugin providing high-performance charts'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-quickcontrols2)
makedepends=(extra-cmake-modules)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-424636.patch::"https://invent.kde.org/frameworks/kquickcharts/commit/b06031fd5439715f7396471f8e8555a5e7bd4edf.patch")
sha256sums=('ee8eeb869fc365d96f37246750c131130255dd12ef6791d40b8ea3e971f6ed83'
            'SKIP'
            '37749e75e16e43af22090501c768615dfc92f7a8449335751837a8b76d124ce1')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
    msg "Add kdebug 424636 patch; fix probably in 5.75"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-424636.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
