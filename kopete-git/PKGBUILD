# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgname=kopete-git
pkgver=17.08.3.309.gdf8093801
pkgrel=1
pkgdesc='The KDE Instant Manager (git version based on the KF5)'
url='https://kde.org/applications/internet/kget/'
arch=(x86_64)
license=(GPL LGPL FDL)
#groups=(kde-applications kdenetwork)
depends=(kcmutils khtml knotifyconfig kdelibs4support kidentitymanagement kcontacts kdnssd qca-qt5 libgadu jasper libotr v4l-utils)
makedepends=(extra-cmake-modules git kdoctools boost kdesignerplugin 
libkleo)
optdepends=()
conflicts=(kopete kdenetwork-kopete)
replaces=(kopete kdenetwork-kopete)
source=(git://anongit.kde.org/kopete.git)
sha256sums=('SKIP')

pkgver() {
    cd kopete
    git describe --long --tags | sed 's/^v//;s/-/./g'
    }

prepare() {
    mkdir -p build
    }

build() {
    cd build
    cmake ../kopete \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt/plugins \
        -DCMAKE_SKIP_RPATH=ON \
        -DWITH_jabber=ON \
        -DWITH_skype=ON
        
    make
    }

package() {
    cd build
    make DESTDIR="$pkgdir" install
    }
